!===============================================================================================
!	콘센트 21
!===============================================================================================

!	콘센트 유형
!-----------------------------------------------------------------------------------------------
values{2}	"iSocketType"	SOCKET_MOUNTED,		stSocketType[SOCKET_MOUNTED],
							SOCKET_PROJECTED,	stSocketType[SOCKET_PROJECTED]

if	iSocketType = SOCKET_MOUNTED	then
	lock	"boxThk"
endif

if	iSocketType = SOCKET_PROJECTED	then
	lock	"socketThk"
endif

!	콘센트 유형
!-----------------------------------------------------------------------------------------------
values{2}	"iSocketContent"	SOCKET_CONTENT_ELEC,			stSocketContent[SOCKET_CONTENT_ELEC],
								SOCKET_CONTENT_ELEC_SHUTDOWN,	stSocketContent[SOCKET_CONTENT_ELEC_SHUTDOWN],
								SOCKET_CONTENT_LAN,				stSocketContent[SOCKET_CONTENT_LAN],
								SOCKET_CONTENT_LAN_MOSAIC,		stSocketContent[SOCKET_CONTENT_LAN_MOSAIC]

	!	수구 개수
	if	iSocketContent = SOCKET_CONTENT_ELEC	then
		values	"nSocket"	1, 2, 4
	endif

	if	iSocketContent = SOCKET_CONTENT_ELEC_SHUTDOWN	then
		values	"nSocket"	1, 3
	endif

	if	iSocketContent = SOCKET_CONTENT_LAN	then
		values	"nSocket"	1, 2, 3, 4, 5, 6
	endif

	if	iSocketContent = SOCKET_CONTENT_LAN_MOSAIC	then
		values	"nSocket"	4
	endif

!	콘센트 사이즈
lock	"socketWdt"
if	iSocketContent = SOCKET_CONTENT_ELEC | iSocketContent = SOCKET_CONTENT_ELEC_SHUTDOWN	then
	if	nSocket - 2 < eps	then
		parameters	socketWdt = 0.071
	else
		parameters	socketWdt = 0.118
	endif
endif

if	iSocketContent = SOCKET_CONTENT_LAN	then
	parameters	socketWdt = 0.070
endif

if	iSocketContent = SOCKET_CONTENT_LAN_MOSAIC	then
	parameters	socketWdt = 0.100
endif

lock	"socketHgt"
if	iSocketContent = SOCKET_CONTENT_ELEC | iSocketContent = SOCKET_CONTENT_ELEC_SHUTDOWN	then
	parameters	socketHgt = 0.121
endif

if	iSocketContent = SOCKET_CONTENT_LAN	then
	parameters	socketHgt = 0.120
endif

if	iSocketContent = SOCKET_CONTENT_LAN_MOSAIC	then
	parameters	socketHgt = 0.100
endif

values	"socketThk"	range	[0.010,		0.050)
values	"boxThk"	range	[0.0295,	0.100)

!	A, B, ZZYZX
!-----------------------------------------------------------------------------------------------
parameters	A = socketWdt,
			ZZYZX = socketHgt

if	iSocketType = SOCKET_MOUNTED	then
	parameters	B = socketThk
endif

if	iSocketType = SOCKET_PROJECTED	then
	parameters	B = boxThk
endif

lock	"A", "B", "ZZYZX"

!	평면 표기
!-----------------------------------------------------------------------------------------------
values{2}	"iPlanview"	PLANVIEW_KOR,		stPlanview[PLANVIEW_KOR],
						PLANVIEW_ARCHICAD,	stPlanview[PLANVIEW_ARCHICAD],
						PLANVIEW_MVO,		stPlanview[PLANVIEW_MVO]

!	폰트 유형
!-----------------------------------------------------------------------------------------------
dim	_fontNames[]
r = request("FONTNAMES_LIST", "", _fontNames)
values	"stFontType"	_fontNames

!	LH표준화 라이브러리
!-----------------------------------------------------------------------------------------------
if	iPlanview = PLANVIEW_MVO	then
	success = LIBRARYGLOBAL("MEP_m_MVO.gsm",	"stFontType",	_stFontType)

	if	success > eps	then
		lock			"stFontType", "fontSize"
		hideparameter	"stFontType", "fontSize"
	endif
endif