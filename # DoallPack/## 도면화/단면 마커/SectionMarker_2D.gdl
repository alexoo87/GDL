
!===============================================================================================
!	기본설정
!===============================================================================================
eps = 0.0001



!===============================================================================================
!	심볼 헤드
!===============================================================================================

	!	심볼 헤드 변수 정의
	!-----------------------------------------------------------------------------------------------
	if	not(SYMB_MIRRORED)	then
		iMarkerHead = iFirstMarkerHead
	else
		iMarkerHead = iSecondMarkerHead
	endif

	!	스케일 연동
	MarkerSize = MarkerSize*GLOB_SCALE
	MarkerWidth = MarkerWidth*GLOB_SCALE
	MarkerHeight = MarkerHeight*GLOB_SCALE
	
	!	심볼 헤드 그리기
	!-----------------------------------------------------------------------------------------------
	
		!	Circle
		!-----------------------------------------------------------------------------------------------
		if	iMarkerHead = Marker_Circle	then

			add2	-MarkerSize/2,	0

				!	원
				!-----------------------------------------------------------------------------------------------
				fill	65
	
				put	MarkerSize/2,	0,		1,
					0,				0,		900+1,
					MarkerSize/2,	180,	4000+1,
					-MarkerSize/2,	0,		1,
					0,				0,		900+1,
					MarkerSize/2,	180,	4000+1
	
				poly2_B	nsp/3,	1+2,	AC_MarkerFillPen,	AC_MarkerFillPenBg,	get(nsp)
	
				!	삼각형
				!-----------------------------------------------------------------------------------------------
				fill	AC_MarkerFill
	
				put	-MarkerSize/(2*cos(45)),	0,						1,
					0,							MarkerSize/(2*cos(45)),	1,
					MarkerSize/(2*cos(45)),		0,						1,
					-MarkerSize/(2*cos(45)),	0,						-1,
					MarkerSize/2,				0,						1,
					0,							0,						900+1,
					MarkerSize/2,				180,					4000+1,
					MarkerSize/2,				0,						1
	
				poly2_B	nsp/3,	1+2+4,	AC_MarkerFillPen,	AC_MarkerFillPenBg,	get(nsp)
	
					!	hotspot
					!-----------------------------------------------------------------------------------------------
					hotspot2	0,	0
					hotspot2	-MarkerSize/2,	0
	
			del 1
	
			!	중간라인
			!-----------------------------------------------------------------------------------------------
			line2	0,	0,	-MarkerSize,	0
	
		endif
		
		!	Flag
		!-----------------------------------------------------------------------------------------------
		if	iMarkerHead = Marker_Flag	then

			fill	AC_MarkerFill
	
			!	깃발
			!-----------------------------------------------------------------------------------------------
			put	0,				0,				1,
				-MarkerWidth,	0,				1,
				-MarkerWidth,	MarkerHeight,	1,
				0,				0,				1
	
			poly2_B	nsp/3,	1+2+4,	AC_MarkerFillPen,	AC_MarkerFillPenBg,	get(nsp)
	
				!	hotspot
				!-----------------------------------------------------------------------------------------------
				hotspot2	0,				0
				hotspot2	-MarkerWidth,	0
				hotspot2	-MarkerWidth,	MarkerHeight
				
		endif

!=====================================================================
! 텍스트
!=====================================================================
	!	텍스트 정의
	!-----------------------------------------------------------------------------------------------
	AnchorPoint_1 = 5

	define style	"TextStyle_1"	AC_TextFont_1,	AC_TextSize_1,	AnchorPoint_1,	AC_TextStyle_1

	!	변수 정의
	!-----------------------------------------------------------------------------------------------
	r = request ("View_Rotangle", "", angleViewRot)
	bRotAngle = (angleViewRot + SYMB_ROTANGLE) MOD 360

	rotAngTmp = 0
	if	(bRotAngle > 90 & bRotAngle < 270)	then
		rotAngTmp = 180
	endif


	!	텍스트 배치
	!-----------------------------------------------------------------------------------------------
	style	"TextStyle_1"

	if	iMarkerHead = Marker_Circle	then

		!	뷰번호
		!-----------------------------------------------------------------------------------------------
		add2	-MarkerSize/2,	(MarkerSize/4)*0.75
			rot2	rotAngTmp
				text2	0,	0,	AC_MarkerText_1
			del 1
		del 1

		!	도면번호
		!-----------------------------------------------------------------------------------------------
		add2	-MarkerSize/2,	-(MarkerSize/4)
			rot2	rotAngTmp
				text2	0,	0,	AC_MarkerText_2
			del 1
		del 1

	endif









