
!=======================================================================================
!	Paragraph / textblock 정리
!=======================================================================================
!	텍스트 스타일 정의
!---------------------------------------------------------------------------------------
define style{2}	"sb_style_01"	LABEL_FONT_NAME,	LABEL_TEXT_SIZE,	LABEL_FONT_STYLE

	r = request("Height_of_style",	"sb_style_01",	_textHgt,	_descent,	_leading)
	adjustY = LABEL_TEXT_SIZE/2 + _descent + _leading
	adjustY = adjustY/1000*GLOB_SCALE

!	텍스트 블럭 정의
!---------------------------------------------------------------------------------------
	!	ID
	paragraph	"ID_P" LABEL_TEXT_ALIGN, 0.75, 0, 0.75, LABEL_TEXT_LEADING, 10
		pen		LABEL_TEXT_PEN
		style	"sb_style_01"
		GLOB_ID
	endparagraph
	
	textblock	"TB_1"	0,	8,	0,	LABEL_TEXT_WIDTH_FACT,	LABEL_TEXT_CHARSPACE_FACT,	1,	"ID_P"

	!	치수
	paragraph	"COL_P"LABEL_TEXT_ALIGN, 0.75, 0, 0.75, LABEL_TEXT_LEADING, 10
		pen		LABEL_TEXT_PEN
		style	"sb_style_01"
		STR("%~.mm", COLU_DIM1)
		'X'
		STR("%~.mm", COLU_DIM2)
	endparagraph
	
	textblock	"TB_2"	0,	8,	0,	LABEL_TEXT_WIDTH_FACT,	LABEL_TEXT_CHARSPACE_FACT,	1,	"COL_P"

	paragraph	"BEAM_P" LABEL_TEXT_ALIGN, 0.75, 0, 0.75, LABEL_TEXT_LEADING, 10
		pen		LABEL_TEXT_PEN
		style	"sb_style_01"
		STR("%~.mm", BEAM_THICKNESS)
		'X'
		STR("%~.mm", BEAM_HEIGHT)
	endparagraph

	textblock	"TB_3"	0,	8,	0,	LABEL_TEXT_WIDTH_FACT,	LABEL_TEXT_CHARSPACE_FACT,	1,	"BEAM_P"

	!	Custom 문자
	paragraph	"CUSTOM_P" LABEL_TEXT_ALIGN, 0.75, 0, 0.75, LABEL_TEXT_LEADING, 10
		pen		LABEL_TEXT_PEN
		style	"sb_style_01"
		LB_custom
	endparagraph

	textblock	"TB_4"	0,	8,	0,	LABEL_TEXT_WIDTH_FACT,	LABEL_TEXT_CHARSPACE_FACT,	1,	"CUSTOM_P"

!	텍스트 블럭 정보 추출
!---------------------------------------------------------------------------------------
r = request("TEXTBLOCK_INFO",	"TB_1",	TB_1_width,	TB_1_height)
r = request("TEXTBLOCK_INFO",	"TB_2",	TB_2_width,	TB_2_height)
r = request("TEXTBLOCK_INFO",	"TB_3",	TB_3_width,	TB_3_height)
r = request("TEXTBLOCK_INFO",	"TB_4",	TB_4_width,	TB_4_height)

TB_1_width = TB_1_width/1000
TB_2_width = TB_2_width/1000
TB_3_width = TB_3_width/1000
TB_4_width = TB_4_width/1000

TB_1_height = TB_1_height/1000
TB_2_height = TB_2_height/1000
TB_3_height = TB_3_height/1000
TB_4_height = TB_4_height/1000


!	텍스트 크기 자동 맞춤
!---------------------------------------------------------------------------------------
if	bAutoSize | iSymbType = Symb_None	then

	!	변수정리
	!---------------------------------------------------------------------------------------
	if	iLabelType = Label_ID	then
		TBwidthTmp = TB_1_width
		TBheightTmp = TB_1_height
	endif

	if	iLabelType = Label_Size	then
		if	GLOB_ELEM_TYPE = 6	then	!	기둥
			TBwidthTmp = TB_2_width
			TBheightTmp = TB_2_height
		endif
		if	GLOB_ELEM_TYPE = 12	then	!	보
			TBwidthTmp = TB_3_width
			TBheightTmp = TB_3_height
		endif
	endif

	if	iLabelType = Label_Custom	then
		TBwidthTmp = TB_4_width
		TBheightTmp = TB_4_height
	endif

	!	Symbol Size
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_DIAMOND | iSymbType = SYMB_ELLIPSE	then	! 마름모형, 타원형
		if	iSymbType = SYMB_DIAMOND	then
			LabelWidth = TBwidthTmp*1.75
			LabelHeight = TBheightTmp*2
		else
			LabelWidth = TBwidthTmp*1.1
			LabelHeight = TBheightTmp*1.4
		endif
	else
		LabelWidth = TBwidthTmp
		LabelHeight = TBheightTmp
	endif

endif


!=======================================================================================
!	공통
!=======================================================================================
eps = 0.0001

!	라벨 길이 스케일에 맞추기
LabelWidth = LabelWidth*GLOB_SCALE
LabelHeight = LabelHeight*GLOB_SCALE

D1 = LabelWidth
D2 = LabelHeight
R1 = LabelWidth/2
R2 = LabelHeight/2

if	bLabelOffset	then
	LabelOffsetLth = LabelOffsetLth/1000
	LabelOffsetLth = LabelOffsetLth*GLOB_SCALE
	offsetH = offsetH*GLOB_SCALE
endif

!	지시선 위치
posLeaderLine[1] = posLeaderLine[1]*GLOB_SCALE
posLeaderLine[2] = posLeaderLine[2]*GLOB_SCALE



!=======================================================================================
!	라벨 위치 / 오프셋 및 객체에 수평
!=======================================================================================
x1 = LABEL_POSITION[1][1]
y1 = LABEL_POSITION[1][2]

x2 = LABEL_POSITION[2][1]
y2 = LABEL_POSITION[2][2]

x3 = LABEL_POSITION[3][1]
y3 = LABEL_POSITION[3][2]

r = request ("View_Rotangle", "", angleViewRot)
totalRotate_1 = (SYMB_ROTANGLE + angleViewRot) MOD 360
totalRotate_2 = (LABEL_ROTANGLE + LABEL_ASSOC_ELEM_ORIENTATION + angleViewRot) MOD 360
totalRotate_3 = (LABEL_ASSOC_ELEM_ORIENTATION + angleViewRot) MOD 360
nTrans = 0

! text2	0,	1,	totalRotate_2
! text2	1,	1,	"total"
! text2	0,	2,	LABEL_ASSOC_ELEM_ORIENTATION
! text2	1,	2,	"LABEL"
! text2	0,	3,	angleViewRot
! text2	1,	3,	"angle"
! text2	0,	4,	LABEL_ROTANGLE

!	라벨 위치
!---------------------------------------------------------------------------------------
add2	x2 + x3,	y2 + y3

hotspot2	0,	0

!	View 각도에 따른 조정
!---------------------------------------------------------------------------------------
if	not(bLabelOffset)	then
	if	GLOB_ELEM_TYPE = 6 | GLOB_ELEM_TYPE = 7	then
		rot2	-angleViewRot

		if	angleViewRot > 90 + eps & angleViewRot < 270 + eps	then
			rot2	180
		endif
	endif

	if	GLOB_ELEM_TYPE = 1 | GLOB_ELEM_TYPE = 5 | GLOB_ELEM_TYPE = 12	then
		rot2	LABEL_ASSOC_ELEM_ORIENTATION

		if	angleViewRot > 180 - eps & angleViewRot < 270 + eps	then
			rot2	180
		endif
	endif
endif

	!	객체 타입별 변수
	!---------------------------------------------------------------------------------------
	totalRotateTmp = 0
	rotAngleTmp = 0
	labelHalfTmp = 0
	yOffsetTmp1 = 0
	yOffsetTmp2 = 0
	yOffsetTmp3 = 0

		if	iSymbType = SYMB_CIRCLE | iSymbType = SYMB_PENTAGON | iSymbType = SYMB_HEXAGON	then
			if	iSymbType = SYMB_CIRCLE	then
				labelHalfTmp = D1/2
			endif
			if	iSymbType = SYMB_PENTAGON	then
				labelHalfTmp = R1
			endif
			if	iSymbType = SYMB_HEXAGON	then
				labelHalfTmp = R1*sin(60)
			endif
		else
			labelHalfTmp = D2/2
		endif


	if	GLOB_ELEM_TYPE = 1	then	! 오브젝트(철골보 라이브러리)
		totalRotateTmp = totalRotate_1
		rotAngleTmp = SYMB_ROTANGLE
		yOffsetTmp1 = labelHalfTmp
		yOffsetTmp2 = yOffsetTmp1 + LabelOffsetLth
		yOffsetTmp3 = -yOffsetTmp1 + LabelOffsetLth
	endif

	if	GLOB_ELEM_TYPE = 5	then	! 벽
		totalRotateTmp = totalRotate_3
		rotAngleTmp = LABEL_ASSOC_ELEM_ORIENTATION
		yOffsetTmp1 = labelHalfTmp + WALL_THICKNESS/2
		yOffsetTmp2 = yOffsetTmp1 + LabelOffsetLth
		yOffsetTmp3 = -yOffsetTmp1 + LabelOffsetLth
	endif

	if	GLOB_ELEM_TYPE = 6	then	! 기둥
		totalRotateTmp = totalRotate_3
		rotAngleTmp = LABEL_ASSOC_ELEM_ORIENTATION
		yOffsetTmp1 = labelHalfTmp + COLU_DIM2/2
		yOffsetTmp2 = yOffsetTmp1 + LabelOffsetLth
		yOffsetTmp3 = -yOffsetTmp1 + LabelOffsetLth
	endif

	if	GLOB_ELEM_TYPE = 7	then	! 슬라브
		totalRotateTmp = totalRotate_3
		rotAngleTmp = LABEL_ASSOC_ELEM_ORIENTATION
		yOffsetTmp1 = 0
		yOffsetTmp2 = 0
		yOffsetTmp3 = 0
	endif

	if	GLOB_ELEM_TYPE = 12	then	! 보
		totalRotateTmp = totalRotate_3
		rotAngleTmp = LABEL_ASSOC_ELEM_ORIENTATION
		yOffsetTmp1 = labelHalfTmp + BEAM_THICKNESS/2
			if	totalRotateTmp <= 90 or totalRotateTmp > 270	then
				yOffsetTmp2 = yOffsetTmp1 + LabelOffsetLth + BEAM_REFLINE_OFFSET
				yOffsetTmp3 = -yOffsetTmp1 + LabelOffsetLth + BEAM_REFLINE_OFFSET
			else
				yOffsetTmp2 = yOffsetTmp1 + LabelOffsetLth - BEAM_REFLINE_OFFSET
				yOffsetTmp3 = -yOffsetTmp1 + LabelOffsetLth - BEAM_REFLINE_OFFSET
			endif
	endif


	!	객체에 수평(오프셋 On)
	!---------------------------------------------------------------------------------------
	if	bLabelOffset	then
		!	각도
		rot2	rotAngleTmp - LABEL_ROTANGLE
		if	totalRotateTmp > 90 + eps & totalRotateTmp < 270 + eps	then
			rot2	180
		endif
		if	angleViewRot > 90 + eps & angleViewRot < 270 + eps	then
			rot2	180
		endif

		!	오프셋
		if	LabelOffsetLth >= 0	then
			add2	offsetH,	yOffsetTmp2
			hotspot2	0,	-(LabelOffsetLth + labelHalfTmp)

			add2	0,	-labelHalfTmp
				hotspot2	0,			0,	unID,	offsetH,	1+1024	:	unID = unID + 1
				hotspot2	-1,			0,	unID,	offsetH,	3		:	unID = unID + 1
				hotspot2	offsetH,	0,	unID,	offsetH,	2		:	unID = unID + 1
			del 1
		else
			add2	offsetH,	yOffsetTmp3
			hotspot2	0,	-(LabelOffsetLth - labelHalfTmp)

			add2	0,	labelHalfTmp
				hotspot2	0,			0,	unID,	offsetH,	1+1024	:	unID = unID + 1
				hotspot2	-1,			0,	unID,	offsetH,	3		:	unID = unID + 1
				hotspot2	offsetH,	0,	unID,	offsetH,	2		:	unID = unID + 1
			del 1
		endif
	endif


!=======================================================================================
!	라벨 내용 (ID, 치수, CUSTOM)
!=======================================================================================

!	항상 읽기 가능
_nTrans = 0
if	totalRotate_2 > 90 + eps & totalRotate_2 < 270 + eps	then
	if	LABEL_ALWAYS_READABLE	then
		rot2	180	:	_nTrans = _nTrans + 1
	endif
endif
	add2	0,	-adjustY

			!	ID
			!---------------------------------------------------------------------------------------
			if	iLabelType = LABEL_ID	then
				richtext2	0,	0,	"TB_1"
			endif

			!	치수
			!---------------------------------------------------------------------------------------
			if	iLabelType = LABEL_SIZE then

				!	기둥
				!---------------------------------------------------------------------------------------
				if GLOB_ELEM_TYPE = 6 then
					richtext2	0,	0,	"TB_2"
				endif

				!	보
				!---------------------------------------------------------------------------------------
				if GLOB_ELEM_TYPE = 12 then
					richtext2	0,	0,	"TB_3"
				endif
			endif

			!	CUSTOM
			!---------------------------------------------------------------------------------------
			if	iLabelType = LABEL_CUSTOM	then
				richtext2	0,	0,	"TB_4"
			endif

	del 1
if	_nTrans > eps	then
	del _nTrans
endif


!=======================================================================================
!	심볼유형에 따라 심볼그리기
!=======================================================================================
unID = 100

pen			penSymb
fill		fillSymb
line_type	ltSymb

	!	None
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_NONE	then

		put	-R1,	-R2,	1,
			R1,		-R2,	1,
			R1,		R2,		1,
			-R1,	R2,		1,
			-R1,	-R2,	1

		poly2_B	nsp/3,	2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

		!	hotspot
		hotspot2	-R1,	-R2
		hotspot2	R1,		-R2
		hotspot2	R1,		R2
		hotspot2	-R1,	R2
		hotspot2	0,		R2
		hotspot2	0,		-R2

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R2
		_xPosLeader_180 = R1
		_xPosLeader_270 = R2

		gosub	"LeaderLine"
	endif

	!	사각형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_RECT	then

		put	-R1,	-R2,	1,
			R1,		-R2,	1,
			R1,		R2,		1,
			-R1,	R2,		1,
			-R1,	-R2,	1

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

		!	hotspot
		hotspot2	-R1,	-R2
		hotspot2	R1,		-R2
		hotspot2	R1,		R2
		hotspot2	-R1,	R2
		hotspot2	0,		R2
		hotspot2	0,		-R2

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R2
		_xPosLeader_180 = R1
		_xPosLeader_270 = R2

		gosub	"LeaderLine"
	endif

	!	마름모형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_DIAMOND	then

		put	0,		-R2,	1,
			R1,		0,		1,
			0,		R2,		1,
			-R1,	0,		1,
			0,		-R2,	1

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

		!	hotspot
		hotspot2	0,		-R2
		hotspot2	R1,		0
		hotspot2	0,		R2
		hotspot2	-R1,	0

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R2
		_xPosLeader_180 = R1
		_xPosLeader_270 = R2

		gosub	"LeaderLine"
	endif

	!	원형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_CIRCLE	then

		put	0,	0,		900+1,
			R1,	360,	4000+1

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

		!	hotspot
		hotspot2	0,		0
		hotspot2	R1,		0
		hotspot2	0,		R1
		hotspot2	-R1,	0
		hotspot2	0,		-R1

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R1
		_xPosLeader_180 = R1
		_xPosLeader_270 = R1

		gosub	"LeaderLine"
	endif

	!	타원형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_ELLIPSE	then

		_ratio = (LabelHeight/LabelWidth)

		mul2	1,	_ratio

			put	0,	0,		900+1,
				R1,	360,	4000+1

			poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

			!	hotspot
			hotspot2	0,		0
			hotspot2	R1,		0
			hotspot2	0,		R1
			hotspot2	-R1,	0
			hotspot2	0,		-R1

		del	1

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R1*_ratio
		_xPosLeader_180 = R1
		_xPosLeader_270 = R1*_ratio

		gosub	"LeaderLine"
	endif

	!	운동장형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_STADIUM	then

		put	-(R1 - R2),	-R2,	1,
			R1 - R2,	-R2,	1,

			R1 - R2,	0,		900,
			0,			180,	4000+1,

			-(R1 - R2),	R2,		1,

			-(R1 - R2),	0,		900,
			0,			180,	4000+1

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	get(nsp)

		!	hotspot
		hotspot2	-(R1 - R2),	-R2
		hotspot2	R1 - R2,	-R2
		hotspot2	R1,			0
		hotspot2	R1 - R2,	R2
		hotspot2	-(R1 - R2),	R2
		hotspot2	-R1,		0
		hotspot2	0,			R2
		hotspot2	0,			-R2

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R2
		_xPosLeader_180 = R1
		_xPosLeader_270 = R2

		gosub	"LeaderLine"
	endif

	!	오각형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_PENTAGON	then
		_nVertex = 5
		_aInner = (360/_nVertex)

		for	i = 1	to	_nVertex + 1
			_aVertex = 90 + _aInner*(i - 1)
			put	R1*cos(_aVertex),	R1*sin(_aVertex),	1
		next	i

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	use(nsp)

		!	hotspot
		for	h = 1	to	_nVertex + 1
			hotspot2	get(2) + (use(1) - get(1))
		next	h

		!	지시선
		_xPosLeader_0 = R1*cos(18) - (R1*sin(18))*tan(18)
		_xPosLeader_90 = R1
		_xPosLeader_180 = R1*cos(18) - (R1*sin(18))*tan(18)
		_xPosLeader_270 = R1*cos(36)

		gosub	"LeaderLine"
	endif


	!	육각형
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_HEXAGON	then
		_nVertex = 6
		_aInner = (360/_nVertex)

		for	i = 1	to	_nVertex + 1
			_aVertex = _aInner*(i - 1)
			put	R1*cos(_aVertex),	R1*sin(_aVertex),	1
		next	i

		poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	use(nsp)

		!	hotspot
		for	h = 1	to	_nVertex + 1
			hotspot2	get(2) + (use(1) - get(1))
		next	h

		!	지시선
		_xPosLeader_0 = R1
		_xPosLeader_90 = R1*sin(_aInner)
		_xPosLeader_180 = R1
		_xPosLeader_270 = R1*sin(_aInner)

		gosub	"LeaderLine"
	endif

	!	플래그(벽체안내도)
	!---------------------------------------------------------------------------------------
	if	iSymbType = SYMB_FLAG	then
		_mulx = 1
		if	bMirrorFlag	then	_mulx = -1

		mul2	_mulx,	1

			put	-R1,		R2,		1,
				R1,			R2,		1,
				R1 + R2,	0,		1,
				R1,			-R2,	1,
				-R1,		-R2,	1,
				-R1,		R2,		1

			poly2_B	nsp/3,	1+2,	penSymb,	LABEL_TEXT_BG_PEN,	use(nsp)

			!	hotspot
			for	h = 1	to	(nsp/3)
				hotspot2	get(2) + (use(1) - get(1))
			next	h
		del 1

		!	지시선
		_xPosLeader_0 = R1 + R2
		_xPosLeader_90 = R2
		_xPosLeader_180 = R1
		_xPosLeader_270 = R2

		if	bMirrorFlag	then
			_xPosLeader_0 = R1
			_xPosLeader_180 = R1 + R2
		endif

		gosub	"LeaderLine"
	endif



!=======================================================================================
!	데크슬라브 철근배근 방향 표시
!=======================================================================================

	!	변수정의
	!---------------------------------------------------------------------------------------
	pen	penSymb

	ArrowHead = 0.050
	offsetBottom = offsetBottom*GLOB_SCALE
	lengthBottom = lengthBottom*GLOB_SCALE
	halfBottom = halfBottom*GLOB_SCALE
	offsetRight = offsetRight*GLOB_SCALE
	lengthRight = lengthRight*GLOB_SCALE
	halfRight = halfRight*GLOB_SCALE

	!	하단
	!---------------------------------------------------------------------------------------
	if	iDeckSlabSymb = DeckSlab_Bottom	then
		add2	0,	-offsetBottom
			put	-lengthBottom/2 + (ArrowHead*cos(20)),	ArrowHead*sin(20),	1,
				-lengthBottom/2,						0,					1,
				lengthBottom/2,							0,					1,
				lengthBottom/2 - (ArrowHead*cos(20)),	-ArrowHead*sin(20),	1

			poly2_	nsp/3,	1,	get(nsp)
		del 1

			!	hotspot
			!---------------------------------------------------------------------------------------
			hotspot2	0,	0,				unID,	offsetBottom,	1+128+1024	:	unID = unID + 1
			hotspot2	0,	1,				unID,	offsetBottom,	3			:	unID = unID + 1
			hotspot2	0,	-offsetBottom,	unID,	offsetBottom,	2			:	unID = unID + 1

			add2	0,	-offsetBottom
				hotspot2	0,			0,	unID,	halfBottom,	1+128+1024,	lengthBottom	:	unID = unID + 1
				hotspot2	-1,			0,	unID,	halfBottom,	3,			lengthBottom	:	unID = unID + 1
				hotspot2	halfBottom,	0,	unID,	halfBottom,	2,			lengthBottom	:	unID = unID + 1
			del 1

	endif

	!	우측
	!---------------------------------------------------------------------------------------
	if	iDeckSlabSymb = DeckSlab_Right	then
		add2	offsetRight,	0
			put	(ArrowHead*sin(20)),	lengthRight/2 - (ArrowHead*cos(20)),	1,
				0,						lengthRight/2,							1,
				0,						-lengthRight/2,							1,
				-(ArrowHead*sin(20)),	-lengthRight/2 + (ArrowHead*cos(20)),	1

			poly2_	nsp/3,	1,	get(nsp)
		del 1

			!	hotspot
			!---------------------------------------------------------------------------------------
			hotspot2	0,				0,	unID,	offsetRight,	1+128+1024	:	unID = unID + 1
			hotspot2	-1,				0,	unID,	offsetRight,	3			:	unID = unID + 1
			hotspot2	offsetRight,	0,	unID,	offsetRight,	2			:	unID = unID + 1

			add2	offsetRight,	0
				hotspot2	0,	0,			unID,	halfRight,	1+128+1024,	lengthRight	:	unID = unID + 1
				hotspot2	0,	-1,			unID,	halfRight,	3,			lengthRight	:	unID = unID + 1
				hotspot2	0,	halfRight,	unID,	halfRight,	2,			lengthRight	:	unID = unID + 1
			del 1
	endif




!=======================================================================================
!=======================================================================================
end
!=======================================================================================
!=======================================================================================




!=======================================================================================
!	Sub-routine
!=======================================================================================

!---------------------------------------------------------------------------------------
"LeaderLine":	!	지시선
!---------------------------------------------------------------------------------------
	if	bLeaderLine	then
		for	i = 1	to	nLeaderLine
			if	i = 1	then
				_hspLth = 0.100
				hotspot2	_hspLth,					0,							unID,	aLeaderLine,	4+128	:	unID = unID + 1
				hotspot2	_hspLth*cos(aLeaderLine),	_hspLth*sin(aLeaderLine),	unID,	aLeaderLine,	5		:	unID = unID + 1
				hotspot2	0,							0,							unID,	aLeaderLine,	6		:	unID = unID + 1

				!	지시선
				pen		penLeader
				fill	64

				rot2	aLeaderLine
					!	좌표이동
					if	abs(aLeaderLine - 0) < eps	then	add2	_xPosLeader_0,		0
					if	abs(aLeaderLine - 90) < eps	then	add2	_xPosLeader_90,		0
					if	abs(aLeaderLine - 180) < eps	then	add2	_xPosLeader_180,	0
					if	abs(aLeaderLine - 270) < eps	then	add2	_xPosLeader_270,	0

						!	지시선 위치
						hotspot2	0,	0,					unID,	posLeaderLine[i],	1+128+1024	:	unID = unID + 1
						hotspot2	0,	-1,					unID,	posLeaderLine[i],	3			:	unID = unID + 1
						hotspot2	0,	posLeaderLine[i],	unID,	posLeaderLine[i],	2			:	unID = unID + 1

						add2	0,	posLeaderLine[i]
							!	메인
							hotspot2	0,				0,	unID,	LineLth_Main,	1+128	:	unID = unID + 1
							hotspot2	-1,				0,	unID,	LineLth_Main,	3		:	unID = unID + 1
							hotspot2	LineLth_Main,	0,	unID,	LineLth_Main,	2		:	unID = unID + 1

							line2		0,	0,	LineLth_Main,	0
							hotline2	0,	0,	LineLth_Main,	0,	unID	:	unID = unID + 1

							if	abs(aLineLth - 0) < eps | abs(aLineLth - 180) < eps	then
								add2	LineLth_Main,	0
									gosub	"drawCircle"
									if	GLOB_FEEDBACK_MODE	then
										line2		0,	-10,	0,	10
										hotline2	0,	-10,	0,	10,	unID	:	unID = unID + 1
									endif
								del 1
							endif

							!	보조
							add2	LineLth_Main,	0
								hotspot2	_hspLth,				0,						unID,	aLineLth,	4+128	:	unID = unID + 1
								hotspot2	_hspLth*cos(aLineLth),	_hspLth*sin(aLineLth),	unID,	aLineLth,	5		:	unID = unID + 1
								hotspot2	0,						0,						unID,	aLineLth,	6		:	unID = unID + 1

								if	abs(aLineLth - 0) > eps	then
									rot2	aLineLth
										hotspot2	0,				0,	unID,	LineLth_Sub,	1+128	:	unID = unID + 1
										hotspot2	-1,				0,	unID,	LineLth_Sub,	3		:	unID = unID + 1
										hotspot2	LineLth_Sub,	0,	unID,	LineLth_Sub,	2		:	unID = unID + 1

										line2		0,	0,	LineLth_Sub,	0
										hotline2	0,	0,	LineLth_Sub,	0,	unID	:	unID = unID + 1

										add2	LineLth_Sub,	0
											gosub	"drawCircle"
										del 1
									del 1
								endif
							del 1

							!	좌측 보조선
							gosub	"leftLine"

							!	우측 보조선
							gosub	"rightLine"

						del 1
					del 1
				del 1
			endif

			if	i = 2	then
				_hspLth = 0.100
				hotspot2	_hspLth,					0,							unID,	aLeaderLine_2,	4+128	:	unID = unID + 1
				hotspot2	_hspLth*cos(aLeaderLine_2),	_hspLth*sin(aLeaderLine_2),	unID,	aLeaderLine_2,	5		:	unID = unID + 1
				hotspot2	0,							0,							unID,	aLeaderLine_2,	6		:	unID = unID + 1

				!	지시선
				pen		penLeader
				fill	64

				rot2	aLeaderLine_2
					!	좌표이동
					if	abs(aLeaderLine_2 - 0) < eps	then	add2	_xPosLeader_0,		0
					if	abs(aLeaderLine_2 - 90) < eps	then	add2	_xPosLeader_90,		0
					if	abs(aLeaderLine_2 - 180) < eps	then	add2	_xPosLeader_180,	0
					if	abs(aLeaderLine_2 - 270) < eps	then	add2	_xPosLeader_270,	0

						!	지시선 위치
						hotspot2	0,	0,					unID,	posLeaderLine[i],	1+128+1024	:	unID = unID + 1
						hotspot2	0,	-1,					unID,	posLeaderLine[i],	3			:	unID = unID + 1
						hotspot2	0,	posLeaderLine[i],	unID,	posLeaderLine[i],	2			:	unID = unID + 1

						add2	0,	posLeaderLine[i]
							!	메인
							hotspot2	0,				0,	unID,	LineLth_Main_2,	1+128	:	unID = unID + 1
							hotspot2	-1,				0,	unID,	LineLth_Main_2,	3		:	unID = unID + 1
							hotspot2	LineLth_Main_2,	0,	unID,	LineLth_Main_2,	2		:	unID = unID + 1

							line2		0,	0,	LineLth_Main_2,	0
							hotline2	0,	0,	LineLth_Main_2,	0,	unID	:	unID = unID + 1

							if	abs(aLineLth_2 - 0) < eps | abs(aLineLth_2 - 180) < eps	then
								add2	LineLth_Main_2,	0
									gosub	"drawCircle"
									if	GLOB_FEEDBACK_MODE	then
										line2		0,	-10,	0,	10
										hotline2	0,	-10,	0,	10,	unID	:	unID = unID + 1
									endif
								del 1
							endif

							!	보조
							add2	LineLth_Main_2,	0
								hotspot2	_hspLth,					0,							unID,	aLineLth_2,	4+128	:	unID = unID + 1
								hotspot2	_hspLth*cos(aLineLth_2),	_hspLth*sin(aLineLth_2),	unID,	aLineLth_2,	5		:	unID = unID + 1
								hotspot2	0,							0,							unID,	aLineLth_2,	6		:	unID = unID + 1

								if	abs(aLineLth_2 - 0) > eps	then
									rot2	aLineLth_2
										hotspot2	0,				0,	unID,	LineLth_Sub_2,	1+128	:	unID = unID + 1
										hotspot2	-1,				0,	unID,	LineLth_Sub_2,	3		:	unID = unID + 1
										hotspot2	LineLth_Sub_2,	0,	unID,	LineLth_Sub_2,	2		:	unID = unID + 1

										line2		0,	0,	LineLth_Sub_2,	0
										hotline2	0,	0,	LineLth_Sub_2,	0,	unID	:	unID = unID + 1

										add2	LineLth_Sub_2,	0
											gosub	"drawCircle"
										del 1
									del 1
								endif
							del 1

							!	좌측 보조선
							gosub	"leftLine"

							!	우측 보조선
							gosub	"rightLine"

						del 1
					del 1
				del 1
			endif
		next	i

	endif

return

!---------------------------------------------------------------------------------------
"drawCircle":
!---------------------------------------------------------------------------------------
	put	rLeader,	0,		1,
		0,			0,		900,
		rLeader,	180,	4000+1,
		-rLeader,	0,		1,
		0,			0,		900,
		rLeader,	180,	4000+1

	poly2_B	nsp/3,	1+2,	penLeader,	0,	get(nsp)

return

!---------------------------------------------------------------------------------------
"leftLine":
!---------------------------------------------------------------------------------------
	_bLeftLine = (abs(bLeftLine[i] - 90) < eps)
	_hspOffset = 0.050

		!	좌측 보조선 위치
		add2	0,	_hspOffset
			hotspot2	0,				0,	unID,	leftLineLct[i],	1+128	:	unID = unID + 1
			hotspot2	-1,				0,	unID,	leftLineLct[i],	3		:	unID = unID + 1
			hotspot2	leftLineLct[i],	0,	unID,	leftLineLct[i],	2		:	unID = unID + 1
		del 1

		add2	leftLineLct[i],	0
			add2	0,	0.050
				hotspot2	_hspLth,					0,							unID,	bLeftLine[i],	4+128	:	unID = unID + 1
				hotspot2	_hspLth*cos(bLeftLine[i]),	_hspLth*sin(bLeftLine[i]),	unID,	bLeftLine[i],	5		:	unID = unID + 1
				hotspot2	0,							0,							unID,	bLeftLine[i],	6		:	unID = unID + 1
			del 1

			if	_bLeftLine	then
				!	메인
				hotspot2	0,	0,						unID,	leftLineLth_Main[i],	1+128	:	unID = unID + 1
				hotspot2	0,	-1,						unID,	leftLineLth_Main[i],	3		:	unID = unID + 1
				hotspot2	0,	leftLineLth_Main[i],	unID,	leftLineLth_Main[i],	2		:	unID = unID + 1

				line2		0,	0,	0,	leftLineLth_Main[i]
				hotline2	0,	0,	0,	leftLineLth_Main[i],	unID	:	unID = unID + 1

				if	abs(aLeftLineLth[i] - 0) < eps	then
					add2	0,	leftLineLth_Main[i]
						gosub	"drawCircle"
					del 1
				endif

				!	보조
				add2	0,	leftLineLth_Main[i]
					rot2	90
						hotspot2	_hspLth,						0,								unID,	aLeftLineLth[i],	4+128	:	unID = unID + 1
						hotspot2	_hspLth*cos(aLeftLineLth[i]),	_hspLth*sin(aLeftLineLth[i]),	unID,	aLeftLineLth[i],	5		:	unID = unID + 1
						hotspot2	0,								0,								unID,	aLeftLineLth[i],	6		:	unID = unID + 1

						if	abs(aLeftLineLth[i] - 0) > eps	then
							rot2	aLeftLineLth[i] - 90
								hotspot2	0,	0,					unID,	leftLineLth_Sub[i],	1+128	:	unID = unID + 1
								hotspot2	0,	-1,					unID,	leftLineLth_Sub[i],	3		:	unID = unID + 1
								hotspot2	0,	leftLineLth_Sub[i],	unID,	leftLineLth_Sub[i],	2		:	unID = unID + 1

								line2		0,	0,	0,	leftLineLth_Sub[i]
								hotline2	0,	0,	0,	leftLineLth_Sub[i],	unID	:	unID = unID + 1

								add2	0,	leftLineLth_Sub[i]
									gosub	"drawCircle"
								del 1
							del 1
						endif
					del 1
				del 1
			endif
		del 1

return

!---------------------------------------------------------------------------------------
"rightLine":
!---------------------------------------------------------------------------------------
	_bRightLine = (abs(bRightLine[i] - 90) < eps)
	_hspOffset = 0.050

		mul2	1,	-1
			!	좌측 보조선 위치
			add2	0,	_hspOffset
				hotspot2	0,					0,	unID,	rightLineLct[i],	1+128	:	unID = unID + 1
				hotspot2	-1,					0,	unID,	rightLineLct[i],	3		:	unID = unID + 1
				hotspot2	rightLineLct[i],	0,	unID,	rightLineLct[i],	2		:	unID = unID + 1
			del 1

			add2	rightLineLct[i],	0
				add2	0,	0.050
					hotspot2	_hspLth,					0,							unID,	bRightLine[i],	4+128	:	unID = unID + 1
					hotspot2	_hspLth*cos(bRightLine[i]),	_hspLth*sin(bRightLine[i]),	unID,	bRightLine[i],	5		:	unID = unID + 1
					hotspot2	0,							0,							unID,	bRightLine[i],	6+512	:	unID = unID + 1
				del 1

				if	_bRightLine	then
					!	메인
					hotspot2	0,	0,						unID,	rightLineLth_Main[i],	1+128	:	unID = unID + 1
					hotspot2	0,	-1,						unID,	rightLineLth_Main[i],	3		:	unID = unID + 1
					hotspot2	0,	rightLineLth_Main[i],	unID,	rightLineLth_Main[i],	2		:	unID = unID + 1

					line2		0,	0,	0,	rightLineLth_Main[i]
					hotline2	0,	0,	0,	rightLineLth_Main[i],	unID	:	unID = unID + 1

					if	abs(aRightLineLth[i] - 0) < eps	then
						add2	0,	rightLineLth_Main[i]
							gosub	"drawCircle"
						del 1
					endif

					!	보조
					add2	0,	rightLineLth_Main[i]
						rot2	90
							hotspot2	_hspLth,						0,								unID,	aRightLineLth[i],	4+128	:	unID = unID + 1
							hotspot2	_hspLth*cos(aRightLineLth[i]),	_hspLth*sin(aRightLineLth[i]),	unID,	aRightLineLth[i],	5		:	unID = unID + 1
							hotspot2	0,								0,								unID,	aRightLineLth[i],	6+512	:	unID = unID + 1

							if	abs(aRightLineLth[i] - 0) > eps	then
								rot2	aRightLineLth[i] - 90
									hotspot2	0,	0,						unID,	rightLineLth_Sub[i],	1+128	:	unID = unID + 1
									hotspot2	0,	-1,						unID,	rightLineLth_Sub[i],	3		:	unID = unID + 1
									hotspot2	0,	rightLineLth_Sub[i],	unID,	rightLineLth_Sub[i],	2		:	unID = unID + 1

									line2		0,	0,	0,	rightLineLth_Sub[i]
									hotline2	0,	0,	0,	rightLineLth_Sub[i],	unID	:	unID = unID + 1

									add2	0,	rightLineLth_Sub[i]
										gosub	"drawCircle"
									del 1
								del 1
							endif
						del 1
					del 1
				endif
			del 1
		del 1

return