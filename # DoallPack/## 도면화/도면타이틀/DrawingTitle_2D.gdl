
!===============================================================================================
!	기본설정
!===============================================================================================
unID = 100

	!	텍스트
	r = request("Height_of_style",	"drwIDsty",	_textHgt,	_descent,	_leading)
	_yPosText = (txtIDSize/1000)/2 + _descent + _leading
	_yPosText = _yPosText

!===============================================================================================
!	도면 / 타이틀 사이즈 추출
!===============================================================================================

!	도면 사이즈
!-----------------------------------------------------------------------------------------------
	drawingWidth = abs(AC_DrawingRect[1] - AC_DrawingRect[3])
	drawingHeight = abs(AC_DrawingRect[2] - AC_DrawingRect[4])


!	타이틀 사이즈
!-----------------------------------------------------------------------------------------------
	r = request("HomeDB_info", "", _layoutID, _layoutNum, _layoutName, _homeContext)

	if	iDrwName = DRW_NAME_LAYOUT	then
		_stDrwName = _layoutName
	endif
	if	iDrwName = DRW_NAME_DRAWING	then
		_stDrwName = ac_drawingName
	endif
	if	iDrwName = DRW_NAME_CUSTOM	then
		_stDrwName = stCustomDrw
	endif

	!	도면명 텍스트 정의
	!-----------------------------------------------------------------------------------------------
	paragraph	"drwNamePrg"	1,	0,	0,	0,	1
			pen		textPen
			style	drwNsty
			_stDrwName
	endparagraph

	textblock	"drwN"	0,	9,	0,	txtDrwWF,	txtDrwCF,	0,	"drwNamePrg"

	r = request ("TEXTBLOCK_INFO", "drwN", drwNwdt, drwNhgt)				!	도면명 길이 / 높이 : drwNwdt / drwNhgt
	r = request	("Height_of_style",	"drwNsty",	drwNstyHgt)

	_txtLth = drwNwdt
	! _txtLth = stw(_stDrwName)
	if	minTxtLth > eps	then
		if	drwNwdt - minTxtLth < eps	then
			_txtLth = minTxtLth
		endif
	endif

	titleWidth = Rd*2 + RdOff + _txtLth + TxtOff								!	타이틀 너비

!===============================================================================================
!	타이틀 기준 전체 이동(회전)
!===============================================================================================
add2	(drawingWidth - titleWidth)/2,	-drwNhgt

rot2	labelAngle

!===============================================================================================
!	2d hotspot
!===============================================================================================
unID = 100

!	도면외곽 핫스팟
!-----------------------------------------------------------------------------------------------
if	drawingWidth > titleWidth		then
	add2	-(drawingWidth - titleWidth)/2,	0
		hotspot2	0,	0
		hotspot2	drawingWidth ,	0
	del		1
endif

!	타이틀 핫스팟
!-----------------------------------------------------------------------------------------------
hotspot2	0,				0
hotspot2	titleWidth/2,	0
hotspot2	titleWidth,		0

!===============================================================================================
!	2d hotline & hotarc
!===============================================================================================
add2	Rd,	0
	hotarc2	0,	0,	Rd,	0,	360,	unID	:	unID = unID + 1
del 1

!===============================================================================================
!	타이틀 그리기
!===============================================================================================
fill	65

!-----------------------------------------------------------------------------------------------
!	도면명
!-----------------------------------------------------------------------------------------------

	richtext2	Rd*2 + RdOff + _txtLth,	0,	"drwN"
	
	add2	Rd*2 + RdOff,	0
		if	GLOB_PREVIEW_MODE = 0	then
			put	0,			0,			1,
				_txtLth,	0,			1,
				_txtLth,	drwNhgt,	1,
				0,			drwNhgt,	1,
				0,			0,			1

			poly2_B	nsp/3,	2,	0,	0,	get(nsp)
		endif
	del 1

!-----------------------------------------------------------------------------------------------
!	스케일
!-----------------------------------------------------------------------------------------------

!	Paragraph / Textblock
!-----------------------------------------------------------------------------------------------
	!	A3 Scale
	!-----------------------------------------------------------------------------------------------
		!	타입별 로컬변수 정의
		!-----------------------------------------------------------------------------------------------
		_stScaleA1 = ""
		_stScaleA3 = ""
		if	iTitleType = TITLE_TYPE1	then
			!	스케일
			_yPosScale_A1 = txtSclSize*0.2/1000
			ScaleNum_A3 = "1:"
			ScaleNum_A1 = "1:"

			ScaleTxt_A1 = "Scale A1: "	
			if	not(bScaleA1)	then
				_yPosScale_A3 = txtSclSize*0.2/1000
				ScaleTxt_A3 = "Scale A3: "
			else
				_yPosScale_A3 = txtSclSize*1.7/1000
				ScaleTxt_A3 = "A3: "
			endif
		endif

		if	iTitleType = TITLE_TYPE2	then
			_yPosScale_A3 = txtSclSize*0.2/1000
			ScaleNum_A3 = "1/"
			ScaleTxt_A3 = "축척 : "

			ScaleNum_A1 = "1/"
			ScaleTxt_A1 = "축척 : "

			_stScaleA1 = "축척 A1 : 1/"
			_stScaleA3 = "(A3 : 1/"
		endif

		paragraph	"drwScaleTxt_A3"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				ScaleTxt_A3
		endparagraph

		paragraph	"drwScaleNum_A3"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				ScaleNum_A3
				a3
		endparagraph

		paragraph	"drwScale_A1_A3"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				_stScaleA1
				a1
				" "
				_stScaleA3
				a3
				")"
		endparagraph

		textblock	"TB_drwScaleTxt_A3"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"drwScaleTxt_A3"
		textblock	"TB_drwScaleNum_A3"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"drwScaleNum_A3"
		textblock	"TB_drwScale_A1_A3"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"drwScale_A1_A3"

		r = request ("TEXTBLOCK_INFO", "TB_drwScaleNum_A3", drwScale_A3_Width, notUseData)

	!	A1 Scale
	!-----------------------------------------------------------------------------------------------
		paragraph	"drwScaleTxt_A1"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				ScaleTxt_A1
		endparagraph
	
		paragraph	"drwScaleNum_A1"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				ScaleNum_A1
				a1
		endparagraph

		textblock	"TB_drwScaleTxt_A1"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"drwScaleTxt_A1"
		textblock	"TB_drwScaleNum_A1"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"drwScaleNum_A1"
	
		r = request ("TEXTBLOCK_INFO", "TB_drwScaleNum_A1", drwScale_A1_Width, notUseData)

		maxScaleTextWidth = max(drwScale_A1_Width, drwScale_A3_Width)

	!	Custom Text for Scale
	!-----------------------------------------------------------------------------------------------
		paragraph	"p_CustomS"	1,	0,	0,	0,	1
				pen 	textPen
				style	drwSsty
				stCustomS
		endparagraph

		textblock	"tb_CustomS"	0,	3,	0,	txtSclWF,	txtSclCF,	0,	"p_CustomS"

		_yPosScale_Custom = txtSclSize*0.2/1000

!	텍스트 배치
!-----------------------------------------------------------------------------------------------
	if	bScale	then
		if	iContentS = CONTENT_S_SCALE	then
			if	iTitleType = TITLE_TYPE1	then
				if	not(bScaleA1)	then
					richtext2	Rd*2 + RdOff + _txtLth - drwScale_A3_Width,	-_yPosScale_A3,	"TB_drwScaleTxt_A3"
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A3,	"TB_drwScaleNum_A3"
				else
					richtext2	Rd*2 + RdOff + _txtLth - maxScaleTextWidth,	-_yPosScale_A1,	"TB_drwScaleTxt_A1"
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A1,	"TB_drwScaleNum_A1"
					
					richtext2	Rd*2 + RdOff + _txtLth - maxScaleTextWidth,	-_yPosScale_A3,	"TB_drwScaleTxt_A3"
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A3,	"TB_drwScaleNum_A3"
				endif
			endif

			if	iTitleType = TITLE_TYPE2	then
				if	iScaleType = SCALE_A1	then
					richtext2	Rd*2 + RdOff + _txtLth - drwScale_A3_Width,	-_yPosScale_A3,	"TB_drwScaleTxt_A1"
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A3,	"TB_drwScaleNum_A1"
				endif
				if	iScaleType = SCALE_A3	then
					richtext2	Rd*2 + RdOff + _txtLth - drwScale_A3_Width,	-_yPosScale_A3,	"TB_drwScaleTxt_A3"
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A3,	"TB_drwScaleNum_A3"
				endif
				if	iScaleType = SCALE_A1_A3	then
					richtext2	Rd*2 + RdOff + _txtLth,						-_yPosScale_A3,	"TB_drwScale_A1_A3"
				endif
			endif
		endif
		if	iContentS = CONTENT_S_CUSTOM	then
			richtext2	Rd*2 + RdOff + _txtLth,	-_yPosScale_Custom,	"tb_CustomS"
		endif
	endif


!-----------------------------------------------------------------------------------------------
!	도면번호
!-----------------------------------------------------------------------------------------------

_ratioText = 0.425

!	도면ID
!-----------------------------------------------------------------------------------------------
if	iContentT = CONTENT_T_DRWID	then
	_stDrwID = ac_drawingNumber
endif
if	iContentT = CONTENT_T_CUSTOM	then
	_stDrwID = stCustomT
endif

paragraph	"drwIDB"	1,	0,	0,	0,	1
		pen 	TextPen
		style	drwIDsty
		_stDrwID
endparagraph

textblock	"drwID"	0,	8,	0,	txtIDWF,	txtIDCF,	0,	"drwIDB"

if	iTitleType = TITLE_TYPE1	then
	richtext2	Rd,		Rd*_ratioText - _yPosText,	"drwID"

	!	도면번호(좌측하단)
	!-----------------------------------------------------------------------------------------------
	paragraph	"drwPrcB"	1,	0,	0,	0,	1
			pen 	TextPen
			style	drwIDsty
			txtPrc
	endparagraph

	textblock	"drwPrc"	0,	8,	0,	txtIDWF,	txtIDCF,	0,	"drwPrcB"

	richtext2	Rd/5*3,	-Rd*_ratioText - _yPosText,	"drwPrc"

	!	도면번호(우측하단)
	!-----------------------------------------------------------------------------------------------
	paragraph	"drwCstB"	1,	0,	0,	0,	1
			pen 	TextPen
			style	drwIDsty
			txtCst
	endparagraph

	textblock	"drwCst"	0,	8,	0,	txtIDWF,	txtIDCF,	0,	"drwCstB"

	richtext2	Rd + Rd/5*2,	-Rd*_ratioText - _yPosText,	"drwCst"
else
	if	iCircleType = CIRCLE_NORMAL	then
		richtext2	Rd,	-_yPosText,	"drwID"
	endif
	if	iCircleType = CIRCLE_HALF	then
		richtext2	Rd,	Rd*_ratioText - _yPosText,	"drwID"

		!	원 하부 내용
		!-----------------------------------------------------------------------------------------------
		if	iContentB = CONTENT_B_LAYOUT	then
			_stContent = _layoutNum
		endif
		if	iContentB = CONTENT_B_CUSTOM	then
			_stContent = stCustomB
		endif

		paragraph	"p_drwContentB"	1,	0,	0,	0,	1
			pen		TextPen
			style	drwIDsty
			_stContent
		endparagraph

		textblock	"tb_drwContentB"	0,	8,	0,	txtIDWF_B,	txtIDCF_B,	0,	"p_drwContentB"

		richtext2	Rd,	-Rd*_ratioText - _yPosText,	"tb_drwContentB"
	endif
endif


!-----------------------------------------------------------------------------------------------
!	선 그리기
!-----------------------------------------------------------------------------------------------
pen lblPen

	!	수평선
	!-----------------------------------------------------------------------------------------------
	if	iTitleType = TITLE_TYPE1	then
		put	0,									0,	1,
			Rd*2,								0,	1,
			Rd*2 + RdOff,						0,	1,
			Rd*2 + RdOff + _txtLth,				0,	1,
			Rd*2 + RdOff + _txtLth + TxtOff,	0,	1
	endif

	if	iTitleType = TITLE_TYPE2	then
		if	iCircleType = CIRCLE_HALF	then
			put	0,	0,	1
		endif

		put	Rd*2,								0,	1,
			Rd*2 + RdOff,						0,	1,
			Rd*2 + RdOff + _txtLth,				0,	1,
			Rd*2 + RdOff + _txtLth + TxtOff,	0,	1
	endif

	poly2_	nsp/3,	1,	use(nsp)

	!	hotline
	for	i = 1	to	(nsp/3) - 2
		hotline2	get(2) + (use(1) - get(1)),	use(2),	unID	:	unID = unID + 1
	next	i
		hotline2	get(2) + (use(1) - get(1)),	get(2) + (use(1) - get(1)),	unID	:	unID = unID + 1

	!	수직선
	!-----------------------------------------------------------------------------------------------
	if	iTitleType = TITLE_TYPE1	then
		add2	Rd,	0
			line2		0,	0,	0,	-Rd
			hotline2	0,	0,	0,	-Rd,	unID	:	unID = unID + 1
		del 1
	endif



!-----------------------------------------------------------------------------------------------
!	원 그리기
!-----------------------------------------------------------------------------------------------
fill	65
pen lblPen

put	Rd,	0,		900,
	Rd,	360,	4000+1

poly2_B	nsp/3,	1+2+4,	0,	0,	get(nsp)

hotarc2	Rd,	0,	Rd,	0,	360,	unID	:	unID = unID + 1
