!===============================================================================================
!	ㅁ형 철골 기둥
!===============================================================================================

!	재질 정보
!-----------------------------------------------------------------------------------------------
values{2}	"iMatInfo"	MAT_INFO_SS400,		stMatInfo[MAT_INFO_SS400],
						MAT_INFO_SM490,		stMatInfo[MAT_INFO_SM490],
						MAT_INFO_SM490A,	stMatInfo[MAT_INFO_SM490A],
						MAT_INFO_SHN400,	stMatInfo[MAT_INFO_SHN400],
						MAT_INFO_SHN490,	stMatInfo[MAT_INFO_SHN490],
						MAT_INFO_SN490,		stMatInfo[MAT_INFO_SN490]

!	호칭 치수
!-----------------------------------------------------------------------------------------------
_nDecimal = 0
_stFormat = "%." + str(_nDecimal,	1,	0) + "mm"
_stProfile = "ㅁ-" + str(_stFormat, colWdt_1) + "X" + str(_stFormat, colWdt_2) + "X" + str(_stFormat, colThk)

parameters	stProfile = _stProfile
lock	"stProfile"

!	철골기둥 관련
!-----------------------------------------------------------------------------------------------
values	"colWdt_1"	range	(0,	)
values	"colWdt_2"	range	(0,	)

_minColWdt = min(colWdt_1, colWdt_2)
values	"colThk"	range	(0,	_minColWdt/2)

values	"colHgt"	range	(0,	)

!	철골 기둥 실제 길이
!-----------------------------------------------------------------------------------------------
lock	"calcLth"

if	(abs(xTrans) < eps & abs(yTrans) < eps)	then
	_calcLth = colHgt
else
	_aLth = sqr(xTrans^2 + yTrans^2)
	_calcLth = sqr(_aLth^2 + colHgt^2)
endif

parameters	calcLth = _calcLth

!	단위 중량
!-----------------------------------------------------------------------------------------------
lock	"specWgt"

_specWgt = ((colWdt_1 + colWdt_2)*1000 - (3.287*colThk*1000))*0.0157*colThk*1000

parameters	specWgt = _specWgt

!	자르기 축
!-----------------------------------------------------------------------------------------------
values{2}	"iCutAxisT"	AXIS_X,	stCutAxis[AXIS_X],
						AXIS_Y,	stCutAxis[AXIS_Y]

values{2}	"iCutAxisB"	AXIS_X,	stCutAxis[AXIS_X],
						AXIS_Y,	stCutAxis[AXIS_Y]

_bCutOption = ((abs(xTrans) < eps) & (abs(yTrans) < eps))

if	not(_bCutOption)	then
	lock	"tTopPlane", "tBtmPlane"
endif

if	iCutAxisT = AXIS_X	then
	_range_Center = colWdt_1/2
endif
if	iCutAxisT = AXIS_Y	then
	_range_Center = colWdt_2/2
endif
values	"posCenterT"	range	[-_range_Center,	_range_Center]

values	"cutLthT_1"	range	(-(colHgt/2),	colHgt/2)
values	"cutLthT_2"	range	(-(colHgt/2),	colHgt/2)


if	iCutAxisB = AXIS_X	then
	_range_Center = colWdt_1/2
endif
if	iCutAxisB = AXIS_Y	then
	_range_Center = colWdt_2/2
endif
values	"posCenterB"	range	[-_range_Center,	_range_Center]

values	"cutLthB_1"	range	(-(colHgt/2),	colHgt/2)
values	"cutLthB_2"	range	(-(colHgt/2),	colHgt/2)

!	Floor Plan Display
!-----------------------------------------------------------------------------------------------
values{2}	"iDisplay"	DISPLAY_PROJECTED,		stDisplay[DISPLAY_PROJECTED],
						DISPLAY_WITHOVERHEAD,	stDisplay[DISPLAY_WITHOVERHEAD],
						DISPLAY_OUTLINES,		stDisplay[DISPLAY_OUTLINES],
						DISPLAY_OVERHEAD,		stDisplay[DISPLAY_OVERHEAD],
						DISPLAY_CUTPLANE,		stDisplay[DISPLAY_CUTPLANE]

if	not(iDisplay = DISPLAY_WITHOVERHEAD | iDisplay = DISPLAY_OVERHEAD)	then
	lock	"overPen", "overLT"
endif

!	Show Projection To
!-----------------------------------------------------------------------------------------------
values{2}	"iProjectTo"	PROJECTTO_FLOORPLAN,	stProjectTo[PROJECTTO_FLOORPLAN],
							PROJECTTO_ABSOLUTE,		stProjectTo[PROJECTTO_ABSOLUTE],
							PROJECTTO_ENTIRE,		stProjectTo[PROJECTTO_ENTIRE]