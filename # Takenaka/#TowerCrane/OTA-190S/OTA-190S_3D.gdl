!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

rotz	HM_WorkingAng
	!	Mast Basement
	!-----------------------------------------------------------------------------------------------
		rotz	45
			call	"HM_mc_OutriggerTower.gsm"	parameters	all	HM_OutrType = 1,	!	Cross
																HM_OutrWidth = 5.500,
																HM_OutrDepth = 5.500,
																HM_OutrHeight = 0.975,
																mBeamW = 0.300,
																mBeamFlangeT = 0.028,
																mBeamWebT = 0.018,
																bPlate = 0
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------
		_zPosMast = 1.250

		addz	_zPosMast
			!	Base Plate(Bottom)
			for	i = 1	to	4
				put	0.800,	0.800,	15,
					0.800,	0.300,	15,
					0.670,	0.300,	15,
					0.300,	0.670,	15,
					0.300,	0.800,	15

				prism_	nsp/3,	-0.275,		get(nsp)

				rotz	90
			next	i

			del 4

			!	Mast Paramaters
			_mastPlateWdt = 0.500
			_mastPlateThk = 0.048
			_mastColWdt = 0.200
			_mastColThk = 0.025
			_bInnerCol = 1

			_nMast1 = 2
			_nMast2 = 6

			_mastHgt1 = 4.500
			_mastHgt2 = 6.000

			_mastEntireHgt = _nMast1*_mastHgt1 + _nMast2*_mastHgt2

			!	Mast Type 1
			call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
														HM_MastNumber = _nMast1,
														HM_MastHeight = _mastHgt1,
														HM_PlateWdt = _mastPlateWdt,
														HM_PlateThk = _mastPlateThk,
														HM_ColWdt = _mastColWdt,
														HM_ColThk = _mastColThk,
														bInnerColumn = _bInnerCol

			addz	_nMast1*_mastHgt1
				call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
															HM_MastNumber = _nMast2,
															HM_MastHeight = _mastHgt2,
															HM_PlateWdt = _mastPlateWdt,
															HM_PlateThk = _mastPlateThk,
															HM_ColWdt = _mastColWdt,
															HM_ColThk = _mastColThk,
															bInnerColumn = _bInnerCol
			del 1
		del 1

	!	Upper Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		dim	_coorGR[][]
		_deckThk = 0.100

		!	1st
		_zPosDeck_1st = _zPosMast + _mastEntireHgt - 3.595

		addz	_zPosDeck_1st
			put	-1.310,	-1.700,	15,
				-1.660,	-1.350,	15,
				-1.660,	1.350,	15,
				-1.310,	1.700,	15,
				1.310,	1.700,	15,
				1.660,	1.350,	15,
				1.660,	-1.350,	15,
				1.310,	-1.700,	15,
				-1.310,	-1.700,	-1,
				!	Hole
				-1.125,	-1.250,	15,
				-1.125,	1.250,	15,
				1.125,	1.250,	15,
				1.125,	-1.250,	15,
				-1.125,	-1.250,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	0.050
				for	i = 1	to	2
					mul	(-2*i + 3),	(-2*i + 3),	1
						!	L, R
						_coorGR[1][1] = -0.650
						_coorGR[1][2] = -1.700

						_coorGR[2][1] = -1.310
						_coorGR[2][2] = -1.700

						_coorGR[3][1] = -1.660
						_coorGR[3][2] = -1.350

						_coorGR[4][1] = -1.660
						_coorGR[4][2] = 1.350

						_coorGR[5][1] = -1.310
						_coorGR[5][2] = 1.700

						_coorGR[6][1] = -0.650
						_coorGR[6][2] = 1.700

						call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 6,
																			HM_GRail_Pt = _coorGR,
																			HM_GRailType = 1,
																			HM_GRailRef = 2,
																			HM_GRailH = 1.000 + 0.025/2,
																			HM_GRailD = 0.025,
																			HM_GRailT = 0.025,
																			HM_GRailDist = 2.700/3,
																			HM_GRailDist_hori = 0.450,
																			HM_matGRail = HM_matBody2

						!	U, D
						_coorGR[1][1] = 0.590
						_coorGR[1][2] = -1.700

						_coorGR[2][1] = -0.590
						_coorGR[2][2] = -1.700

						call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																			HM_GRail_Pt = _coorGR,
																			HM_GRailType = 1,
																			HM_GRailRef = 2,
																			HM_GRailH = 1.000 + 0.025/2,
																			HM_GRailD = 0.025,
																			HM_GRailT = 0.025,
																			HM_GRailDist = 1.180/2,
																			HM_GRailDist_hori = 0.450,
																			HM_matGRail = HM_matBody2
					del 1
				next	i
			del 1
		del 1

		!	2nd
		_zPosDeck_2nd = _zPosDeck_1st + 2.639

		addz	_zPosDeck_2nd
			!	L
			put	-1.125,	-1.575,	15,
				-1.650,	-1.575,	15,
				-2.150,	-1.225,	15,
				-2.150,	1.225,	15,
				-1.650,	1.575,	15,
				-1.125,	1.575,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	R
			put	1.125,	-1.575,	15,
				1.125,	1.575,	15,
				1.310,	1.575,	15,
				1.660,	1.225,	15,
				1.660,	-1.225,	15,
				1.310,	-1.575,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	U, D
			addz	0.012
				for	i = 1	to	2
					muly	(-2*i + 3)
						put	-1.105,	-1.675,	15,
							-1.105,	-1.170,	15,
							1.105,	-1.170,	15,
							1.105,	-1.675,	15

						prism_	nsp/3,	_deckThk,	get(nsp)
					del 1
				next	i
			del 1

			!	Guard Rail
			addz	0.050
				!	L
				_coorGR[1][1] = -1.155
				_coorGR[1][2] = -1.575

				_coorGR[2][1] = -1.650
				_coorGR[2][2] = -1.575

				_coorGR[3][1] = -2.150
				_coorGR[3][2] = -1.225

				_coorGR[4][1] = -2.150
				_coorGR[4][2] = 1.225

				_coorGR[5][1] = -1.650
				_coorGR[5][2] = 1.575

				_coorGR[6][1] = -1.155
				_coorGR[6][2] = 1.575

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 6,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 1,
																	HM_GRailRef = 2,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 2.450/3,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2

				!	R
				_coorGR[1][1] = 1.155
				_coorGR[1][2] = 1.575

				_coorGR[2][1] = 1.310
				_coorGR[2][2] = 1.575

				_coorGR[3][1] = 1.660
				_coorGR[3][2] = 1.225

				_coorGR[4][1] = 1.660
				_coorGR[4][2] = -1.225

				_coorGR[5][1] = 1.310
				_coorGR[5][2] = -1.575

				_coorGR[6][1] = 1.155
				_coorGR[6][2] = -1.575

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 6,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 1,
																	HM_GRailRef = 2,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 2.450/3,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2

				!	U, D
				addz	0.012
					for	i = 1	to	2
						muly	(-2*i + 3)
							_coorGR[1][1] = 1.075
							_coorGR[1][2] = -1.675

							_coorGR[2][1] = -1.075
							_coorGR[2][2] = -1.675

							call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																				HM_GRail_Pt = _coorGR,
																				HM_GRailType = 1,
																				HM_GRailRef = 2,
																				HM_GRailH = 1.000 + 0.025/2,
																				HM_GRailD = 0.025,
																				HM_GRailT = 0.025,
																				HM_GRailDist = 2.150/3,
																				HM_GRailDist_hori = 0.450,
																				HM_matGRail = HM_matBody2
						del 1
					next	i
				del 1
			del 1
		del 1

	!	Connect to Main Object
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = _zPosMast + _mastEntireHgt

		!	Cylinder
		addz	_zPosConnect
			cylind	1.462,	1.315

			! addz	1.300
			! 	cylind	0.050,	1.085
			! del 1
		del 1

		!	Main Floor
		addz	_zPosConnect + 1.462
			put	-1.400,	-1.240,	15,
				-1.400,	1.240,	15,
				1.390,	1.240,	15,
				1.390,	-1.240,	15

			prism_	nsp/3,	0.800,	get(nsp)

			_aCut = atn(212/1385)
			addx	-1.400
				roty	_aCut
					cutplane	1,	1,	0,	1
				del 1
			del 1

				put	-2.785,	-1.240,	15,
					-2.785,	1.240,	15,
					-1.400,	1.240,	15,
					-1.400,	-1.240,	15

				prism_	nsp/3,	0.800,	get(nsp)

			cutend

			addz	0.212
				put	-2.950,	-1.240,	15,
					-2.950,	1.240,	15,
					-2.785,	1.240,	15,
					-2.785,	-1.240,	15

				prism_	nsp/3,	0.800 - 0.212,	get(nsp)
			del 1

		del 1

		!	H-Beam
		addz	_zPosConnect + 1.462 + 0.212
			add	-2.950,	1.151,	0
				rotz	180
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
																	HM_Beam_Width_EP = 0.200,
																	HM_Beam_Height_SP = 0.588,
																	HM_Beam_Height_EP = 0.588,
																	HM_Beam_Length = 2.515,
																	HM_Beam_FlangeT = 0.016,
																	HM_Beam_WebT = 0.012,
																	HM_matBeam = HM_matBody2
				del 1
			del 1

			add	-2.950,	-1.151,	0
				rotz	180
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
																	HM_Beam_Width_EP = 0.200,
																	HM_Beam_Height_SP = 0.588,
																	HM_Beam_Height_EP = 0.588,
																	HM_Beam_Length = 2.515,
																	HM_Beam_FlangeT = 0.016,
																	HM_Beam_WebT = 0.012,
																	HM_matBeam = HM_matBody2
				del 1
			del 1

			addy	-1.051
				addx	-3.100
					gosub	"draw_Connect_Beam_H"
				del 1
				addx	-4.405
					gosub	"draw_Connect_Beam_H"
				del 1
				addx	-5.315
					gosub	"draw_Connect_Beam_H"
				del 1
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		_deckThk = 0.200

		_zPosMainDeck = _zPosConnect + 2.162

		addz	_zPosMainDeck
			put	-3.535,	-3.400,	15,
				-4.765,	-3.400,	15,
				-5.215,	-2.811,	15,
				-5.465,	-2.276,	15,
				-5.465,	-1.950,	15,
				-5.570,	-1.950,	15,
				-5.784,	-1.160,	15,
				-5.884,	-0.430,	15,
				-5.884,	0.430,	15,
				-5.784,	1.160,	15,
				-5.570,	1.950,	15,
				1.250,	1.950,	15,
				1.780,	1.500,	15,
				1.780,	1.255,	15,
				1.850,	1.255,	15,
				1.850,	-1.255,	15,
				1.780,	-1.255,	15,
				1.780,	-1.695,	15,
				-3.535,	-1.695,	15,
				-3.535,	-3.400,	-1,
				!	Hole
				-5.465,	-1.251,	15,
				-5.465,	1.251,	15,
				1.390,	1.251,	15,
				1.390,	-1.251,	15,
				-5.465,	-1.251,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	0.050
				_coorGR[1][1] = -3.565
				_coorGR[1][2] = -3.400

				_coorGR[2][1] = -4.765
				_coorGR[2][2] = -3.400

				_coorGR[3][1] = -5.215
				_coorGR[3][2] = -2.811

				_coorGR[4][1] = -5.465
				_coorGR[4][2] = -2.276

				_coorGR[5][1] = -5.465
				_coorGR[5][2] = -1.950

				_coorGR[6][1] = -5.570
				_coorGR[6][2] = -1.950

				_coorGR[7][1] = -5.784
				_coorGR[7][2] = -1.160

				_coorGR[8][1] = -5.884
				_coorGR[8][2] = -0.430

				_coorGR[9][1] = -5.884
				_coorGR[9][2] = 0.430

				_coorGR[10][1] = -5.784
				_coorGR[10][2] = 1.160

				_coorGR[11][1] = -5.570
				_coorGR[11][2] = 1.950

				_coorGR[12][1] = 1.250
				_coorGR[12][2] = 1.950

				_coorGR[13][1] = 1.780
				_coorGR[13][2] = 1.500

				_coorGR[14][1] = 1.780
				_coorGR[14][2] = 1.255

				_coorGR[15][1] = 1.850
				_coorGR[15][2] = 1.255

				_coorGR[16][1] = 1.850
				_coorGR[16][2] = -1.255

				_coorGR[17][1] = 1.780
				_coorGR[17][2] = -1.255

				_coorGR[18][1] = 1.780
				_coorGR[18][2] = -1.665

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 18,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 1,
																	HM_GRailRef = 2,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 6.820/5,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Machinery
	!-----------------------------------------------------------------------------------------------
		_zPosMachine = _zPosConnect + 2.262

		addz	_zPosMachine
			!	U
			! add	-3.220,	-0.520,	0
			! 	block	0.640,	1.389,	0.227
			! del 1

			add	-4.750,	0,	0.630
				call	"HM_mc_Pulley.gsm"	parameters	all		HM_Pulley_Width = 0.820,
																HM_Pulley_R1 = 0.610,
																HM_Pulley_R2 = 0.375,
																HM_Pulley_Thick = 0.020,
																HM_Pulley_Fixed = 0,
																HM_matPulley = HM_matBody2
			del 1

			!	D
			! add	-2.580,	-0.010,	0
			! 	block	0.595,	0.520,	0.147
			! del 1

			add	-3.800,	0,	0.340
				call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 2.660,
															HM_Pulley_R1 = 0.250,
															HM_Pulley_R2 = 0.200,
															HM_Pulley_Thick = 0.025,
															HM_Pulley_Fixed = 0,
															HM_matPulley = HM_matBody2
			del 1
		del 1

	!	Operator Cap
	!-----------------------------------------------------------------------------------------------
		_zPosOperCap = _zPosConnect + 2.162

		addz	_zPosOperCap
			put	-3.515,	-3.375,	15,
				-3.515,	-1.725,	15,
				-2.750,	-1.725,	15,
				-2.750,	-3.375,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	0.050
				_coorGR[1][1] = -2.780
				_coorGR[1][2] = -3.375

				_coorGR[2][1] = -3.485
				_coorGR[2][2] = -3.375

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 1,
																	HM_GRailRef = 2,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 2.150/3,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1

			add	-2.750,	-3.375,	0.100
				call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Width = 1.650,
																HM_OP_Depth = 4.800,
																HM_OP_Depth_bot = 4.200,
																HM_OP_Height = 2.100
			del 1
		del 1

	!	Cat Head
	!-----------------------------------------------------------------------------------------------
		_zPosCatHead = _zPosConnect + 2.562

		addz	_zPosCatHead
			addx	-3.280
				call	"HM_mc_CatHead.gsm"	parameters	all	HM_CH_Type = 1,
															HM_CH_Width = 4.550,
															HM_CH_Depth = 2.500,
															HM_CH_Height = 9.900,
															HM_CH_Radius1 = 0.095,
															HM_CH_Radius2 = 0.160,
															HM_CH_Topnt_posx = -0.420,
															!	Deck1
															HM_CH_Deck = 1,
															HM_CH_DeckW = 1.650,
															HM_CH_DeckD = 3.500,
															HM_CH_DeckT = 0.150,
															HM_CH_Deck_HoleW = 1.250,
															HM_CH_Deck_HoleD = 2.800,
															HM_CH_Deck_posx = 0.950,
															HM_CH_Deck_posz = 9.000,
															!	Pulley
															HM_CH_Pulley_R1 = 0.200,
															HM_CH_Pulley_R2 = 0.180
			del 1
		del 1


	!	Jib
	!-----------------------------------------------------------------------------------------------
		_zPosJib = _zPosConnect + 2.462

		if	HM_Jib_flag	then
			addz	_zPosJib
				addx	-2.950
					!	Jib
					call	"HM_mc_Jib.gsm"	parameters	all	HM_Jib_type = 1

					roty	-HM_JibAng
						addx	HM_JibLength - _mJib_pulleyR
							!	Jib Pulley
							

							for	i = 1	to	2
								muly	-2*i + 3
									addy	0.850
										add	-0.400,	0,	0.550
											call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.183,
																						HM_Pulley_R1 = 0.200,
																						HM_Pulley_R2 = 0.180,
																						HM_Pulley_Thick = 0.009,
																						HM_matPulley = HM_matBody2,
																						HM_Pulley_Fixed = 0
										del 1

										addz	-0.300
											call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.183,
																						HM_Pulley_R1 = 0.200,
																						HM_Pulley_R2 = 0.180,
																						HM_Pulley_Thick = 0.009,
																						HM_matPulley = HM_matBody2,
																						HM_Pulley_Fixed = 0
										del 1
									del 1
								del 1
							next	i
						del 1
					del 1
				del 1
			del 1
		endif

	!	Wire
	!-----------------------------------------------------------------------------------------------
		_zPosWire = _zPosMachine

		addz	_zPosWire
			!	Mirror
			for	i = 1	to	2
				muly	(2*i - 3)
					!	Deck → Cat Head
					!-----------------------------------------------------------------------------------------------
					!	Top Machine
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(4.750 + 0.375),
																x2 = -(3.700 + 0.180),
																y1 = 0,
																y2 = -2.500/2,
																z1 = 0.630,
																z2 = 10.200

					!	Bottom Machine
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(3.800 + 0.200),
																x2 = -(3.700 + 0.180),
																y1 = 0,
																y2 = -2.500/2,
																z1 = 0.340,
																z2 = 10.200

					!	Cat Head → Jib
					!-----------------------------------------------------------------------------------------------
					_xPosJibPulley = -2.950 + (HM_JibLength - (_mJib_pulleyR + 0.400))*cos(HM_JibAng) - 0.550*sin(HM_JibAng)
					_zPosJibPulley = 0.200 + (HM_JibLength - (_mJib_pulleyR + 0.400))*sin(HM_JibAng) + 0.550*cos(HM_JibAng) + 0.180

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -3.700,
																x2 = _xPosJibPulley,
																y1 = -2.500/2,
																y2 = -0.850,
																z1 = 10.200 + 0.180,
																z2 = _zPosJibPulley

					_xPosJibPulley = -2.950 + (HM_JibLength - (_mJib_pulleyR + 0.700))*cos(HM_JibAng)
					_zPosJibPulley = 0.200 + (HM_JibLength - (_mJib_pulleyR + 0.700))*sin(HM_JibAng)

					call	"HM_mc_WirePulley.gsm"	parameters	all	HM_WP_startptX1 = -3.700,
																	HM_WP_startptZ1 = (10.200 - 0.180),
																	HM_WP_startptX2 = -3.700,
																	HM_WP_startptZ2 = (10.200 + 0.180),
																	HM_WP_startptY = -2.500/2,
																	HM_WP_endptX = _xPosJibPulley,
																	HM_WP_endptZ = _zPosJibPulley,
																	HM_WP_endptY = -0.850,
																	HM_WP_posPulley_ratio = 0.31

					!	Jib → Hook
					!-----------------------------------------------------------------------------------------------
					_zPosJibPulley = 0.150 + HM_JibLength*sin(HM_JibAng) - 0.300*cos(HM_JibAng)

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR,
																x2 = HM_WorkingR,
																y1 = -0.850,
																y2 = -HM_HookRadius,
																z1 = _zPosJibPulley,
																z2 = _zPosJibPulley - HM_JibWireLen + HM_HookHeight
				del 1
			next	i
		del 1

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosJib + HM_JibLength*sin(HM_JibAng)

		add	HM_WorkingR,	0,	_zPosHook
			hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
			hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
			hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

			addz	-HM_JibWireLen
				call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.160

			del 1
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - HM_JibWireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!----------------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif
del 1

!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all




end



!	Connect Beam Horizontal
!-----------------------------------------------------------------------------------------------
"draw_Connect_Beam_H":

	rotz	90
		call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.300,
														HM_Beam_Width_EP = 0.300,
														HM_Beam_Height_SP = 0.588,
														HM_Beam_Height_EP = 0.588,
														HM_Beam_Length = 2.102,
														HM_Beam_FlangeT = 0.016,
														HM_Beam_WebT = 0.012,
														HM_matBeam = HM_matBody2
	del 1

return