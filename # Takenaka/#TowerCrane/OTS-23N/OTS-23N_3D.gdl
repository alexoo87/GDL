!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

rotz	HM_WorkingAng
	!	Mast Basement
	!-----------------------------------------------------------------------------------------------
		rotz	45
			call	"HM_mc_OutriggerTower.gsm"	parameters	all	HM_OutrType = 1,	!	Cross
																HM_OutrWidth = 2.850,
																HM_OutrDepth = 2.850,
																HM_OutrHeight = 0.722,
																mBeamW = 0.300,
																mBeamFlangeT = 0.028,
																mBeamWebT = 0.018,
																bPlate = 0
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------
		_zPosMast = 0.740

		addz	_zPosMast
			!	Base Plate(Bottom)
			for	i = 1	to	4
				put	0.500,	0.500,	15,
					0.500,	0.210,	15,
					0.390,	0.210,	15,
					0.210,	0.390,	15,
					0.210,	0.500,	15

				prism_	nsp/3,	-0.029,		get(nsp)

				rotz	90
			next	i

			del 4

			!	Mast Paramaters
			_mastPlateWdt = 0.225
			_mastPlateThk = 0.033
			_mastColWdt = 0.130
			_mastColThk = 0.015
			_bInnerCol = 1

			_mastEntireHgt = HM_MastNumber*HM_MastHeight

			!	Mast Type 1
			call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
														HM_PlateWdt = _mastPlateWdt,
														HM_PlateThk = _mastPlateThk,
														HM_ColWdt = _mastColWdt,
														HM_ColThk = _mastColThk,
														bInnerColumn = _bInnerCol
		del 1

	!	Upper Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		dim	_coorGR[][]
		_deckThk = 0.100

		!	1st
		_zPosDeck_1st = _zPosMast + _mastEntireHgt - 3.6575

		addz	_zPosDeck_1st
			put	-0.735,	-0.625,	15,
				-1.385,	-0.625,	15,
				-1.385,	0.885,	15,
				-1.135,	1.135,	15,
				0.945,	1.135,	15,
				1.195,	0.885,	15,
				1.195,	-0.625,	15,
				0.695,	-0.625,	15,
				0.695,	0.681,	15,
				-0.735,	0.681,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -0.765
				_coorGR[1][2] = -0.595

				_coorGR[2][1] = -1.355
				_coorGR[2][2] = -0.595

				_coorGR[3][1] = -1.355
				_coorGR[3][2] = 0.8725

				_coorGR[4][1] = -1.1225
				_coorGR[4][2] = 1.105

				_coorGR[5][1] = 0.9325
				_coorGR[5][2] = 1.105

				_coorGR[6][1] = 1.165
				_coorGR[6][2] = 0.8725

				_coorGR[7][1] = 1.165
				_coorGR[7][2] = -0.595

				_coorGR[8][1] = 0.725
				_coorGR[8][2] = -0.595

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 8,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

		!	2nd
		_zPosDeck_2nd = _zPosDeck_1st + 2.2175

		addz	_zPosDeck_2nd
			put	-1.250,	-1.150,	15,
				-1.550,	-0.850,	15,
				-1.550,	0.555,	15,
				-0.850,	0.555,	15,
				-0.850,	-0.650,	15,
				0.750,	-0.650,	15,
				0.750,	0.650,	15,
				-0.900,	0.650,	15,
				-0.900,	1.150,	15,
				1.150,	1.150,	15,
				1.450,	0.850,	15,
				1.450,	-0.850,	15,
				1.150,	-1.150,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -0.870
				_coorGR[1][2] = 0.680

				_coorGR[2][1] = -0.870
				_coorGR[2][2] = 1.120

				_coorGR[3][1] = 1.1375
				_coorGR[3][2] = 1.120

				_coorGR[4][1] = 1.420
				_coorGR[4][2] = 0.8375

				_coorGR[5][1] = 1.420
				_coorGR[5][2] = -0.8375

				_coorGR[6][1] = 1.1375
				_coorGR[6][2] = -1.120

				_coorGR[7][1] = -1.2375
				_coorGR[7][2] = -1.120

				_coorGR[8][1] = -1.520
				_coorGR[8][2] = -0.8375

				_coorGR[9][1] = -1.520
				_coorGR[9][2] = 0.525

				_coorGR[10][1] = -0.880
				_coorGR[10][2] = 0.525

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 10,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1


	!	Connect to Main Object
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = _zPosMast + _mastEntireHgt

		!	Cylinder
		addz	_zPosConnect
			cylind	0.176,	0.885

			! addz	1.300
			! 	cylind	0.050,	1.085
			! del 1
		del 1

		!	H-Beam
		addz	_zPosConnect + 0.176 !+ 0.050
			add	0.800,	0.700,	0
				rotz	180
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
																	HM_Beam_Width_EP = 0.200,
																	HM_Beam_Height_SP = 0.294,
																	HM_Beam_Height_EP = 0.294,
																	HM_Beam_Length = 4.100,
																	HM_Beam_FlangeT = 0.012,
																	HM_Beam_WebT = 0.010,
																	HM_matBeam = HM_matBody2
				del 1
			del 1

			add	0.800,	-0.700,	0
				rotz	180
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
																	HM_Beam_Width_EP = 0.200,
																	HM_Beam_Height_SP = 0.294,
																	HM_Beam_Height_EP = 0.294,
																	HM_Beam_Length = 4.100,
																	HM_Beam_FlangeT = 0.012,
																	HM_Beam_WebT = 0.010,
																	HM_matBeam = HM_matBody2
				del 1
			del 1

			addy	-0.600
				addx	0.700
					gosub	"draw_Connect_Beam_H"
				del 1
				addx	-0.700
					gosub	"draw_Connect_Beam_H"
				del 1
				addx	-1.700
					gosub	"draw_Connect_Beam_H"
				del 1
				addx	-3.200
					gosub	"draw_Connect_Beam_H"
				del 1
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		_deckThk = 0.100

		_zPosMainDeck = _zPosConnect + 0.313

		addz	_zPosMainDeck
			for	i = 1	to	2
				muly	(-2*i + 3)
					put	-3.300,	-1.465,	15,
						-3.300,	-0.825,	15,
						0.800,	-0.825,	15,
						0.800,	-1.465,	15

					prism_	nsp/3,	_deckThk,	get(nsp)

					!	Guard Rail
					addz	_deckThk
						!	Insert Coordinates
						_coorGR[1][1] = 0.770
						_coorGR[1][2] = -0.855

						_coorGR[2][1] = 0.770
						_coorGR[2][2] = -1.435

						_coorGR[3][1] = -3.270
						_coorGR[3][2] = -1.435

						_coorGR[4][1] = -3.270
						_coorGR[4][2] = -0.855

						call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 4,
																			HM_GRail_Pt = _coorGR,
																			HM_GRailType = 2,
																			HM_GRailRef = 1,
																			HM_GRailH = 0.905 + 0.025/2,
																			HM_GRailD = 0.025,
																			HM_GRailT = 0.025,
																			HM_GRailDist = 1.010,
																			HM_GRailDist_hori = 0.450,
																			HM_matGRail = HM_matBody2
					del 1
				del 1
			next	i
		del 1

	!	Machinery
	!-----------------------------------------------------------------------------------------------
		_zPosMachine = _zPosConnect + 0.470

		addz	_zPosMachine
			!	U
			add	-3.220,	-0.520,	0
				block	0.640,	1.389,	0.227
			del 1

			add	-2.900,	0.045,	0.450
				call	"HM_mc_Pulley.gsm"	parameters	all		HM_Pulley_Width = 0.400,
																HM_Pulley_R1 = 0.225,
																HM_Pulley_R2 = 0.135,
																HM_Pulley_Thick = 0.020,
																HM_Pulley_Fixed = 0,
																HM_matPulley = HM_matBody2
			del 1

			!	D
			add	-2.580,	-0.010,	0
				block	0.595,	0.520,	0.147
			del 1

			add	-2.200,	0.250,	0.342
				call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.270,
															HM_Pulley_R1 = 0.195,
															HM_Pulley_R2 = 0.125,
															HM_Pulley_Thick = 0.020,
															HM_Pulley_Fixed = 0,
															HM_matPulley = HM_matBody2
			del 1
		del 1

	!	Cat Head
	!-----------------------------------------------------------------------------------------------
		_zPosCatHead = _zPosConnect + 0.590

		addz	_zPosCatHead
			addx	-1.900
				call	"HM_mc_CatHead.gsm"	parameters	all	HM_CH_Type = 1,
															HM_CH_Width = 2.600,
															HM_CH_Depth = 1.400,
															HM_CH_Height = 5.980,
															HM_CH_Radius1 = 0.050,
															HM_CH_Radius2 = 0.075,
															HM_CH_Topnt_posx = -0.200,
															!	Deck1
															HM_CH_Deck = 1,
															HM_CH_DeckW = 0.550,
															HM_CH_DeckD = 2.800,
															HM_CH_DeckT = 0.150,
															HM_CH_Deck_HoleW = 0.200,
															HM_CH_Deck_HoleD = 2.000,
															HM_CH_Deck_posx = 0.650,
															HM_CH_Deck_posz = 5.174,
															!	Pulley
															HM_CH_Pulley_R1 = 0.179,
															HM_CH_Pulley_R2 = 0.159
			del 1
		del 1

	!	Jib
	!-----------------------------------------------------------------------------------------------
		_zPosJib = _zPosConnect + 0.590

		if	HM_Jib_flag	then
			addz	_zPosJib
				addx	-1.600
					!	Jib
					call	"HM_mc_Jib.gsm"	parameters	all	HM_Jib_type = 1

					roty	-HM_JibAng
						addx	HM_JibLength - _mJib_pulleyR
							!	Jib Pulley
							rotx	90
								addz	-0.440
									cylind	0.440*2,	0.075
								del 1
							del 1

							for	i = 1	to	2
								muly	-2*i + 3
									addy	0.440 + 0.090/2
										call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.090,
																					HM_Pulley_R1 = 0.160,
																					HM_Pulley_R2 = 0.125,
																					HM_Pulley_Thick = 0.015,
																					HM_matPulley = HM_matBody2,
																					HM_Pulley_Fixed = 0
									del 1
								del 1
							next	i
						del 1
					del 1
				del 1
			del 1
		endif

	!	Wire
	!-----------------------------------------------------------------------------------------------
		_zPosWire = _zPosMachine

		addz	_zPosWire
			!	Mirror
			for	i = 1	to	2
				muly	(2*i - 3)
					!	Deck ¡ú Cat Head
					!-----------------------------------------------------------------------------------------------
					!	Top Machine
					if	i = 1	then	_y1 = -0.045
					if	i = 2	then	_y1 = 0.045
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(2.900 - 0.135),
																x2 = -(2.100 + 0.159),
																y1 = _y1,
																y2 = -1.400/2,
																z1 = 0.450,
																z2 = 6.100

					!	Bottom Machine
					if	i = 1	then	_y1 = -0.250
					if	i = 2	then	_y1 = 0.250
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(2.200 + 0.125),
																x2 = -(2.100 + 0.159),
																y1 = _y1,
																y2 = -1.400/2,
																z1 = 0.342,
																z2 = 6.100

					!	Cat Head ¡ú Jib
					!-----------------------------------------------------------------------------------------------
					_xPosJibPulley = -1.600 + (HM_JibLength - _mJib_pulleyR)*cos(HM_JibAng)
					_zPosJibPulley = 0.120 + (HM_JibLength - _mJib_pulleyR)*sin(HM_JibAng) + 0.140

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -2.100,
																x2 = _xPosJibPulley,
																y1 = -1.400/2,
																y2 = -(0.440 + 0.090/2),
																z1 = 6.100 + 0.159,
																z2 = _zPosJibPulley

					_xPosJibPulley = -1.600 + (HM_JibLength - _mJib_pulleyR)*cos(HM_JibAng)
					_zPosJibPulley = 0.120 + (HM_JibLength - _mJib_pulleyR)*sin(HM_JibAng)

					call	"HM_mc_WirePulley.gsm"	parameters	all	HM_WP_startptX1 = -2.100,
																	HM_WP_startptZ1 = (6.100 - 0.159),
																	HM_WP_startptX2 = -2.100,
																	HM_WP_startptZ2 = (6.100 + 0.159),
																	HM_WP_startptY = -1.400/2,
																	HM_WP_endptX = _xPosJibPulley,
																	HM_WP_endptZ = _zPosJibPulley,
																	HM_WP_endptY = -0.416,
																	HM_WP_posPulley_ratio = 0.31

					!	Jib ¡ú Hook
					!-----------------------------------------------------------------------------------------------
					_zPosJibPulley = 0.150 + HM_JibLength*sin(HM_JibAng)

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR,
																x2 = HM_WorkingR,
																y1 = -(0.440 + 0.090/2),
																y2 = -HM_HookRadius,
																z1 = _zPosJibPulley,
																z2 = _zPosJibPulley - HM_JibWireLen + HM_HookHeight
				del 1
			next	i
		del 1

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosJib + HM_JibLength*sin(HM_JibAng)

		add	HM_WorkingR,	0,	_zPosHook
			hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
			hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
			hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

			addz	-HM_JibWireLen
				call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.160

			del 1
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - HM_JibWireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!----------------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif

del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all




end



!	Connect Beam Horizontal
!-----------------------------------------------------------------------------------------------
"draw_Connect_Beam_H":

	rotz	90
		call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
														HM_Beam_Width_EP = 0.200,
														HM_Beam_Height_SP = 0.294,
														HM_Beam_Height_EP = 0.294,
														HM_Beam_Length = 1.200,
														HM_Beam_FlangeT = 0.012,
														HM_Beam_WebT = 0.010,
														HM_matBeam = HM_matBody2
	del 1

return