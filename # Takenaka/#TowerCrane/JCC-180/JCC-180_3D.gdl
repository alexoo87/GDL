!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

rotz	HM_WorkingAng
	!	Mast Basement
	!-----------------------------------------------------------------------------------------------
		rotz	45
			call	"HM_mc_OutriggerTower.gsm"	parameters	all	HM_OutrType = 1,	!	Cross
																HM_OutrWidth = 6.250,
																HM_OutrDepth = 6.250,
																HM_OutrHeight = 1.200,
																mBeamW = 0.400,
																mBeamFlangeT = 0.032,
																mBeamWebT = 0.020,
																bPlate = 0
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------
		_zPosMast = 1.525

		addz	_zPosMast
			!	Base Plate(Bottom)
			for	i = 1	to	4
				put	0.950,	0.950,	15,
					0.950,	0.440,	15,
					0.800,	0.440,	15,
					0.440,	0.800,	15,
					0.440,	0.950,	15

				prism_	nsp/3,	-0.325,		get(nsp)

				rotz	90
			next	i

			del 4

			!	Mast Paramaters
			_mastPlateWdt = 0.450
			_mastPlateThk = 0.032
			_mastColWdt = 0.160
			_mastColThk = 0.020
			_bInnerCol = 1

			!	Mast Type 1
			call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
														HM_PlateWdt = _mastPlateWdt,
														HM_PlateThk = _mastPlateThk,
														HM_ColWdt = _mastColWdt,
														HM_ColThk = _mastColThk,
														bInnerColumn = _bInnerCol
		del 1

	!	Upper Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		dim	_coorGR[][]
		_deckThk = 0.100

		!	1st
		_zPosDeck_1st = _zPosMast + HM_MastHeight*HM_MastNumber - 7.925

		addz	_zPosDeck_1st
			put	-1.250,	-1.250,	15,
				-2.440,	-1.250,	15,
				-2.440,	1.250,	15,
				-1.250,	1.250,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -1.280
				_coorGR[1][2] = -1.220

				_coorGR[2][1] = -2.410
				_coorGR[2][2] = -1.220

				_coorGR[3][1] = -2.410
				_coorGR[3][2] = 1.220

				_coorGR[4][1] = -1.280
				_coorGR[4][2] = 1.220

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 4,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 0.900,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

		!	2nd
		_zPosDeck_2nd = _zPosDeck_1st + 4.500

		addz	_zPosDeck_2nd
			put	-2.076,	-1.800,	15,
				-2.076,	-1.240,	15,
				-3.100,	-1.240,	15,
				-3.100,	1.240,	15,
				-2.076,	1.240,	15,
				-2.076,	1.800,	15,
				1.700,	1.800,	15,
				1.700,	1.240,	15,
				1.820,	1.240,	15,
				1.820,	0.400,	15,
				2.500,	0.400,	15,
				2.500,	-0.400,	15,
				1.820,	-0.400,	15,
				1.820,	-1.240,	15,
				1.700,	-1.240,	15,
				1.700,	-1.800,	15,
				-2.076,	-1.800,	-1,
				!	Hole
				-1.250,	-1.250,	15,
				-1.250,	1.250,	15,
				1.250,	1.250,	15,
				1.250,	-1.250,	15,
				-1.250,	-1.250,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -2.046
				_coorGR[1][2] = -1.770

				_coorGR[2][1] = -2.046
				_coorGR[2][2] = -1.210

				_coorGR[3][1] = -3.070
				_coorGR[3][2] = -1.210

				_coorGR[4][1] = -3.070
				_coorGR[4][2] = 1.210

				_coorGR[5][1] = -2.046
				_coorGR[5][2] = 1.210

				_coorGR[6][1] = -2.046
				_coorGR[6][2] = 1.770

				_coorGR[7][1] = 1.670
				_coorGR[7][2] = 1.770

				_coorGR[8][1] = 1.670
				_coorGR[8][2] = 1.210

				_coorGR[9][1] = 1.790
				_coorGR[9][2] = 1.210

				_coorGR[10][1] = 1.790
				_coorGR[10][2] = 0.370

				_coorGR[11][1] = 2.470
				_coorGR[11][2] = 0.370

				_coorGR[12][1] = 2.470
				_coorGR[12][2] = -0.370

				_coorGR[13][1] = 1.790
				_coorGR[13][2] = -0.370

				_coorGR[14][1] = 1.790
				_coorGR[14][2] = -1.210

				_coorGR[15][1] = 1.670
				_coorGR[15][2] = -1.210

				_coorGR[16][1] = 1.670
				_coorGR[16][2] = -1.770

				_coorGR[17][1] = -2.046
				_coorGR[17][2] = -1.770

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 17,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Connect to Main Object
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = _zPosMast + HM_MastHeight*HM_MastNumber

		!	Cylinder
		addz	_zPosConnect
			cylind	0.205,	1.540

			! addz	1.300
			! 	cylind	0.050,	1.085
			! del 1
		del 1

		!	Floor
		addz	_zPosConnect + 0.205 !+ 0.050
			put	-3.500,	-1.635,	15,
				-3.500,	-1.430,	15,
				-7.650,	-1.430,	15,
				-7.650,	1.430,	15,
				-3.500,	1.430,	15,
				-3.500,	1.635,	15,
				1.635,	1.635,	15,
				1.635,	-1.635,	15

			prism_	nsp/3,	0.792,	get(nsp)
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		_deckThk = 0.100

		_zPosMainDeck = _zPosConnect + 0.897

		addz	_zPosMainDeck
			put	-3.500,	-1.940,	15,
				-7.650,	-1.940,	15,
				-8.145,	-1.635,	15,
				-8.145,	1.635,	15,
				-7.650,	1.940,	15,
				-7.000,	1.940,	15,
				-7.000,	2.340,	15,
				-5.535,	2.340,	15,
				-5.535,	1.940,	15,
				0.600,	1.940,	15,
				0.600,	2.260,	15,
				1.635,	2.260,	15,
				1.635,	1.635,	15,
				-3.500,	1.635,	15,
				-3.500,	1.430,	15,
				-7.650,	1.430,	15,
				-7.650,	-1.430,	15,
				-3.500,	-1.430,	15,
				-3.500,	-1.635,	15,
				2.030,	-1.635,	15,
				2.030,	-3.950,	15,
				-1.600,	-3.950,	15,
				-1.600,	-2.600,	15,
				-3.500,	-2.600,	15

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -0.800
				_coorGR[1][2] = -3.920

				_coorGR[2][1] = -1.570
				_coorGR[2][2] = -3.920

				_coorGR[3][1] = -1.570
				_coorGR[3][2] = -2.570

				_coorGR[4][1] = -3.470
				_coorGR[4][2] = -2.570

				_coorGR[5][1] = -3.470
				_coorGR[5][2] = -1.910

				_coorGR[6][1] = -7.6415
				_coorGR[6][2] = -1.910

				_coorGR[7][1] = -8.115
				_coorGR[7][2] = -1.618

				_coorGR[8][1] = -8.115
				_coorGR[8][2] = 1.618

				_coorGR[9][1] = -7.6415
				_coorGR[9][2] = 1.910

				_coorGR[10][1] = -6.970
				_coorGR[10][2] = 1.910

				_coorGR[11][1] = -6.970
				_coorGR[11][2] = 2.310

				_coorGR[12][1] = -5.565
				_coorGR[12][2] = 2.310

				_coorGR[13][1] = -5.565
				_coorGR[13][2] = 1.910

				_coorGR[14][1] = 0.630
				_coorGR[14][2] = 1.910

				_coorGR[15][1] = 0.630
				_coorGR[15][2] = 2.230

				_coorGR[16][1] = 1.605
				_coorGR[16][2] = 2.230

				_coorGR[17][1] = 1.605
				_coorGR[17][2] = 1.665

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 17,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2

				!	2nd
				_coorGR[1][1] = 1.665
				_coorGR[1][2] = -1.665

				_coorGR[2][1] = 2.000
				_coorGR[2][2] = -1.665

				_coorGR[3][1] = 2.000
				_coorGR[3][2] = -2.570

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 3,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Machinery
	!-----------------------------------------------------------------------------------------------
		_zPosMachine = _zPosConnect + 0.997

		addz	_zPosMachine
			!	U
			! add	-6.245,	-0.645,	0
			! 	block	1.430,	1.845,	0.216
			! del 1

			add	-7.100,	0,	0.520
				call	"HM_mc_Pulley.gsm"	parameters	all		HM_Pulley_Width = 1.200,
																HM_Pulley_R1 = 0.630,
																HM_Pulley_R2 = 0.400,
																HM_Pulley_Thick = 0.025,
																HM_Pulley_Fixed = 0,
																HM_matPulley = HM_matBody2
			del 1

			!	D
			! add	-4.110,	-1.205,	0
			! 	block	0.720,	2.085,	0.266
			! del 1

			add	-5.010,	0,	0.420
				call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 1.200,
															HM_Pulley_R1 = 0.380,
															HM_Pulley_R2 = 0.320,
															HM_Pulley_Thick = 0.025,
															HM_Pulley_Fixed = 0,
															HM_matPulley = HM_matBody2
			del 1
		del 1

	!	Operator Cap
	!-----------------------------------------------------------------------------------------------
		_zPosOperCap = _zPosMainDeck + 0.100

		addz	_zPosOperCap
			add	-0.770,	-3.950,	0
				!	Box(Room)
				block	1.000,	1.350,	2.200

				!	Operator Cap
				addx	1.000
					call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Type = 2,
																	HM_OP_Width = 1.350,
																	HM_OP_Depth = 2.490,
																	HM_OP_Depth_bot = 1.800,
																	HM_OP_Depth_roo = 2.170,
																	HM_OP_Height = 2.200
				del 1
			del 1
		del 1

	!	Cat Head
	!-----------------------------------------------------------------------------------------------
		_zPosCatHead = _zPosConnect + 1.197

		addz	_zPosCatHead
			addx	-7.400
				call	"HM_mc_CatHead.gsm"	parameters	all	HM_CH_Type = 1,
															HM_CH_Width = 8.795,
															HM_CH_Depth = 2.970,
															HM_CH_Height = 8.950,
															HM_CH_Radius1 = 0.0385,
															HM_CH_Radius2 = 0.100,
															HM_CH_Topnt_posx = 2.900,
															HM_CH_Column3 = 1,
															HM_CH_Column3_posx = 4.050,
															HM_CH_Radius3 = 0.0675,
															!	Deck1
															HM_CH_Deck = 1,
															HM_CH_DeckW = 2.400,
															HM_CH_DeckD = 4.500,
															HM_CH_DeckT = 0.100,
															HM_CH_Deck_HoleW = 2.050,
															HM_CH_Deck_HoleD = 3.400,
															HM_CH_Deck_posx = 0.980,
															HM_CH_Deck_posz = 7.500,
															!	Pulley
															HM_CH_Pulley_R1 = 0.350,
															HM_CH_Pulley_R2 = 0.315
			del 1
		del 1

	!	Jib
	!-----------------------------------------------------------------------------------------------
		_zPosJib = _zPosConnect + 1.397

		if	HM_Jib_flag	then
			addz	_zPosJib
				addx	-2.850
					!	Jib
					call	"HM_mc_Jib.gsm"	parameters	all	HM_Jib_type = 1

					roty	-HM_JibAng
						addx	HM_JibLength - _mJib_pulleyR
							!	Jib Pulley
							for	i = 1	to	2
								muly	-2*i + 3
									addz	-0.150
										addy	0.600
											call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.175,
																						HM_Pulley_R1 = 0.200,
																						HM_Pulley_R2 = 0.180,
																						HM_Pulley_Thick = 0.016,
																						HM_matPulley = HM_matBody2,
																						HM_Pulley_Fixed = 0
										del 1
									del 1

									addx	-0.410
										addz	0.240
											addy	0.600
												call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.175,
																							HM_Pulley_R1 = 0.200,
																							HM_Pulley_R2 = 0.180,
																							HM_Pulley_Thick = 0.016,
																							HM_matPulley = HM_matBody2,
																							HM_Pulley_Fixed = 0
											del 1
										del 1
									del 1
								del 1
							next	i
						del 1
					del 1
				del 1
			del 1
		endif

	!	Wire
	!-----------------------------------------------------------------------------------------------
		_zPosWire = _zPosMachine

		addz	_zPosWire
			!	Mirror
			for	i = 1	to	2
				muly	(2*i - 3)
					!	Deck → Cat Head
					!-----------------------------------------------------------------------------------------------
					!	Bottom Machine
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(5.010 + 0.320),
																x2 = -(4.500 + 0.315),
																y1 = -0.250,
																y2 = -1.485,
																z1 = 0.420,
																z2 = 9.150

					!	Top Machine
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(7.100 - 0.400),
																x2 = -(4.500 + 0.315),
																y1 = -0.250,
																y2 = -1.485,
																z1 = 0.520,
																z2 = 9.150

					!	Cat Head → Jib
					!-----------------------------------------------------------------------------------------------
					_xPosJibPulley = -2.850 + (HM_JibLength - (_mJib_pulleyR + 0.410))*cos(HM_JibAng) - 0.240*sin(HM_JibAng)
					_zPosJibPulley = 0.400 + (HM_JibLength - (_mJib_pulleyR + 0.410))*sin(HM_JibAng) + 0.240*cos(HM_JibAng) + 0.180

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -4.500,
																x2 = _xPosJibPulley,
																y1 = -1.485,
																y2 = -0.600,
																z1 = (9.150 + 0.180),
																z2 = _zPosJibPulley

					_xPosJibPulley = -2.850 + (HM_JibLength - (_mJib_pulleyR + 0.620))*cos(HM_JibAng) - 0.050*sin(HM_JibAng)
					_zPosJibPulley = 0.400 + (HM_JibLength - (_mJib_pulleyR + 0.620))*sin(HM_JibAng) - 0.050*cos(HM_JibAng)

					call	"HM_mc_WirePulley.gsm"	parameters	all	HM_WP_startptX1 = -4.500,
																	HM_WP_startptZ1 = (9.150 - 0.180),
																	HM_WP_startptX2 = -4.500,
																	HM_WP_startptZ2 = (9.150 + 0.180),
																	HM_WP_startptY = -1.485,
																	HM_WP_endptX = _xPosJibPulley,
																	HM_WP_endptZ = _zPosJibPulley,
																	HM_WP_endptY = -0.600,
																	HM_WP_posPulley_ratio = 0.35

					!	Jib → Hook
					!-----------------------------------------------------------------------------------------------
					_zPosJibPulley = 0.400 + (HM_JibLength - 0.620)*sin(HM_JibAng) - 0.050*cos(HM_JibAng)

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR,
																x2 = HM_WorkingR,
																y1 = -0.600,
																y2 = -HM_HookRadius,
																z1 = _zPosJibPulley,
																z2 = _zPosJibPulley - HM_JibWireLen + HM_HookHeight
				del 1
			next	i
		del 1

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosJib + HM_JibLength*sin(HM_JibAng)

		add	HM_WorkingR,	0,	_zPosHook
			hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
			hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
			hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

			addz	-HM_JibWireLen
				call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.194

			del 1
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - HM_JibWireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot 	0, 	0, 	0,				unID, HM_SLwireLen, 1 			: unID = unID + 1
				hotspot 	0, 	0, 	-HM_SLwireLen,	unID, HM_SLwireLen, 2			: unID = unID + 1
				hotspot 	0, 	0, 	1,				unID, HM_SLwireLen, 3			: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!----------------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif
del 1

!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all




end



!	Connect Beam Horizontal
!-----------------------------------------------------------------------------------------------
"draw_Connect_Beam_H":

	rotz	90
		call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.300,
														HM_Beam_Width_EP = 0.300,
														HM_Beam_Height_SP = 0.488,
														HM_Beam_Height_EP = 0.488,
														HM_Beam_Length = 1.460,
														HM_Beam_FlangeT = 0.018,
														HM_Beam_WebT = 0.012,
														HM_matBeam = HM_matBody2
	del 1

return

