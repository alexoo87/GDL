
!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	DATE : 2017/11/30 WED
!	VER : AC 20
!	DEV : JO HYUNG JUN


!----------------------------------------------------------------------------------------------------
!	TEST
!----------------------------------------------------------------------------------------------------
if	bOrigin	then
	hotspot	0, 0, 0
	model	wire
	circle	0.1
	circle	0.2
	model	solid
endif

if	bTest	then
	add	-HM_OutrLen/2,	HM_OutrWidth/2,	0
		hotspot	0,	0,	0
		circle	0.1
	del 1

	add	-HM_OutrLen/2,	-HM_OutrWidth/2,	0
		hotspot	0,	0,	0
		circle	0.1
	del 1

	add	HM_OutrLen/2,	HM_OutrWidth/2,	0
		hotspot	0,	0,	0
		circle	0.1
	del 1

	add	HM_OutrLen/2,	-HM_OutrWidth/2,	0
		hotspot	0,	0,	0
		circle	0.1
	del 1
endif

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
unID = 4000



!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
resol HM_3D_resol

if	GLOB_FEEDBACK_MODE	then
	resol	8
endif

!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen			HM_3D_pen

if	HM_matWarning = ""	then
	material	HM_matBody2
else
	material	HM_matWarning
endif

	!----------------------------------------------------------------------------------------------------
	!	OTA-150S
	!----------------------------------------------------------------------------------------------------
	if	HM_OutrType = OTA_150S	then
		!	Wheel/Wheel Column
		!----------------------------------------------------------------------------------------------------
		_offsetBtm = 0.025

		_wheelCenterY = 0.178
		_wheelCenterX = 0.250
		_wheelRad = 0.1975
		_holeRad = 0.0375
		_wheelCapLth = 0.940
		_wheelCapHgt = 0.370
		_wheelCapWdt = 0.244
		_wheelCapThk = 0.022
		_wheelThk = 0.100	!	Temp Value
		_exWheelCapLth = 0.130
		_exWheelCapHgt = 0.320

		_colBRad1 = 0.100
		_colBRad2 = 0.055
		_colBHgt = 0.180
		_colTRad = 0.045
		_colTHgt = 0.172

		addz	_offsetBtm
			!	Back/Right
			add	-HM_OutrLen/2,	-HM_OutrWidth/2,	0
				_wheelType = 1
				gosub	"wheel_"
			del 1

			!	Back/Left
			add	-HM_OutrLen/2,	HM_OutrWidth/2,	0
				_wheelType = 2
				mulx	-1
					gosub	"wheel_"
				del 1
			del 1

			!	Front/Right
			add	HM_OutrLen/2,	-HM_OutrWidth/2,	0
				_wheelType = 2
				gosub	"wheel_"
			del 1

			!	Front/Left
			add	HM_OutrLen/2,	HM_OutrWidth/2,	0
				_wheelType = 1
				gosub	"wheel_"
			del 1
		del 1

		!	Beam
		!----------------------------------------------------------------------------------------------------

			!	beamH
			_offsetBeamH = _offsetBtm + _wheelCapHgt + _colBHgt + _colTHgt
			_exBeamHLth = 0.220
			_beamHWdt = 0.300
			_beamHThk = 0.025

			put	-_beamHWdt/2,	-(HM_OutrWidth/2 + _exBeamHLth),	15,
				-_beamHWdt/2,	(HM_OutrWidth/2 + _exBeamHLth),		15,
				_beamHWdt/2,	(HM_OutrWidth/2 + _exBeamHLth),		15,
				_beamHWdt/2,	-(HM_OutrWidth/2 + _exBeamHLth),	15

			addz	_offsetBeamH
				addx	-HM_OutrLen/2
					prism_	nsp/3,	_beamHThk,	use(nsp)
				del 1

				addx	HM_OutrLen/2
					prism_	nsp/3,	_beamHThk,	get(nsp)
				del 1
			del 1

			!	TBeamH
			_offsetTBeamH = _offsetBeamH + _beamHThk
			_TBeamHHgt = 0.400
			_TBeamHWdt = 0.300
			_TBeamHWebThk = 0.014
			_TBeamHFlangeThk = 0.026

			put	-_TBeamHWebThk/2,	0,								15,
				-_TBeamHWebThk/2,	_TBeamHHgt - _TBeamHFlangeThk,	15,
				-_TBeamHWdt/2,		_TBeamHHgt - _TBeamHFlangeThk,	15,
				-_TBeamHWdt/2,		_TBeamHHgt,						15,
				_TBeamHWdt/2,		_TBeamHHgt,						15,
				_TBeamHWdt/2,		_TBeamHHgt - _TBeamHFlangeThk,	15,
				_TBeamHWebThk/2,	_TBeamHHgt - _TBeamHFlangeThk,	15,
				_TBeamHWebThk/2,	0,								15

			addz	_offsetTBeamH
				addx	-HM_OutrLen/2
					rotx	90
						addz	-(HM_OutrWidth/2 + _exBeamHLth)
							prism_	nsp/3,	HM_OutrWidth + _exBeamHLth*2,	use(nsp)
						del 1
					del 1
				del 1

				addx	HM_OutrLen/2
					rotx	90
						addz	-(HM_OutrWidth/2 + _exBeamHLth)
							prism_	nsp/3,	HM_OutrWidth + _exBeamHLth*2,	get(nsp)
						del 1
					del 1
				del 1
			del 1

			!	BeamV
			_offsetBeamV = _offsetTBeamH
			_beamVLct = 1.800
			_beamVWdt = 0.300
			_beamVLth = HM_OutrLen - _beamHWdt

			addz	_offsetBeamV
				addx	-_beamVLth/2
					addy	_beamVLct
						call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = 0.300,
																		HM_Beam_Height_SP = 0.400,
																		HM_Beam_Height_EP = 0.400,
																		HM_Beam_Offset_SP = 0,
																		HM_Beam_Offset_EP = 0,
																		HM_Beam_Length = _beamVLth,
																		HM_Beam_FlangeT = 0.013,
																		HM_Beam_WebT = 0.012,
																		HM_matBeam = HM_matBody2
					del 1

					addy	-_beamVLct
						call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = 0.300,
																		HM_Beam_Height_SP = 0.400,
																		HM_Beam_Height_EP = 0.400,
																		HM_Beam_Offset_SP = 0,
																		HM_Beam_Offset_EP = 0,
																		HM_Beam_Length = _beamVLth,
																		HM_Beam_FlangeT = 0.013,
																		HM_Beam_WebT = 0.012,
																		HM_matBeam = HM_matBody2
					del 1
				del 1
			del 1

		!	Oblique Beam
		!----------------------------------------------------------------------------------------------------
		_offsetOlqBeam = _offsetTBeamH - 0.400

		_aBeam = 34.10
		_aSubBeam = 90 - _aBeam
		_olqBeamHgt = 0.400
		_olqBeamWdt = 0.200
		_olqBeamLth = (HM_OutrWidth/2 - _beamVLct)/sin(_aBeam)
		_olqBeamWebThk = 0.006	!	Temp Value
		_olqBeamFlangeThk = 0.013

		addz	_offsetOlqBeam + _olqBeamHgt
			!	Back/Right
			add	-HM_OutrLen/2,	-HM_OutrWidth/2,	0
				gosub	"obliqueBeam_OTA_150S"
			del 1

			!	Front/Right
			add	HM_OutrLen/2,	-HM_OutrWidth/2,	0
				mulx	-1
					gosub	"obliqueBeam_OTA_150S"
				del 1
			del 1

			!	Back/Left
			add	-HM_OutrLen/2,	HM_OutrWidth/2,	0
				muly	-1
					gosub	"obliqueBeam_OTA_150S"
				del 1
			del 1

			!	Front/Left
			add	HM_OutrLen/2,	HM_OutrWidth/2,	0
				mulx	-1
					muly	-1
						gosub	"obliqueBeam_OTA_150S"
					del 1
				del 1
			del 1
		del 1


		!	Bottom Board H
		!----------------------------------------------------------------------------------------------------
		_offsetBtmHBoard = 0.872

		_btmBoardHDist = 0.009
		_btmBoardHWdt = 0.500
		_btmBoardHThk = 0.050
		_exBtmBoardHLth = 0.160

		put	-_btmBoardHWdt/2,	-(HM_OutrWidth/2 + _exBtmBoardHLth),	15,
			-_btmBoardHWdt/2,	(HM_OutrWidth/2 + _exBtmBoardHLth),	15,
			_btmBoardHWdt/2,	(HM_OutrWidth/2 + _exBtmBoardHLth),	15,
			_btmBoardHWdt/2,	-(HM_OutrWidth/2 + _exBtmBoardHLth),	15

		addz	_offsetBtmHBoard
			addx	-(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt/2)
				prism_	nsp/3,	_btmBoardHThk,	use(nsp)
			del 1

			addx	(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt/2)
				prism_	nsp/3,	_btmBoardHThk,	get(nsp)
			del 1
		del 1

			!	HandRail
			_offsetBtmHRail = _offsetBtmHBoard + 0.010

			_btmHRailHgt = 1.040
			_btmHRailDpt = 0.050
			_btmHRailThk = 0.034

			add	-(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt),	(HM_OutrWidth/2 + _exBtmBoardHLth),	_offsetBtmHRail
				rotz	-90
					call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																	HM_GRailRef = 3,
																	HM_GRailL = HM_OutrWidth + _exBtmBoardHLth*2,
																	HM_GRailH = _btmHRailHgt,
																	HM_GRailD = _btmHRailDpt,
																	HM_GRailT = _btmHRailThk,
																	HM_GRail_colHide_Left = 0,
																	HM_GRail_colHide_Right = 0,
																	HM_matGRail = HM_matBody2
				del 1
			del 1

			add	(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt),	(HM_OutrWidth/2 + _exBtmBoardHLth),	_offsetBtmHRail
				rotz	-90
					call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																	HM_GRailRef = 2,
																	HM_GRailL = HM_OutrWidth + _exBtmBoardHLth*2,
																	HM_GRailH = _btmHRailHgt,
																	HM_GRailD = _btmHRailDpt,
																	HM_GRailT = _btmHRailThk,
																	HM_GRail_colHide_Left = 0,
																	HM_GRail_colHide_Right = 0,
																	HM_matGRail = HM_matBody2
				del 1
			del 1

			!	Ladder
			_offsetBtmHLdr = _offsetBtmHBoard - 0.700

			_btmHLdrLct = 0.150
			_btmHLdrWdt = 0.400
			_btmHLdrHgt = 0.750
			_btmHLdrRad = 0.0136
			_btmHLdrDist = 0.350

			_btmHLdrAng = 5.71
			_btmHLdrObqHgt = 1.005

			add	-(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt/2),	-(HM_OutrWidth/2 + _exBtmBoardHLth + _btmHLdrLct),	_offsetBtmHLdr
				gosub	"btmHLadder_"
			del 1

			add	(HM_OutrLen/2 + _beamHWdt/2 + _btmBoardHDist + _btmBoardHWdt/2),	(HM_OutrWidth/2 + _exBtmBoardHLth + _btmHLdrLct),	_offsetBtmHLdr
				gosub	"btmHLadder_"
			del 1

		!	Bottom Board V
		!----------------------------------------------------------------------------------------------------
		_offsetBtmVBoard = 0.622

		_xPosBtmBoardV = 0.150
		_btmBoardVDist = 0.070
		_btmBoardVLth = 4.500
		_btmBoardVWdt = 0.600
		_btmBoardVThk = 0.100

		put	-_btmBoardVLth/2,	-_btmBoardVWdt/2,	15,
			-_btmBoardVLth/2,	_btmBoardVWdt/2,	15,
			_btmBoardVLth/2,	_btmBoardVWdt/2,	15,
			_btmBoardVLth/2,	-_btmBoardVWdt/2,	15

		addz	_offsetBtmVBoard
			addx	_xPosBtmBoardV
				addy	-(_beamVLct + _beamVWdt/2 + _btmBoardVDist + _btmBoardVWdt/2)
					prism_	nsp/3,	_btmBoardVThk,	use(nsp)
				del 1
			del 1

			addx	-_xPosBtmBoardV
				addy	(_beamVLct + _beamVWdt/2 + _btmBoardVDist + _btmBoardVWdt/2)
					prism_	nsp/3,	_btmBoardVThk,	get(nsp)
				del 1
			del 1
		del 1

			!	HandRail
			_offsetBtmVRail = _offsetBtmVBoard - 0.040

			_btmVRailHgt = 1.040
			_btmVRailDpt = 0.050
			_btmVRailThk = 0.034

			addz	_offsetBtmVRail
				gosub	"btmVRail_"

				rotz	180
					gosub	"btmVRail_"
				del 1
			del 1


		!	Board
		!----------------------------------------------------------------------------------------------------
		_offsetBoard = _offsetBeamV + 0.400

		_boardWdt = HM_OutrLen - _beamHWdt
		_modWdt = 0.940
		_nBoard = int(_boardWdt/_modWdt)
		_boardDist = (_boardWdt - _modWdt*_nBoard)/(_nBoard - 1)
		_boardWdt = _beamVLct*2 - _beamVWdt
		_boardThk = 0.025	!	Temp Value

		addz	_offsetBoard - _boardThk
			addx	-(HM_OutrLen/2 - _beamHWdt/2)

				for	i = 1	to	_nBoard
					put	0,			-_boardWdt/2,	15,
						0,			_boardWdt/2,	15,
						_modWdt,	_boardWdt/2,	15,
						_modWdt,	-_boardWdt/2,	15

					prism_	nsp/3,	_boardThk,	get(nsp)

					addx	_modWdt + _boardDist
				next	i

				del _nBoard

			del 1
		del 1

		!	Column
		!----------------------------------------------------------------------------------------------------
		_offsetColumn = _offsetBoard

		dim	_colRad[], _colHgt[]

		_colRad[1] = 1.000
		_colHgt[1] = 0.9655
		_colRad[2] = 0.750
		_colHgt[2] = 0.500
		_colRad[3] = 1.0535
		_colHgt[3] = 0.588
		_colRad[4] = 1.045
		_colHgt[4] = 0.042
		_colRad[5] = 1.065
		_colHgt[5] = 0.090
		_colRad[6] = 0.960
		_colHgt[6] = 0.042

		addz	_offsetColumn
			for	i = 1	to	6
				cylind	_colHgt[i],	_colRad[i]

				addz	_colHgt[i]
			next	i

			del 6
		del 1

		!	Top Board
		!----------------------------------------------------------------------------------------------------
		_offsetTopBoard = 2.305

		_topBoardWdt = 3.400
		_topBoardThk = 0.100

		put	-_topBoardWdt/2,	-_topBoardWdt/2,	15,
			-_topBoardWdt/2,	_topBoardWdt/2,		15,
			_topBoardWdt/2,		_topBoardWdt/2,		15,
			_topBoardWdt/2,		-_topBoardWdt/2,	15,
			-_topBoardWdt/2,	-_topBoardWdt/2,	-1,
			0,					0,					900,
			_colRad[2],			360,				4000+15


		addz	_offsetTopBoard
			prism_	nsp/3,	_topBoardThk,	get(nsp)
		del 1

			!	HandRail/Ladder
			_offsetTopRail = _offsetTopBoard - 0.040

			_topRailHgt = 1.040
			_topRailDpt = 0.050
			_topRailThk = 0.034

			_offsetTopLdr = _offsetTopBoard - 0.950

			_xPosTopLdr = 0.165
			_topLdrDist = 0.500
			_topLdrWdt = 0.400
			_topLdrHgt1 = 1.875
			_topLdrHgt2 = 0.925
			_topLdrRad = 0.0136
			_topLdrElb = 0.075
			_nTopLdr = 4

				!	HandRail
				addz	_offsetTopRail
					!	Left
					add	-_topBoardWdt/2,	_topBoardWdt/2,	0
						rotz	-90
							call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																			HM_GRailRef = 3,
																			HM_GRailL = (_topBoardWdt - _topLdrDist)/2,
																			HM_GRailH = _topRailHgt,
																			HM_GRailD = _topRailDpt,
																			HM_GRailT = _topRailThk,
																			HM_GRailDist = 0.800,
																			HM_GRail_colHide_Left = 1,
																			HM_GRail_colHide_Right = 0,
																			HM_GRail_cutLeft_ang = 45,
																			HM_matGRail = HM_matBody2
						del 1

						addy	-(_topBoardWdt + _topLdrDist)/2
							rotz	-90
								call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																				HM_GRailRef = 3,
																				HM_GRailL = (_topBoardWdt - _topLdrDist)/2,
																				HM_GRailH = _topRailHgt,
																				HM_GRailD = _topRailDpt,
																				HM_GRailT = _topRailThk,
																				HM_GRailDist = 0.800,
																				HM_GRail_colHide_Left = 0,
																				HM_GRail_colHide_Right = 1,
																				HM_GRail_cutRight_ang = 45,
																				HM_matGRail = HM_matBody2
							del 1
						del 1
					del 1

					!	Bottom
					add	-_topBoardWdt/2,	-_topBoardWdt/2,	0
						call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																		HM_GRailRef = 3,
																		HM_GRailL = _topBoardWdt,
																		HM_GRailH = _topRailHgt,
																		HM_GRailD = _topRailDpt,
																		HM_GRailT = _topRailThk,
																		HM_GRailDist = 0.700,
																		HM_GRail_colHide_Left = 1,
																		HM_GRail_colHide_Right = 1,
																		HM_GRail_cutLeft_ang = 45,
																		HM_GRail_cutRight_ang = 45,
																		HM_matGRail = HM_matBody2
					del 1

					!	Right
					add	_topBoardWdt/2,	-_topBoardWdt/2,	0
						rotz	180
							rotz	-90
								call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																				HM_GRailRef = 3,
																				HM_GRailL = (_topBoardWdt - _topLdrDist)/2,
																				HM_GRailH = _topRailHgt,
																				HM_GRailD = _topRailDpt,
																				HM_GRailT = _topRailThk,
																				HM_GRailDist = 0.800,
																				HM_GRail_colHide_Left = 1,
																				HM_GRail_colHide_Right = 0,
																				HM_GRail_cutLeft_ang = 45,
																				HM_matGRail = HM_matBody2
							del 1

							addy	-(_topBoardWdt + _topLdrDist)/2
								rotz	-90
									call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																					HM_GRailRef = 3,
																					HM_GRailL = (_topBoardWdt - _topLdrDist)/2,
																					HM_GRailH = _topRailHgt,
																					HM_GRailD = _topRailDpt,
																					HM_GRailT = _topRailThk,
																					HM_GRailDist = 0.800,
																					HM_GRail_colHide_Left = 0,
																					HM_GRail_colHide_Right = 1,
																					HM_GRail_cutRight_ang = 45,
																					HM_matGRail = HM_matBody2
								del 1
							del 1
						del 1
					del 1

					!	Top
					add	_topBoardWdt/2,	_topBoardWdt/2,	0
						rotz	180
						call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																		HM_GRailRef = 3,
																		HM_GRailL = _topBoardWdt,
																		HM_GRailH = _topRailHgt,
																		HM_GRailD = _topRailDpt,
																		HM_GRailT = _topRailThk,
																		HM_GRailDist = 0.700,
																		HM_GRail_colHide_Left = 1,
																		HM_GRail_colHide_Right = 1,
																		HM_GRail_cutLeft_ang = 45,
																		HM_GRail_cutRight_ang = 45,
																		HM_matGRail = HM_matBody2
						del 1
					del 1
				del 1

				!	Ladder
				addz	_offsetTopLdr

					!	Left
					add	-(_topBoardWdt/2 + _xPosTopLdr),	-_topLdrWdt/2,	0
						gosub	"topLdr_"

						addz	0.050
							for	i = 1	to	_nTopLdr
								rotx	-90
									cylind	_topLdrWdt,	_topLdrRad
								del 1

								addz	0.350
							next	i

							del _nTopLdr
						del 1
					del 1

					add	-(_topBoardWdt/2 + _xPosTopLdr),	_topLdrWdt/2,	0
						gosub	"topLdr_"
					del 1

					!	Right
					add	_topBoardWdt/2 + _xPosTopLdr,	-_topLdrWdt/2,	0
						mulx	-1
							gosub	"topLdr_"

							addz	0.050
								for	i = 1	to	_nTopLdr
									rotx	-90
										cylind	_topLdrWdt,	_topLdrRad
									del 1

									addz	0.350
								next	i

								del _nTopLdr
							del 1
						del 1
					del 1

					add	_topBoardWdt/2 + _xPosTopLdr,	_topLdrWdt/2,	0
						mulx	-1
							gosub	"topLdr_"
						del 1
					del 1
				del 1
	endif

	!----------------------------------------------------------------------------------------------------
	!	U-06H
	!----------------------------------------------------------------------------------------------------
	if	HM_OutrType = U_06H	then

		!	Main Cylinder
		!----------------------------------------------------------------------------------------------------
		_offsetMainCyd = 0.150
		_mainCydRad = 0.355
		_mainCydHgt = 0.500

		addz	_offsetMainCyd
			cylind	_mainCydHgt,	_mainCydRad
		del 1

		!	Sub Cylinder
		!----------------------------------------------------------------------------------------------------
		_offsetSubCyd = _offsetMainCyd + _mainCydHgt
		_cydRad1 = 0.366
		_cydHgt1 = 0.048
		_cydRad2 = 0.3125
		_cydHgt2 = 0.024
		_cydRad3 = 0.305
		_cydHgt3 = 0.222
		_cydRad4 = 0.360
		_cydHgt4 = 0.022

		addz	_offsetSubCyd
			cylind	_cydHgt1,	_cydRad1

			addz	_cydHgt1
				cylind	_cydHgt2,	_cydRad2

				addz	_cydHgt2
					cylind	_cydHgt3,	_cydRad3

					addz	_cydHgt3
						cylind	_cydHgt4,	_cydRad4
					del 1
				del 1
			del 1
		del 1

		!	Leg
		!----------------------------------------------------------------------------------------------------
		_LegLth = sqr((HM_OutrLen/2)^2 + (HM_OutrWidth/2)^2)
		rotz	45
			for	i = 1	to	4
				!	Support
				addx	_LegLth
					!	Basement Plate
					_bsmPltWdt = 0.300
					_bsmPltThk = 0.012

					put	-_bsmPltWdt/2,	-_bsmPltWdt/2,	15,
						-_bsmPltWdt/2,	_bsmPltWdt/2,	15,
						_bsmPltWdt/2,	_bsmPltWdt/2,	15,
						_bsmPltWdt/2,	-_bsmPltWdt/2,	15

					rotz	45
						prism_	nsp/3,	_bsmPltThk,	get(nsp)
					del 1

					!	Basement Cylind
					_offsetBsmCyd = _bsmPltThk
					_bsmCydRad = 0.065
					_bsmCydHgt = 0.040

					addz	_offsetBsmCyd
						cylind	_bsmCydHgt,	_bsmCydRad
					del 1

					!	Wing
					_offsetWing = _bsmPltThk
					_wingThk = 0.009

					addz	_offsetWing
						for	w = 1	to	4
							addx	_bsmCydRad
								put	0,		0,		15,
									0,		0.032,	15,
									0.055,	0.032,	15,
									0.115,	0.020,	15,
									0.115,	0,		15

								rotx	90
									prism_	nsp/3,	_wingThk,	get(nsp)
								del 1
							del 1

							rotz	90
						next	w

						del 4
					del 1

					!	Support Bar
					_offsetSptBar = _offsetBsmCyd + _bsmCydHgt
					_sptBarRad = 0.023
					_sptBarHgt = 0.315

					addz	_offsetSptBar
						cylind	_sptBarHgt,	_sptBarRad
					del 1

					!	Box
					_offsetBox = 0.163
					_boxWdt = 0.100
					_boxHgt = 0.150

					addz	_offsetBox
						put	-_boxWdt/2,	-_boxWdt/2,	15,
							-_boxWdt/2,	_boxWdt/2,	15,
							_boxWdt/2,	_boxWdt/2,	15,
							_boxWdt/2,	-_boxWdt/2,	15,
							-_boxWdt/2,	-_boxWdt/2,	-1,
							0,			0,			900,
							_sptBarRad,	360,		4000+15

						prism_	nsp/3,	_boxHgt,	get(nsp)
					del 1
				del 1

				!	Leg
				_offsetLeg = 0.153
				_LegDist = 0.022

				addz	_offsetLeg
					addx	_mainCydRad
						_plgThk = 0.009
						_webThk = 0.150 - 0.020

						!	1st
						_1stWdt = 0.150
						_1stLth = 0.115
						_1stHgt = 0.378

						call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _1stWdt,
																		HM_Beam_Height_SP = _1stHgt,
																		HM_Beam_Height_EP = _1stHgt,
																		HM_Beam_Offset_SP = 0,
																		HM_Beam_Offset_EP = 0,
																		HM_Beam_Length = _1stLth,
																		HM_Beam_FlangeT = _plgThk,
																		HM_Beam_WebT = _webThk,
																		HM_matBeam = HM_matBody2

						!	2nd
						_2ndWdt = 0.150
						_2ndLth = 0.250
						_2ndHgt = 0.328

						addx	_1stLth
							call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _2ndWdt,
																			HM_Beam_Height_SP = _1stHgt,
																			HM_Beam_Height_EP = _2ndHgt,
																			HM_Beam_Offset_SP = 0,
																			HM_Beam_Offset_EP = 0,
																			HM_Beam_Length = _2ndLth,
																			HM_Beam_FlangeT = _plgThk,
																			HM_Beam_WebT = _webThk,
																			HM_matBeam = HM_matBody2
						del 1

						!	Connect Plate/Bar
						_cntLth = 0.300
						_offsetBar = -0.037
						_barRad = 0.025
						_barHgt = 0.375

							addx	_1stLth + _2ndLth
								!	Top/Bottom Plate
								put	0.205,				0.1035016859605,	15,
									0.1500196332745,	0.1049711330425,	900,
									0,					123.9129661009,		4000 + 15,
									0.1205637499439,	0.1514184250757,	15,
									0,					0.075,				15,
									0,					-0.075,				15,
									0.1205920342135,	-0.1514868730031,	15,
									0.150047917544,		-0.1050395809699,	900,
									0,					124.7738563686,		4000 + 15,
									0.205,				-0.1027442305058,	15,
									0.205,				0.1035016859605,	-1,
									0.150,				0.105,				900,
									0.025,				360,				4000 + 15,
									0.150,				-0.105,				900,
									0.025,				360,				4000 + 15

								prism_	nsp/3,	_plgThk,	use(nsp)

								addz	_2ndHgt - _plgThk
									prism_	nsp/3,	_plgThk,	get(nsp)
								del 1
							del 1

							!	Bar
							addz	_offsetBar
								addx	_1stLth + _2ndLth + 0.150
									addy	-0.105
										cylind	_barHgt,	_barRad
									del 1

									addy	0.105
										cylind	_barHgt,	_barRad
									del 1
								del 1
							del 1

						addz	_LegDist
							!	3rd
							_plgThk = 0.009
							_webThk = 0.140 - 0.020

							_3rdLth = 0.095
							_3rdWdt = 0.140
							_3rdHgt = 0.288

							addx	_1stLth + _2ndLth + _cntLth
								!	Top/Bottom Plate
								put	0.205,				0.1035016859605,	15,
									0.1500196332745,	0.1049711330425,	900,
									0,					123.9129661009,		4000 + 15,
									0.1205637499439,	0.1514184250757,	15,
									0,					0.070,				15,
									0,					-0.070,				15,
									0.1205920342135,	-0.1514868730031,	15,
									0.150047917544,		-0.1050395809699,	900,
									0,					124.7738563686,		4000 + 15,
									0.205,				-0.1027442305058,	15,
									0.205,				0.1035016859605,	-1,
									0.150,				0.105,				900,
									0.025,				360,				4000 + 15,
									0.150,				-0.105,				900,
									0.025,				360,				4000 + 15

								rotz	180
									prism_	nsp/3,	_plgThk,	use(nsp)

									addz	_3rdHgt - _plgThk
										prism_	nsp/3,	_plgThk,	get(nsp)
									del 1
								del 1

								put	0,			_webThk/2,	15,
									0,			-_webThk/2,	15,
									-_3rdLth,	-_webThk/2,	15,
									-_3rdLth,	_webThk/2,	15

								addz	_plgThk
									prism_	nsp/3,	_3rdHgt - _plgThk*2,	get(nsp)
								del 1
							del 1

							!	4th
							_4thLth = 0.320
							_4thWdt = 0.140
							_4thHgt = 0.208

							addx	_1stLth + _2ndLth + _cntLth
								call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _4thWdt,
																				HM_Beam_Height_SP = _3rdHgt,
																				HM_Beam_Height_EP = _4thHgt,
																				HM_Beam_Offset_SP = 0,
																				HM_Beam_Offset_EP = 0,
																				HM_Beam_Length = _4thLth,
																				HM_Beam_FlangeT = _plgThk,
																				HM_Beam_WebT = _webThk,
																				HM_matBeam = HM_matBody2
							del 1

							!	5th
							_5thLth = 0.430
							_5thWdt = 0.140
							_5thHgt = 0.208

							addx	_1stLth + _2ndLth + _cntLth + _4thLth
								call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _5thWdt,
																				HM_Beam_Height_SP = _5thHgt,
																				HM_Beam_Height_EP = _5thHgt,
																				HM_Beam_Offset_SP = 0,
																				HM_Beam_Offset_EP = 0,
																				HM_Beam_Length = _5thLth,
																				HM_Beam_FlangeT = _plgThk,
																				HM_Beam_WebT = _webThk,
																				HM_matBeam = HM_matBody2
							del 1

							!	6th
							_6thLth = _LegLth - (_mainCydRad + _1stLth + _2ndLth + _cntLth + _4thLth + _5thLth + 0.050)
							_6thWdt = 0.140
							_6thHgt = 0.130

							addx	_1stLth + _2ndLth + _cntLth + _4thLth + _5thLth
								call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _6thWdt,
																				HM_Beam_Height_SP = _5thHgt,
																				HM_Beam_Height_EP = _6thHgt,
																				HM_Beam_Offset_SP = 0,
																				HM_Beam_Offset_EP = 0,
																				HM_Beam_Length = _6thLth,
																				HM_Beam_FlangeT = _plgThk,
																				HM_Beam_WebT = _webThk,
																				HM_matBeam = HM_matBody2
							del 1
						del 1

					del 1
				del 1

				rotz	90
			next	i

			del 4
		del 1
	endif

	!----------------------------------------------------------------------------------------------------
	!	U-09H
	!----------------------------------------------------------------------------------------------------
	if	HM_OutrType = U_09H	then

		!	Main Cylinder
		!----------------------------------------------------------------------------------------------------
		_offsetMainCyd = 0.009
		_mainCydRad = 0.355
		_mainCydHgt = 0.5005

		addz	_offsetMainCyd
			cylind	_mainCydHgt,	_mainCydRad
		del 1

		!	Sub Cylinder
		!----------------------------------------------------------------------------------------------------
		_offsetSubCyd = _offsetMainCyd + _mainCydHgt
		_cydRad1 = 0.366
		_cydHgt1 = 0.048
		_cydRad2 = 0.3125
		_cydHgt2 = 0.024
		_cydRad3 = 0.305
		_cydHgt3 = 0.222
		_cydRad4 = 0.360
		_cydHgt4 = 0.022

		addz	_offsetSubCyd
			cylind	_cydHgt1,	_cydRad1

			addz	_cydHgt1
				cylind	_cydHgt2,	_cydRad2

				addz	_cydHgt2
					cylind	_cydHgt3,	_cydRad3

					addz	_cydHgt3
						cylind	_cydHgt4,	_cydRad4
					del 1
				del 1
			del 1
		del 1

		!	Leg
		!----------------------------------------------------------------------------------------------------
		rotz	45
			for	i = 1	to	4
				!	Basement Plate/Bar
				addx	HM_OutrLen/2
					!	Basement Plate_Top
					_offsetBsmPlt = 0.340
					_bsmPltThk = 0.019
					_bsmPltLth = 0.420
					_bsmPltWdt = 0.090
					_bsmSubPltThk = 0.040
					_bsmSubPltWdt = 0.075
					_bsmSubPltLth = 0.290
					_OutrSlabThk = 0.200

					addz	_offsetBsmPlt
						gosub	"basePlate_"
					del 1


					addz	-_OutrSlabThk
						mulz	-1
							gosub	"basePlate_"
						del 1
					del 1

					!	Bar
					_offsetBar = -(_OutrSlabThk + 0.061)
					_barRad = 0.010
					_barHgt = _OutrSlabThk + 0.462

					addz	_offsetBar
						addy	-0.175
							cylind	_barHgt,	_barRad
						del 1

						addy	0.175
							cylind	_barHgt,	_barRad
						del 1
					del 1
				del 1

				!	Connect to Leg
				_offsetCntPlt = 0.021
				_xPosCntPlt = 0.050
				_yPosCntPlt = 0.072
				_cntPltHgt = 0.360
				_cntPltLth = 0.120
				_cntPltThk = 0.009

				addz	_offsetCntPlt
					addx	_mainCydRad - _xPosCntPlt
						addy	-_yPosCntPlt
							put	0,					0,					15,
								0,					_cntPltHgt,			15,
								_cntPltLth - 0.055,	_cntPltHgt,			15,
								_cntPltLth - 0.055,	_cntPltHgt - 0.055,	900,
								0.055,				-90,				4000 + 15,
								_cntPltLth,			0.055,				15,
								_cntPltLth - 0.055,	0.055,				900,
								0.055,				-90,				4000 + 15

							rotx	90
								prism_	nsp/3,	_cntPltThk,	use(nsp)
							del 1
						del 1

						addy	_yPosCntPlt
							rotx	90
								mulz	-1
									prism_	nsp/3,	_cntPltThk,	get(nsp)
								del 1
							del 1
						del 1
					del 1
				del 1

				!	Leg
				_offsetLeg = 0.012

				addz	_offsetLeg
					addx	_mainCydRad + 0.015
						_plgThk = 0.009
						_webThk = 0.150 - 0.020

						!	1st
						_1stWdt = 0.150
						_1stLth = 0.100
						_1stHgt = 0.378

						call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _1stWdt,
																		HM_Beam_Height_SP = _1stHgt,
																		HM_Beam_Height_EP = _1stHgt,
																		HM_Beam_Offset_SP = 0,
																		HM_Beam_Offset_EP = 0,
																		HM_Beam_Length = _1stLth,
																		HM_Beam_FlangeT = _plgThk,
																		HM_Beam_WebT = _webThk,
																		HM_matBeam = HM_matBody2

						!	2nd
						_2ndWdt = 0.150
						_2ndLth = 0.250
						_2ndHgt = 0.328

						addx	_1stLth
							call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _2ndWdt,
																			HM_Beam_Height_SP = _1stHgt,
																			HM_Beam_Height_EP = _2ndHgt,
																			HM_Beam_Offset_SP = 0,
																			HM_Beam_Offset_EP = 0,
																			HM_Beam_Length = _2ndLth,
																			HM_Beam_FlangeT = _plgThk,
																			HM_Beam_WebT = _webThk,
																			HM_matBeam = HM_matBody2
						del 1

						!	Connect
						_cntLth = 0.300

						addx	_1stLth + _2ndLth
							!	Top/Bottom Plate
							put	0.205,				0.1035016859605,	15,
								0.1500196332745,	0.1049711330425,	900,
								0,					123.9129661009,		4000 + 15,
								0.1205637499439,	0.1514184250757,	15,
								0,					0.075,				15,
								0,					-0.075,				15,
								0.1205920342135,	-0.1514868730031,	15,
								0.150047917544,		-0.1050395809699,	900,
								0,					124.7738563686,		4000 + 15,
								0.205,				-0.1027442305058,	15,
								0.205,				0.1035016859605,	-1,
								0.150,				0.105,				900,
								0.025,				360,				4000 + 15,
								0.150,				-0.105,				900,
								0.025,				360,				4000 + 15

							prism_	nsp/3,	_plgThk,	use(nsp)

							addz	_2ndHgt - _plgThk
								prism_	nsp/3,	_plgThk,	get(nsp)
							del 1
						del 1

						!	3rd
						_3rdLth = 0.125
						_3rdWdt = 0.150
						_3rdHgt = 0.328

						addx	_1stLth + _2ndLth
							put	0,			_webThk/2,	15,
								0,			-_webThk/2,	15,
								_3rdLth,	-_webThk/2,	15,
								_3rdLth,	_webThk/2,	15

							addz	_plgThk
								prism_	nsp/3,	_3rdHgt - _plgThk*2,	get(nsp)
							del 1
						del 1

					del 1
				del 1


				rotz	90
			next	i

			del 4
		del 1
	endif

	!----------------------------------------------------------------------------------------------------
	!	OJ-30HR
	!----------------------------------------------------------------------------------------------------
	if	HM_OutrType = OJ_30HR	then
		!	Basement
		!----------------------------------------------------------------------------------------------------
		add	-HM_OutrLen/2,	-HM_OutrWidth/2,	0
			gosub	"basement_OJ_30HR"
		del 1
		add	-HM_OutrLen/2,	HM_OutrWidth/2,	0
			gosub	"basement_OJ_30HR"
		del 1
		add	HM_OutrLen/2,	HM_OutrWidth/2,	0
			gosub	"basement_OJ_30HR"
		del 1
		add	HM_OutrLen/2,	-HM_OutrWidth/2,	0
			gosub	"basement_OJ_30HR"
		del 1

		!	Bottom Beam
		!----------------------------------------------------------------------------------------------------
		_plgThk = 0.016
		_webThk = 0.014

		!	HBeam
		add	-HM_OutrLen/2,	-HM_OutrWidth/2,	0
			gosub	"HBeam_OJ_30HR"
		del 1
		add	-HM_OutrLen/2,	HM_OutrWidth/2,	0
			gosub	"HBeam_OJ_30HR"
		del 1

		!	VBeam
		_xPosVBeam = 0.850
		_zPosVBeam = 0.264
		_vBeamWdt = 0.300
		_vBeamLth = 4.700
		_vBeamHgt = 0.390

		add	_xPosVBeam,	_vBeamLth/2,	_zPosVBeam
			rotz	-90
				call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _vBeamWdt,
																HM_Beam_Height_SP = _vBeamHgt,
																HM_Beam_Height_EP = _vBeamHgt,
																HM_Beam_Offset_SP = 0,
																HM_Beam_Offset_EP = 0,
																HM_Beam_Length = _vBeamLth,
																HM_Beam_FlangeT = _plgThk,
																HM_Beam_WebT = _webThk,
																HM_matBeam = HM_matBody2
			del 1
		del 1

		add	-_xPosVBeam,	_vBeamLth/2,	_zPosVBeam
			rotz	-90
				call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _vBeamWdt,
																HM_Beam_Height_SP = _vBeamHgt,
																HM_Beam_Height_EP = _vBeamHgt,
																HM_Beam_Offset_SP = 0,
																HM_Beam_Offset_EP = 0,
																HM_Beam_Length = _vBeamLth,
																HM_Beam_FlangeT = _plgThk,
																HM_Beam_WebT = _webThk,
																HM_matBeam = HM_matBody2
			del 1
		del 1

		!	Oblique Beam
		_zPosObq = 0.654
		_obqPltThk = 0.010
		_xCenter1 = 2.2415
		_yCenter1 = 2.264
		_xCenter2 = 1.100
		_yCenter2 = 0.764
		_obqBeamLth = sqr((_xCenter1 - _xCenter2)^2 + (_yCenter1 - _yCenter2)^2)
		_aObqBeam = atn((_xCenter1 - _xCenter2)/(_yCenter1 - _yCenter2))
		_obqBeamWdt = 0.126
		_obqBeamHgt = 0.126

			!	Front/Right
			gosub	"ObliqueBeam_OJ_30HR"

			!	Front/Left
			muly	-1
				gosub	"ObliqueBeam_OJ_30HR"
			del 1

			!	Back/Right
			mulx	-1
				gosub	"ObliqueBeam_OJ_30HR"
			del 1

			!	Back/Left
			mulx	-1
				muly	-1
					gosub	"ObliqueBeam_OJ_30HR"
				del 1
			del 1

		!	Deck
		!----------------------------------------------------------------------------------------------------
		_zPosDeck = 0.654
		_yPosDeck = 1.010
		_deckThk = 0.016

		addz	_zPosDeck - _deckThk
			put	-0.685,	0,		15,
				-0.685,	1.160,	15,
				-0.610,	1.160,	15,
				-0.610,	1.330,	15,
				0.610,	1.330,	15,
				0.610,	1.160,	15,
				0.685,	1.160,	15,
				0.685,	0,		15

			addy	_yPosDeck
				prism_	nsp/3,	_deckThk,	use(nsp)
			del 1
			addy	-_yPosDeck
				muly	-1
					prism_	nsp/3,	_deckThk,	get(nsp)
				del 1
			del 1
		del 1

		!	Top Beam
		!----------------------------------------------------------------------------------------------------
		_plgThk = 0.014
		_webThk = 0.010

		_yPosTBeam = 0.510
		_zPosTBeam = 0.676
		_tBeamWdt = 0.300
		_tBeamLth = 2.000
		_tBeamHgt = 0.340

		addz	_zPosTBeam
			addx	-_tBeamLth/2
				addy	_yPosTBeam
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _tBeamWdt,
																	HM_Beam_Height_SP = _tBeamHgt,
																	HM_Beam_Height_EP = _tBeamHgt,
																	HM_Beam_Offset_SP = 0,
																	HM_Beam_Offset_EP = 0,
																	HM_Beam_Length = _tBeamLth,
																	HM_Beam_FlangeT = _plgThk,
																	HM_Beam_WebT = _webThk,
																	HM_matBeam = HM_matBody2
				del 1

				addy	-_yPosTBeam
					call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _tBeamWdt,
																	HM_Beam_Height_SP = _tBeamHgt,
																	HM_Beam_Height_EP = _tBeamHgt,
																	HM_Beam_Offset_SP = 0,
																	HM_Beam_Offset_EP = 0,
																	HM_Beam_Length = _tBeamLth,
																	HM_Beam_FlangeT = _plgThk,
																	HM_Beam_WebT = _webThk,
																	HM_matBeam = HM_matBody2
				del 1
			del 1
		del 1

		!	Cylinder
		!----------------------------------------------------------------------------------------------------
		_zPosCyd = 1.016
		_cydRad1 = 0.6375
		_cydHgt1 = 0.020
		_cydRad2 = 0.660
		_cydHgt2 = 0.077
		_cydRad3 = 0.585
		_cydHgt3 = 0.036

		addz	_zPosCyd
			cylind	_cydHgt1,	_cydRad1

			addz	_cydHgt1
				cylind	_cydHgt2,	_cydRad2

				addz	_cydHgt2
					cylind	_cydHgt3,	_cydRad3
				del 1
			del 1
		del 1

		!	Ladder
		!----------------------------------------------------------------------------------------------------
		_yPosLdr = 2.800
		_zPosLdr = 0.034
		_ldrRad = 0.0135
		_ldrHgt = 0.695
		_ldrLth = 0.430
		_ldrWdt = 0.400

			!	Vertical Bar
				!	Section
				put	0,			0,		900,
					_ldrRad,	360,	4000

				_nPoly = 2

				!	Path
				put	0,	0,				_zPosLdr - 1,			0,
					0,	0,				_zPosLdr,				0,
					0,	0,				_zPosLdr + _ldrHgt,		0,
					0,	_ldrLth,		_zPosLdr + _ldrHgt,		0,
					0,	_ldrLth + 1,	_zPosLdr + _ldrHgt,		0

				_nPath = 5

				!	Bottom
				addy	-_yPosLdr
					addx	_ldrWdt/2
						tube	_nPoly,	_nPath,	1+2+16+32,	use(nsp)
					del 1

					addx	-_ldrWdt/2
						tube	_nPoly,	_nPath,	1+2+16+32,	use(nsp)
					del 1
				del 1

				!	Top
				addy	_yPosLdr
					muly	-1
						addx	_ldrWdt/2
							tube	_nPoly,	_nPath,	1+2+16+32,	use(nsp)
						del 1

						addx	-_ldrWdt/2
							tube	_nPoly,	_nPath,	1+2+16+32,	get(nsp)
						del 1
					del 1
				del 1

			!	Horizontal Bar
				addz	_zPosLdr + 0.020
					!	Bottom
					addy	-_yPosLdr
						addx	-_ldrWdt/2
							roty	90
								cylind	_ldrWdt,	_ldrRad
							del 1

							addz	0.300
								roty	90
									cylind	_ldrWdt,	_ldrRad
								del 1

								addz	0.375
									addy	0.400
										roty	90
											cylind	_ldrWdt,	_ldrRad
										del 1
									del 1
								del 1
							del 1
						del 1
					del 1

					!	Top
					addy	_yPosLdr
						addx	-_ldrWdt/2
							roty	90
								cylind	_ldrWdt,	_ldrRad
							del 1

							addz	0.300
								roty	90
									cylind	_ldrWdt,	_ldrRad
								del 1

								addz	0.375
									addy	-0.400
										roty	90
											cylind	_ldrWdt,	_ldrRad
										del 1
									del 1
								del 1
							del 1
						del 1
					del 1
				del 1
	endif





!======================================================================================================
end
!======================================================================================================

"wheel_":

	!	Cap Left/Right
	addy	-_wheelCapWdt/2

		if	_wheelType = 1	then
			put	-_wheelCapLth/2,	0,				15,
				-_wheelCapLth/2,	-_wheelCapHgt,	15,
				_wheelCapLth/2,		-_wheelCapHgt,	15,
				_wheelCapLth/2,		0,				15,
				-_wheelCapLth/2,	0,				-1
		endif

		if	_wheelType = 2	then
			put	-_wheelCapLth/2,					0,					15,
				-_wheelCapLth/2,					-_wheelCapHgt,		15,
				_wheelCapLth/2,						-_wheelCapHgt,		15,
				_wheelCapLth/2 + _exWheelCapLth,	-_exWheelCapHgt,	15,
				_wheelCapLth/2 + _exWheelCapLth,	0,					15,
				-_wheelCapLth/2,					0,					-1
		endif

		!	Hole
		put	-_wheelCenterX,	-_wheelCenterY,	900,
			_holeRad,		360,			4000+15,
			_wheelCenterX,	-_wheelCenterY,	900,
			_holeRad,		360,			4000+15

		rotx	-90
			prism_	nsp/3,	_wheelCapThk,	use(nsp)
		del 1

		addy	_wheelCapWdt - _wheelCapThk
			rotx	-90
				prism_	nsp/3,	_wheelCapThk,	get(nsp)
			del 1
		del 1
	del 1

	!	Cap Roof
	addz	_wheelCapHgt - _wheelCapThk
		put	-_wheelCapLth/2,	-(_wheelCapWdt/2 - _wheelCapThk),	15,
			-_wheelCapLth/2,	(_wheelCapWdt/2 - _wheelCapThk),	15,
			_wheelCapLth/2,		(_wheelCapWdt/2 - _wheelCapThk),	15,
			_wheelCapLth/2,		-(_wheelCapWdt/2 - _wheelCapThk),	15


		prism_	nsp/3,	_wheelCapThk,	get(nsp)
	del 1

	!	Wheel
	addy	-_wheelCapWdt/2
		add	-_wheelCenterX,	0,	_wheelCenterY
			rotx	-90
				cylind	_wheelCapWdt,	_holeRad

				addz	_wheelCapWdt/2 - _wheelThk/2
					put	0,			0,		900+15,
						_wheelRad,	360,	4000

					prism_	nsp/3,	_wheelThk,	get(nsp)
				del 1
			del 1
		del 1

		add	_wheelCenterX,	0,	_wheelCenterY
			rotx	-90
				cylind	_wheelCapWdt,	_holeRad

				addz	_wheelCapWdt/2 - _wheelThk/2
					put	0,			0,		900+15,
						_wheelRad,	360,	4000

					prism_	nsp/3,	_wheelThk,	get(nsp)
				del 1
			del 1
		del 1
	del 1

	!	Column
	addz	_wheelCapHgt
		cone	_colBHgt,	_colBRad1,	_colBRad2,	90,	90

		addz	_colBHgt
			cylind	_colTHgt,	_colTRad
		del 1
	del 1

return

"obliqueBeam_OTA_150S":
	!	Cut
	addx	_beamHWdt/2
		roty	90
			cutplane	1,	1,	0,	1
		del 1
	del 1

	addy	HM_OutrWidth/2 - _beamVLct - _beamVWdt/2
		rotx	90
			cutplane	1,	1,	0,	1
		del 1
	del 1

		rotz	_aBeam
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _olqBeamWdt,
															HM_Beam_Height_SP = _olqBeamHgt,
															HM_Beam_Height_EP = _olqBeamHgt,
															HM_Beam_Offset_SP = 0,
															HM_Beam_Offset_EP = 0,
															HM_Beam_Length = _olqBeamLth,
															HM_Beam_FlangeT = _olqBeamFlangeThk,
															HM_Beam_WebT = _olqBeamWebThk,
															HM_matBeam = HM_matBody2
		del 1

	cutend
	cutend

return

"btmHLadder_":

	!	Vertical Bar
	addx	-_btmHLdrWdt/2
		cylind	_btmHLdrHgt,	_btmHLdrRad
	del 1

	addx	_btmHLdrWdt/2
		cylind	_btmHLdrHgt,	_btmHLdrRad
	del 1

	!	Horizontal Bar
	addx	-_btmHLdrWdt/2
		addz	0.050
			roty	90
				cylind	_btmHLdrWdt,	_btmHLdrRad
			del 1

			addz	_btmHLdrDist
				roty	90
					cylind	_btmHLdrWdt,	_btmHLdrRad
				del 1

				addz	_btmHLdrDist
					roty	90
						cylind	_btmHLdrWdt,	_btmHLdrRad
					del 1
				del 1
			del 1
		del 1
	del 1

	!	Oblique Bar
	addz	_btmHLdrHgt
		addx	-_btmHLdrWdt/2
			roty	-_btmHLdrAng
				cylind	_btmHLdrObqHgt,	_btmHLdrRad
			del 1
		del 1

		addx	_btmHLdrWdt/2
			roty	_btmHLdrAng
				cylind	_btmHLdrObqHgt,	_btmHLdrRad
			del 1
		del 1
	del 1

return

"btmVRail_":

	addx	_xPosBtmBoardV - _btmBoardVLth/2
		addy	-(_beamVLct + _beamVWdt/2 + _btmBoardVDist)
			rotz	-90
				call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																HM_GRailRef = 3,
																HM_GRailL = _btmBoardVWdt,
																HM_GRailH = _btmVRailHgt,
																HM_GRailD = _btmVRailDpt,
																HM_GRailT = _btmVRailThk,
																HM_GRail_colHide_Left = 0,
																HM_GRail_colHide_Right = 1,
																HM_GRail_cutRight_ang = 45,
																HM_GRailhori_show = 0,
																HM_matGRail = HM_matBody2
			del 1
		del 1

		addy	-(_beamVLct + _beamVWdt/2 + _btmBoardVDist + _btmBoardVWdt)
			call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
															HM_GRailRef = 3,
															HM_GRailL = _btmBoardVLth,
															HM_GRailH = _btmVRailHgt,
															HM_GRailD = _btmVRailDpt,
															HM_GRailT = _btmVRailThk,
															HM_GRailDist = 1.200,
															HM_GRail_colHide_Left = 1,
															HM_GRail_colHide_Right = 1,
															HM_GRail_cutLeft_ang = 45,
															HM_GRail_cutRight_ang = 45,
															HM_GRailhori_show = 0,
															HM_matGRail = HM_matBody2

			addx	_btmBoardVLth
				rotz	90
					call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 2,
																	HM_GRailRef = 3,
																	HM_GRailL = _btmBoardVWdt,
																	HM_GRailH = _btmVRailHgt,
																	HM_GRailD = _btmVRailDpt,
																	HM_GRailT = _btmVRailThk,
																	HM_GRail_colHide_Left = 1,
																	HM_GRail_colHide_Right = 0,
																	HM_GRail_cutLeft_ang = 45,
																	HM_GRailhori_show = 0,
																	HM_matGRail = HM_matBody2
				del 1
			del 1
		del 1
	del 1

return

"topLdr_":

	!	Vertical Bar
	cylind	_topLdrHgt1,	_topLdrRad

	addz	_topLdrHgt1
		elbow	_topLdrElb,	180,	_topLdrRad

		add	_topLdrElb*2,	0,	-_topLdrHgt2
			cylind	_topLdrHgt2,	_topLdrRad
		del 1
	del 1

return

"basePlate_":

	put	-_bsmPltWdt/2,	-_bsmPltLth/2,	15,
		-_bsmPltWdt/2,	_bsmPltLth/2,	15,
		_bsmPltWdt/2,	_bsmPltLth/2,	15,
		_bsmPltWdt/2,	-_bsmPltLth/2,	15,
		-_bsmPltWdt/2,	-_bsmPltLth/2,	-1,
		0,				0.175,			900,
		0.010,			360,			4000+15,
		0,				-0.175,			900,
		0.010,			360,			4000+15

	prism_	nsp/3,	_bsmPltThk,	get(nsp)

	addz	_bsmPltThk
		put	-_bsmSubPltWdt/2,	-_bsmSubPltLth/2,	15,
			-_bsmSubPltWdt/2,	_bsmSubPltLth/2,	15,
			_bsmSubPltWdt/2,	_bsmSubPltLth/2,	15,
			_bsmSubPltWdt/2,	-_bsmSubPltLth/2,	15

		prism_	nsp/3,	_bsmSubPltThk,	get(nsp)
	del 1

return

"basement_OJ_30HR":

	!	Basement Plate
	_zPosBsmPlt = 0.013
	_bsmPltWdt = 0.200
	_bsmPltThk = 0.008

	addz	_zPosBsmPlt
		put	-_bsmPltWdt/2,	-_bsmPltWdt/2,	15,
			-_bsmPltWdt/2,	_bsmPltWdt/2,	15,
			_bsmPltWdt/2,	_bsmPltWdt/2,	15,
			_bsmPltWdt/2,	-_bsmPltWdt/2,	15

		prism_	nsp/3,	_bsmPltThk,	get(nsp)
	del 1

	!	Basement Around
		!	Section
		put	0,	0,	0,
			-0.025,	-0.013,	0,
			-0.050,	0,		0,
			-0.050,	-0.008,	0,
			-0.025,	-0.021,	0,
			0,		-0.008,	0

		_nPoly = 6

		!	Path
		put	-_bsmPltWdt/2,	_bsmPltWdt/2,	0,	0,
			-_bsmPltWdt/2,	-_bsmPltWdt/2,	0,	0,
			_bsmPltWdt/2,	-_bsmPltWdt/2,	0,	0,
			_bsmPltWdt/2,	_bsmPltWdt/2,	0,	0,
			-_bsmPltWdt/2,	_bsmPltWdt/2,	0,	0,
			-_bsmPltWdt/2,	-_bsmPltWdt/2,	0,	0,
			_bsmPltWdt/2,	-_bsmPltWdt/2,	0,	0

		_nPath = 7

		addz	_zPosBsmPlt + _bsmPltThk
			tube	_nPoly,	_nPath,	1+2+16+32,	get(nsp)
		del 1

	!	Support Bar
	_zPosSptBar = _zPosBsmPlt + _bsmPltThk
	_SptBarRad1 = 0.060
	_SptBarHgt1 = 0.060
	_SptBarRad2 = 0.020
	_SptBarHgt2 = 0.018
	_SptBarRad3 = 0.031
	_SptBarHgt3 = 0.255
	_SptBarRad4 = 0.031
	_SptBarHgt4 = 0.065

	addz	_zPosSptBar
		cylind	_SptBarHgt1,	_SptBarRad1

		addz	_SptBarHgt1
			cylind	_SptBarHgt2,	_SptBarRad2

			addz	_SptBarHgt2
				cylind	_SptBarHgt3,	_SptBarRad3

				addz	_SptBarHgt3 + 0.300
					cylind	_SptBarHgt4,	_SptBarRad4
				del 1
			del 1
		del 1
	del 1

return

"HBeam_OJ_30HR":
	!	1st
	_xPos1st = 0.175
	_zPos1st = 0.354
	_1stWdt = 0.300
	_1stLth = 0.550
	_1stHgt = 0.300

	addz	_zPos1st
		addx	-_xPos1st
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _1stWdt,
															HM_Beam_Height_SP = _1stHgt,
															HM_Beam_Height_EP = _1stHgt,
															HM_Beam_Offset_SP = 0,
															HM_Beam_Offset_EP = 0,
															HM_Beam_Length = _1stLth,
															HM_Beam_FlangeT = _plgThk,
															HM_Beam_WebT = _webThk,
															HM_matBeam = HM_matBody2
		del 1
	del 1

	!	2nd
	_zPos2nd = 0.264
	_2ndDist = 0.090
	_2ndWdt = 0.300
	_2ndLth = 0.450
	_2ndHgt = 0.390

	addz	_zPos2nd
		addx	-_xPos1st + _1stLth
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _2ndWdt,
															HM_Beam_Height_SP = _1stHgt,
															HM_Beam_Height_EP = _2ndHgt,
															HM_Beam_Offset_SP = _2ndDist,
															HM_Beam_Offset_EP = 0,
															HM_Beam_Length = _2ndLth,
															HM_Beam_FlangeT = _plgThk,
															HM_Beam_WebT = _webThk,
															HM_matBeam = HM_matBody2
		del 1
	del 1

	!	3rd
	_zPos3rd = 0.264
	_3rdWdt = 0.300
	_3rdLth = 3.350
	_3rdHgt = 0.390

	addz	_zPos3rd
		addx	-_xPos1st + _1stLth + _2ndLth
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _3rdWdt,
															HM_Beam_Height_SP = _3rdHgt,
															HM_Beam_Height_EP = _3rdHgt,
															HM_Beam_Offset_SP = 0,
															HM_Beam_Offset_EP = 0,
															HM_Beam_Length = _3rdLth,
															HM_Beam_FlangeT = _plgThk,
															HM_Beam_WebT = _webThk,
															HM_matBeam = HM_matBody2
		del 1
	del 1

	!	4th
	_zPos4th = 0.264
	_4thDist = 0.090
	_4thWdt = 0.300
	_4thLth = 0.450
	_4thHgt = 0.300

	addz	_zPos4th
		addx	-_xPos1st + _1stLth + _2ndLth + _3rdLth
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _4thWdt,
															HM_Beam_Height_SP = _3rdHgt,
															HM_Beam_Height_EP = _4thHgt,
															HM_Beam_Offset_SP = 0,
															HM_Beam_Offset_EP = _4thDist,
															HM_Beam_Length = _4thLth,
															HM_Beam_FlangeT = _plgThk,
															HM_Beam_WebT = _webThk,
															HM_matBeam = HM_matBody2
		del 1
	del 1

	!	5th
	_zPos5th = 0.354
	_5thWdt = 0.300
	_5thLth = 0.550
	_5thHgt = 0.300

	addz	_zPos5th
		addx	-_xPos1st + _1stLth + _2ndLth + _3rdLth + _4thLth
			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width = _5thWdt,
															HM_Beam_Height_SP = _5thHgt,
															HM_Beam_Height_EP = _5thHgt,
															HM_Beam_Offset_SP = 0,
															HM_Beam_Offset_EP = 0,
															HM_Beam_Length = _5thLth,
															HM_Beam_FlangeT = _plgThk,
															HM_Beam_WebT = _webThk,
															HM_matBeam = HM_matBody2
		del 1
	del 1

return

"ObliqueBeam_OJ_30HR":

	add	_xCenter1,	-_yCenter1,	_zPosObq
		put	0.070,	0,			15,
			0,		0,			900,
			0.070,	180,		4000 + 15,
			-0.070,	0,			15,
			-0.070,	-0.136,		15,
			0.130,	-0.136,		15,
			0.130,	-0.086,		15,
			0.070,	0,			-1,
			0,		0,			900,
			0.020,	360,		4000 + 15

		prism_	nsp/3,	_obqPltThk,	get(nsp)

		put	-_obqBeamWdt/2,	0,				15,
			-_obqBeamWdt/2,	_obqBeamLth,	15,
			_obqBeamWdt/2,	_obqBeamLth,	15,
			_obqBeamWdt/2,	0,				15

		rotz	_aObqBeam
			addz	-_obqBeamHgt
				prism_	nsp/3,	_obqBeamHgt,	get(nsp)
			del 1
		del 1

	del 1

	add	_xCenter2,	-_yCenter2,	_zPosObq
		put	0.070,	0,			15,
			0,		0,			900,
			0.070,	180,		4000 + 15,
			-0.070,	0,			15,
			-0.070,	-0.136,		15,
			0.130,	-0.136,		15,
			0.130,	-0.086,		15,
			0.070,	0,			-1,
			0,		0,			900,
			0.020,	360,		4000 + 15

		rotz	-90
			prism_	nsp/3,	_obqPltThk,	get(nsp)
		del 1
	del 1

return