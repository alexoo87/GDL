!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	DATE : 2017/12/22 THU
!	VER : AC 20
!	DEV : LEE HAE CHAN


!----------------------------------------------------------------------------------------------------
!	TEST
!----------------------------------------------------------------------------------------------------
if	bOrigin	then
	hotspot	0, 0, 0
	model	wire
	circle	0.1
	circle	0.2
	model	solid
endif

if	bTest	then

endif

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------





!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
resol HM_3D_resol
pen		HM_3D_pen

if	GLOB_FEEDBACK_MODE	then
	resol	8
endif

!	Set Material
if	HM_matWarning = ""	then
	material HM_matBody2
else
	material HM_matWarning
endif

!----------------------------------------------------------------------------------------------------
!	Shape - Cylinder
!----------------------------------------------------------------------------------------------------
if	HM_MastType = MAST_CYLIND	then

	for	i = 1	to	HM_MastNumber
		cylind	HM_MastHeight,	HM_MastRadius

		addz	HM_MastHeight
	next	i

	del HM_MastNumber

endif

!-----------------------------------------------------------------------------------------------
!	Shape - Rectangle Type 1
!-----------------------------------------------------------------------------------------------
if	HM_MastType = MAST_RECT_TYPE1	then
	!	Value
	_plateThk = HM_PlateThk
	_columnHgt = HM_MastHeight - _plateThk*2

	group	"MastRectType1_Base"
		!	Base Plate(Bottom)
		for	i = 1	to	4
			gosub	"coordinate_BasePlate_Type1"

			prism_	nsp/3,	_plateThk,	get(nsp)

			rotz	90
		next	i

		del 4

		!	Base Column
		addz	_plateThk
			for	i = 1	to	4
				put	HM_MastWidth/2,				HM_MastWidth/2,				15,
					HM_MastWidth/2,				HM_MastWidth/2 - HM_ColWdt,	15,
					HM_MastWidth/2 - HM_ColThk,	HM_MastWidth/2 - HM_ColWdt,	15,
					HM_MastWidth/2 - HM_ColThk,	HM_MastWidth/2 - HM_ColThk,	15,
					HM_MastWidth/2 - HM_ColWdt,	HM_MastWidth/2 - HM_ColThk,	15,
					HM_MastWidth/2 - HM_ColWdt,	HM_MastWidth/2,				15

				prism_	nsp/3,	_columnHgt,	get(nsp)

				if	bInnerColumn	then
					put	HM_MastWidth/2 - HM_ColWdt*0.875,					HM_MastWidth/2 - HM_ColWdt*0.875,					15,
						HM_MastWidth/2 - HM_ColWdt*0.875,					HM_MastWidth/2 - HM_ColThk,							15,
						HM_MastWidth/2 - HM_ColWdt*0.875 + HM_ColThk*0.75,	HM_MastWidth/2 - HM_ColThk,							15,
						HM_MastWidth/2 - HM_ColWdt*0.875 + HM_ColThk*0.75,	HM_MastWidth/2 - HM_ColWdt*0.875 + HM_ColThk*0.75,	15,
						HM_MastWidth/2 - HM_ColThk,							HM_MastWidth/2 - HM_ColWdt*0.875 + HM_ColThk*0.75,	15,
						HM_MastWidth/2 - HM_ColThk,							HM_MastWidth/2 - HM_ColWdt*0.875,					15

					prism_	nsp/3,	_columnHgt,	get(nsp)
				endif

				rotz	90
			next	i

			del 4
		del 1

		!	Base Side Plate(Bottom)
		addz	_plateThk
			for	i = 1	to	4
				addx	HM_MastWidth/2
					roty	-90

						put	0,				-(HM_MastWidth/2 - HM_ColWdt),			15,
							HM_PlateWdt,	-(HM_MastWidth/2 - HM_ColWdt),			15,
							HM_PlateWdt,	-(HM_MastWidth/2 - HM_ColWdt) + 0.020,	15,
							0.090,			-(HM_MastWidth/2 - HM_PlateWdt),		15,
							0,				-(HM_MastWidth/2 - HM_PlateWdt),		15

						prism_	nsp/3,	0.025,	use(nsp)

						muly	-1
							prism_	nsp/3,	0.025,	get(nsp)
						del 1

					del 1
				del 1

				rotz	90
			next	i

			del 4
		del 1

		!	Base Side Plate(Top)
		addz	_plateThk + _columnHgt
			for	i = 1	to	4
				addx	HM_MastWidth/2
					roty	90

						put	0,				-(HM_MastWidth/2 - HM_ColWdt),			15,
							0,				-(HM_MastWidth/2 - HM_PlateWdt),		15,
							HM_PlateWdt,	-(HM_MastWidth/2 - HM_ColWdt) + 0.020,	15,
							HM_PlateWdt,	-(HM_MastWidth/2 - HM_ColWdt),			15

						prism_	nsp/3,	-0.025,	use(nsp)

						muly	-1
							prism_	nsp/3,	-0.025,	get(nsp)
						del 1

					del 1
				del 1

				rotz	90
			next	i

			del 4
		del 1

		!	Base Plate(Top)
		addz	_plateThk + _columnHgt
			for	i = 1	to	4
				gosub	"coordinate_BasePlate_Type1"

				prism_	nsp/3,	_plateThk,	get(nsp)

				rotz	90
			next	i

			del 4
		del 1
	endgroup

	group	"MastRectType1_Side"
		if	not(GLOB_FEEDBACK_MODE)	then
			addz	_plateThk
				!	1st
				for	i = 1	to	4
					addx	HM_MastWidth/2
						roty	-90
							!	Side Plate(Bottom)
							put	-0.010,	-(HM_MastWidth/2 - HM_PlateWdt),	15,
								-0.010,	(HM_MastWidth/2 - HM_PlateWdt),		15,
								0.090,	(HM_MastWidth/2 - HM_PlateWdt),		15,
								0.090,	-(HM_MastWidth/2 - HM_PlateWdt),	15

							prism_	nsp/3,	0.025,	get(nsp)

							!	대각선 Plate
							_zPosSidePlate = 1.070
							_tempValue = (_zPosSidePlate - 0.070)/(HM_MastWidth/2 - HM_ColWdt)
							_aObl = atn(_tempValue)
							_oblWdt = 0.130
							_yReferLth = (_zPosSidePlate - _oblWdt/cos(_aObl))
							_xReferLth = (HM_MastWidth/2 - HM_ColWdt)

							put	0,						-(HM_MastWidth/2 - HM_ColWdt),							15,
								_xReferLth*tan(_aObl),	0,														15,
								0,						(HM_MastWidth/2 - HM_ColWdt),							15,
								_oblWdt/cos(_aObl),		(HM_MastWidth/2 - HM_ColWdt),							15,
								_zPosSidePlate,			(HM_MastWidth/2 - HM_ColWdt) - _yReferLth/tan(_aObl),	15,
								_zPosSidePlate,			-(HM_MastWidth/2 - HM_ColWdt) + _yReferLth/tan(_aObl),	15,
								_oblWdt/cos(_aObl),		-(HM_MastWidth/2 - HM_ColWdt),							15

							prism_	nsp/3,	0.025,	get(nsp)

							!	Side Plate(Top)
							addx	_zPosSidePlate
								put	0,		-(HM_MastWidth/2 - HM_ColWdt),	15,
									0,		(HM_MastWidth/2 - HM_ColWdt),	15,
									0.130,	(HM_MastWidth/2 - HM_ColWdt),	15,
									0.130,	-(HM_MastWidth/2 - HM_ColWdt),	15

								prism_	nsp/3,	0.025,	get(nsp)
							del 1
						del 1
					del 1

					rotz	90
				next	i

				del 4

				!	2nd~
				_modHgt = 1.500
				_nSidePlate = int((_columnHgt - _zPosSidePlate - _oblWdt)/_modHgt)
				_rSidePlate = fra((_columnHgt - _zPosSidePlate - _oblWdt)/_modHgt)

				if	_nSidePlate > eps	then
					addz	_zPosSidePlate + _oblWdt
						for	n = 1	to	_nSidePlate
							for	i = 1	to	4
								addx	HM_MastWidth/2
									roty	-90
										!	대각선 Plate
										_zPosSidePlate = 1.370
										_tempValue = (_zPosSidePlate - 0.070)/(HM_MastWidth/2 - HM_ColWdt)
										_aObl = atn(_tempValue)
										_oblWdt = 0.130
										_yReferLth = (_zPosSidePlate - _oblWdt/cos(_aObl))
										_xReferLth = (HM_MastWidth/2 - HM_ColWdt)

										put	0,						-(HM_MastWidth/2 - HM_ColWdt),							15,
											_xReferLth*tan(_aObl),	0,														15,
											0,						(HM_MastWidth/2 - HM_ColWdt),							15,
											_oblWdt/cos(_aObl),		(HM_MastWidth/2 - HM_ColWdt),							15,
											_zPosSidePlate,			(HM_MastWidth/2 - HM_ColWdt) - _yReferLth/tan(_aObl),	15,
											_zPosSidePlate,			-(HM_MastWidth/2 - HM_ColWdt) + _yReferLth/tan(_aObl),	15,
											_oblWdt/cos(_aObl),		-(HM_MastWidth/2 - HM_ColWdt),							15

										prism_	nsp/3,	0.025,	get(nsp)

										!	Side Plate(Top)
										addx	_zPosSidePlate
											put	0,		-(HM_MastWidth/2 - HM_ColWdt),	15,
												0,		(HM_MastWidth/2 - HM_ColWdt),	15,
												0.130,	(HM_MastWidth/2 - HM_ColWdt),	15,
												0.130,	-(HM_MastWidth/2 - HM_ColWdt),	15

											prism_	nsp/3,	0.025,	get(nsp)
										del 1
									del 1
								del 1

								rotz	90
							next	i

							del 4

							addz	_modHgt
						next	n

						del _nSidePlate
					del 1
				endif

				!	Remain
				if	_rSidePlate > HM_PlateWdt	then
					addz	1.200 + 1.500*(_nSidePlate)
						for	i = 1	to	4
							addx	HM_MastWidth/2
								roty	-90
									!	대각선 Plate
									_zPosSidePlate = _rSidePlate - 0.130
									_tempValue = (_zPosSidePlate - 0.070)/(HM_MastWidth/2 - HM_ColWdt)
									_aObl = atn(_tempValue)
									_oblWdt = 0.130
									_yReferLth = (_zPosSidePlate - _oblWdt/cos(_aObl))
									_xReferLth = (HM_MastWidth/2 - HM_ColWdt)

									put	0,						-(HM_MastWidth/2 - HM_ColWdt),							15,
										_xReferLth*tan(_aObl),	0,														15,
										0,						(HM_MastWidth/2 - HM_ColWdt),							15,
										_oblWdt/cos(_aObl),		(HM_MastWidth/2 - HM_ColWdt),							15,
										_zPosSidePlate,			(HM_MastWidth/2 - HM_ColWdt) - _yReferLth/tan(_aObl),	15,
										_zPosSidePlate,			-(HM_MastWidth/2 - HM_ColWdt) + _yReferLth/tan(_aObl),	15,
										_oblWdt/cos(_aObl),		-(HM_MastWidth/2 - HM_ColWdt),							15

									prism_	nsp/3,	0.025,	get(nsp)

									!	Side Plate(Top)
									addx	_zPosSidePlate
										put	0,		-(HM_MastWidth/2 - HM_ColWdt),	15,
											0,		(HM_MastWidth/2 - HM_ColWdt),	15,
											0.130,	(HM_MastWidth/2 - HM_ColWdt),	15,
											0.130,	-(HM_MastWidth/2 - HM_ColWdt),	15

										prism_	nsp/3,	0.025,	get(nsp)
									del 1
								del 1
							del 1

							rotz	90
						next	i

						del 4


					del 1
				endif
			del 1
		endif
	endgroup

	!	배치
	for	i = 1	to	HM_MastNumber
		placegroup	"MastRectType1_Base"
		placegroup	subgroup("MastRectType1_Side", "MastRectType1_Base")

		addz	HM_MastHeight
	next	i

	del HM_MastNumber

endif

!-----------------------------------------------------------------------------------------------
!	Shape - Rectangle Type 2
!-----------------------------------------------------------------------------------------------
if	HM_MastType = MAST_RECT_TYPE2	then
	!	Value
	_plateThk = 0.048
	_columnRad = 0.120

	group	"MastRectType2_Base"
		!	Base Plate(Bottom)
		for	i = 1	to	4
			add	HM_MastWidth/2,	HM_MastWidth/2,	0
				cylind	HM_MastHeight,	_columnRad
			del 1

			rotz	90
		next	i
	endgroup

	group	"MastRectType2_Side"
		if	not(GLOB_FEEDBACK_MODE)	then
			!	Beam
				!	Start Point
				_zPosBeam = 0.250
				gosub	"drawHBeam_Type2"

				!	End Point
				_zPosBeam = HM_MastHeight - (0.250 + 0.200)
				gosub	"drawHBeam_Type2"

				!	Middle
				_midLth = HM_MastHeight - (0.250 + 0.200)*2

				_zPosBeam = (0.250 + 0.200) + _midLth*0.4 - 0.100
				gosub	"drawHBeam_Type2"

			!	Oblique Beam
			_oblBeamRad = 0.083
				!	1st
				_tempValue = (HM_MastWidth/2)/(_midLth*0.4 - 0.100)
				_aObl = atn(_tempValue)
				_zPosOblBeam = (0.250 + 0.200)
				_oblBeamHgt = (_midLth*0.4 - 0.100)

				gosub	"drawOblBeam_Type2"

				!	2nd
				_tempValue = (HM_MastWidth/2)/(_midLth*0.6 - 0.100)
				_aObl = atn(_tempValue)
				_zPosOblBeam = (0.250 + 0.200) + _midLth*0.4 + 0.100
				_oblBeamHgt = (_midLth*0.6 - 0.100)

				gosub	"drawOblBeam_Type2"
		endif
	endgroup

	!	배치
	for	i = 1	to	HM_MastNumber
		placegroup	"MastRectType2_Base"
		placegroup	subgroup("MastRectType2_Side", "MastRectType2_Base")

		addz	HM_MastHeight
	next	i

	del HM_MastNumber
endif

!======================================================================================================
end
!======================================================================================================

!	Coordinates of Base Plate(Mast Type : Cylinder)
!-----------------------------------------------------------------------------------------------
"coordinate_BasePlate_Type1":

	put	HM_MastWidth/2,						HM_MastWidth/2,						15,
		HM_MastWidth/2,						HM_MastWidth/2 - HM_PlateWdt,		15,
		HM_MastWidth/2 - HM_PlateWdt*0.24,	HM_MastWidth/2 - HM_PlateWdt,		15,
		HM_MastWidth/2 - HM_PlateWdt,		HM_MastWidth/2 - HM_PlateWdt*0.24,	15,
		HM_MastWidth/2 - HM_PlateWdt,		HM_MastWidth/2,						15

return

!	H-Beam(Mast Type : Rectangle Type2)
!-----------------------------------------------------------------------------------------------
"drawHBeam_Type2":

	addz	_zPosBeam
		for	i = 1	to	4
			add	-HM_MastWidth/2,	HM_MastWidth/2,	0
				call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
																HM_Beam_Width_EP = 0.200,
																HM_Beam_Height_SP = 0.200,
																HM_Beam_Height_EP = 0.200,
																HM_Beam_Length = HM_MastWidth,
																HM_Beam_FlangeT = 0.012,
																HM_Beam_WebT = 0.009,		!	임의 값
																HM_matBeam = HM_matBody2
			del 1

			rotz	90
		next	i

		del 4
	del 1

return

!	Oblique Beam(Mast Type : Rectangle Type2)
!-----------------------------------------------------------------------------------------------
"drawOblBeam_Type2":

	_oblBeamLth = (HM_MastWidth/2)/sin(_aObl)

	addz	_zPosOblBeam

		for	i = 1	to	4
			addy	HM_MastWidth/2
				!	Left
				roty	90
					cutplane	1,	1,	0,	0
				del 1
				addz	_oblBeamHgt
					cutplane	1,	1,	0,	0
				del 1

					addx	-HM_MastWidth/2
						roty	_aObl
							cylind	_oblBeamLth,	_oblBeamRad
						del 1
					del 1

				cutend
				cutend

				!	Right
				roty	-90
					cutplane	1,	1,	0,	0
				del 1
				addz	_oblBeamHgt
					cutplane	1,	1,	0,	0
				del 1

					addx	HM_MastWidth/2
						roty	-_aObl
							cylind	_oblBeamLth,	_oblBeamRad
						del 1
					del 1

				cutend
				cutend
			del 1

			rotz	90
		next	i

		del 4

	del 1


return