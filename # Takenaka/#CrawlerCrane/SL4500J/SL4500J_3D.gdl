!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

	!	Crawler Wheel
	!-----------------------------------------------------------------------------------------------
		rotz	-90
			call	"HM_mc_Wheel.gsm"	parameters	all	HM_wheelHeight_total = 1.753,
														HM_wheelWidth = 1.219,
														HM_wheelLen = 9.310,
														HM_wheelDistance = 6.281,
														HM_wheelBeltThk = 0.119
		del 1

	!	Connector to Main Object - Part1
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.622

		_ConnectThk = 0.921

		addz	_zPosConnect
			put	-1.050,	-3.140,	15,
				-1.100,	-2.965,	15,
				-1.500,	-2.965,	15,
				-1.500,	2.965,	15,
				-1.100,	2.965,	15,
				-1.050,	3.140,	15,
				1.050,	3.140,	15,
				1.100,	2.965,	15,
				1.500,	2.965,	15,
				1.500,	-2.965,	15,
				1.100,	-2.965,	15,
				1.050,	-3.140,	15

			prism_	nsp/3,	_ConnectThk,	get(nsp)
		del 1

rotz	HM_WorkingAng
	!	Connector to Main Object - Part2
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.622

		_ConnectThk = 0.921

		addz	_zPosConnect
			addz	_ConnectThk
				cylind	0.552,	1.476
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		dim	_coorGR[][]

		_zPosMain = 2.095
		_mainDpt = 2.990

		addz	_zPosMain
			!	Main Object
			addy	_mainDpt/2
				rotx	90
					put	-1.480,		0,			15,
						-1.480,		0.0393,		15,
						-7.665,		0.577,		15,
						-7.665,		1.194,		15,
						-3.2012,	1.748,		15,
						0.275,		1.748,		15,
						0.700,		1.589,		15,
						1.250,		1.570,		15,
						1.668,		1.4191,		13,
						1.600,		1.231,		900,
						0,			-109.2829,	4000+13,
						1.7551,		1.1048,		15,
						1.3375,		0.5915,		15,
						1.3375,		0,			15

					prism_	nsp/3,	_mainDpt,	get(nsp)
				del 1
			del 1

			!	Counter Weight Deck
			_deckThk = 0.370

			addz	-0.213
				put	-7.305,	-3.745,	15,
					-7.575,	-3.174,	15,
					-7.575,	3.174,	15,
					-7.305,	3.745,	15,
					-5.395,	3.745,	15,
					-5.125,	3.174,	15,
					-5.125,	-3.174,	15,
					-5.395,	-3.745,	15

				prism_	nsp/3,	_deckThk,	get(nsp)
			del 1

			!	Counter Weight
			_nCW = 5
			_CWHgt = 0.391

			for	j = 1	to	2
				muly	-2*j + 3

					for	i = 1	to	_nCW
						addz	0.166 + 0.400*(i - 1)
							put	-7.305,	-3.745,	15,
								-7.575,	-3.174,	15,
								-7.575,	-1.581,	15,
								-5.125,	-1.581,	15,
								-5.125,	-3.174,	15,
								-5.395,	-3.745,	15

							prism_	nsp/3,	_CWHgt,	get(nsp)
						del 1
					next	i

				del 1
			next	j

			!	Main Deck - 1
			_zPosDeck_1 = 0.592
			_deckThk_1 = 0.060

			addz	_zPosDeck_1
				!	Bottom
				put	-1.038,	-1.948,	15,
					-1.038,	-1.516,	15,
					0.955,	-1.516,	15,
					0.955,	-1.948,	15

				prism_	nsp/3,	_deckThk_1,	get(nsp)

				put	0.965,	-2.907,	15,
					0.965,	-1.520,	15,
					2.023,	-1.520,	15,
					2.023,	-3.407,	15,
					1.803,	-3.407,	15

				prism_	nsp/3,	_deckThk_1,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = -1.008
					_coorGR[1][2] = -1.948

					_coorGR[2][1] = 0.965
					_coorGR[2][2] = -1.948

					_coorGR[3][1] = 0.965
					_coorGR[3][2] = -2.907

					_coorGR[4][1] = 1.803
					_coorGR[4][2] = -3.407

					_coorGR[5][1] = 1.993
					_coorGR[5][2] = -3.407

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 5,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.000,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2
			del 1

			!	Main Deck - 2
			_zPosDeck_2 = 1.543
			_deckThk_2 = 0.060

			addz	_zPosDeck_2
				!	Flat
				put	-3.190,	-1.948,	15,
					-3.190,	-1.508,	15,
					-1.090,	-1.508,	15,
					-1.090,	-1.948,	15

				prism_	nsp/3,	_deckThk_2,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = -3.160
					_coorGR[1][2] = -1.948

					_coorGR[2][1] = -1.120
					_coorGR[2][2] = -1.948

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 2.040/2,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2

				!	Slope
				addz	-0.221
					put	-4.990,	-1.948,	0,		15,
						-4.990,	-1.508,	0,		15,
						-3.280,	-1.508,	0.221,	15,
						-3.280,	-1.948,	0.221,	15

					slab_	nsp/4,	_deckThk_2,	get(nsp)

					!	Guard Rail
					add	-4.960,	-1.948,	0
						call	"HM_mc_GuardRail.gsm"	parameters	all	HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailL = 1.650,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.650/2,
																		HM_zPosEnd = 0.221,
																		HM_matGRail = HM_matBody2
					del 1
				del 1
			del 1
		del 1

	!	Operator's Cab
	!-----------------------------------------------------------------------------------------------
		_zPosOper = 2.095 + 0.120

		addz	_zPosOper
			!	Deck
			_deckThk_Oper = 0.260

			put	2.0465,	-3.410,	15,
				2.0465,	-1.675,	15,
				3.9115,	-1.675,	15,
				3.9115,	-3.410,	15

			prism_	nsp/3,	_deckThk_Oper,	get(nsp)

				!	Guard Rail
				addz	0.195
					_coorGR[1][1] = 2.0765
					_coorGR[1][2] = -3.410

					_coorGR[2][1] = 3.8815
					_coorGR[2][2] = -3.410

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.805/2,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2
				del 1

			!	Operator's Cab
			add	2.0635,	-1.614,	_deckThk_Oper! + 0.040
				call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Type = 4,
																HM_OP_SL = 1
			del 1
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------\
		_zPosMast = 3.326 + 0.358

		addz	_zPosMast
			addx	1.000
				call	"HM_mc_MastStrut.gsm"	parameters	all	iType = 1,
																bAnchor = 1,
																xPosEnd = _xPosEnd,
																zPosMast = 0.342,
																distMast = 1.860,
																mastLth = _mastLth,
																mastThk = 0.032,
																mastRad = 0.185,
																pulleyRad_1 = 0.325,
																pulleyRad_2 = 0.300


			del 1
		del 1


	!	Boom/Jib
	!-----------------------------------------------------------------------------------------------
		_zPosBoom = 3.326

		addz	_zPosBoom
			addx	_xPosBoom
				!	Boom
				call	"HM_mc_BoomJib.gsm"	parameters	all

				roty	-HM_BoomAng
					addx	HM_BoomLength
						!	Jib Pulley
						for	i = 1	to	2
							muly	-2*i + 3
								if	(HM_CraneMode = MODE_MAIN_BOOM) | (HM_CraneMode = MODE_LONG_BOOM)	then
									addz	-_zPosMainPulley
										call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.155,
																					HM_Pulley_R1 = 0.350,
																					HM_Pulley_R2 = 0.325,
																					HM_Pulley_Thick = 0.012,
																					HM_matPulley = HM_matBody2,
																					HM_Pulley_Fixed = 0
									del 1
								endif
								if	HM_CraneMode = MODE_LUFFING_JIB	then
									addx	_xPosJib
										addz	-_zPosJib
											roty	HM_BoomAng - HM_JibAng
												addx	HM_JibLength
													addz	-_zPosJibPulley
														call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.180,
																									HM_Pulley_R1 = 0.350,
																									HM_Pulley_R2 = 0.325,
																									HM_Pulley_Thick = 0.012,
																									HM_matPulley = HM_matBody2,
																									HM_Pulley_Fixed = 0
													del 1
												del 1
											del 1
										del 1
									del 1
								endif
							del 1
						next	i

						!	Strut
						if	HM_CraneMode = MODE_LUFFING_JIB	then
							!	Strut 1st
							addx	1.360
								addz	-1.650
									roty	HM_BoomAng
										call	"HM_mc_MastStrut.gsm"	parameters	all	iCraneName = 1,
																						iType = 4,	
																						iStrutLct = 1
									del 1
								del 1
							del 1

							!	Strut 2nd
							addx	0.700
								addz	0.300
									roty	HM_BoomAng
										call	"HM_mc_MastStrut.gsm"	parameters	all	iCraneName = 1,
																						iType = 4,	
																						iStrutLct = 2
									del 1
								del 1
							del 1
						endif
					del 1
				del 1
			del 1
		del 1

	!	Wire
	!-----------------------------------------------------------------------------------------------
		if	(HM_CraneMode = MODE_MAIN_BOOM) | (HM_CraneMode = MODE_LONG_BOOM)	then
			_zAddPos = HM_BoomLength*sin(HM_BoomAng) - _zPosMainPulley*cos(HM_BoomAng)
			_wireLen = HM_boomWireLen
		endif
		if	HM_CraneMode = MODE_LUFFING_JIB	then
			_zAddPos = (HM_BoomLength + _xPosJib)*sin(HM_BoomAng) - _zPosJib*cos(HM_BoomAng) + HM_JibLength*sin(HM_JibAng) - _zPosJibPulley*cos(HM_JibAng)
			_wireLen = HM_JibWireLen
		endif


		!	Boom or Jib â†’ Hook
		!-----------------------------------------------------------------------------------------------
		_zPosJibPulley = _zPosBoom + _zAddPos

		call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR + 0.325,
													x2 = HM_WorkingR + HM_HookRadius,
													y1 = 0,
													y2 = 0,
													z1 = _zPosJibPulley,
													z2 = _zPosJibPulley - _wireLen + HM_HookHeight

		call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR - 0.325,
													x2 = HM_WorkingR - HM_HookRadius,
													y1 = 0,
													y2 = 0,
													z1 = _zPosJibPulley,
													z2 = _zPosJibPulley - _wireLen + HM_HookHeight

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosBoom + _zAddPos

		add	HM_WorkingR,	0,	_zPosHook
			if	(HM_CraneMode = MODE_MAIN_BOOM) | (HM_CraneMode = MODE_LONG_BOOM)	then
				hotspot	0,	0,	0,					unID,	HM_boomWireLen,	1+128	:	unID = unID + 1
				hotspot	0,	0,	1,					unID,	HM_boomWireLen,	3		:	unID = unID + 1
				hotspot	0,	0,	-HM_boomWireLen,	unID,	HM_boomWireLen,	2		:	unID = unID + 1

				addz	-HM_boomWireLen
					rotz	_aRot_Hook
						call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = HM_HookThick
					del 1
				del 1
			endif
			if	HM_CraneMode = MODE_LUFFING_JIB	then
				hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
				hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

				addz	-HM_JibWireLen
					rotz	_aRot_Hook
						call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = HM_HookThick
					del 1
				del 1
			endif
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - _wireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!-----------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif


del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all



end