!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

	!	Crawler Wheel
	!-----------------------------------------------------------------------------------------------
		rotz	-90
			call	"HM_mc_Wheel.gsm"	parameters	all	HM_wheelHeight_total = 1.793,
														HM_wheelWidth = 1.500,
														HM_wheelLen = 10.184,
														HM_wheelDistance = 6.900,
														HM_wheelBeltThk = 0.141
		del 1

	!	Connector to Main Object - Part1
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.470

		_ConnectThk = 1.100

		addz	_zPosConnect
			put	-1.797,	-3.450,	15,
				-1.695,	-3.045,	15,
				-1.495,	-3.045,	15,
				-1.495,	3.045,	15,
				-1.695,	3.045,	15,
				-1.797,	3.450,	15,
				1.797,	3.450,	15,
				1.695,	3.045,	15,
				1.495,	3.045,	15,
				1.495,	-3.045,	15,
				1.695,	-3.045,	15,
				1.797,	-3.450,	15

			prism_	nsp/3,	_ConnectThk,	get(nsp)
		del 1

rotz	HM_WorkingAng
	!	Connector to Main Object - Part2
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.470

		_ConnectThk = 1.100

		addz	_zPosConnect
			addz	_ConnectThk
				cylind	0.584,	1.480
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		dim	_coorGR[][]

		_zPosMain = 2.154
		_mainDpt = 2.813

		addz	_zPosMain
			!	Main Object
			addy	_mainDpt/2
				rotx	90
					put	-1.495,	0,			15,
						-1.495,	0.050,		15,
						-7.565,	0.800,		15,
						-7.565,	1.396,		15,
						-6.885,	1.396,		15,
						-5.065,	1.662,		15,
						-0.048,	1.662,		15,
						1.636,	1.270,		15,
						2.068,	1.114,		13,
						2.000,	0.926,		900,
						0,		-117.4251,	4000+13,
						2.135,	0.7788,		15,
						1.760,	0.4330,		15,
						1.670,	0.050,		15,
						1.670,	0,			15

					prism_	nsp/3,	_mainDpt,	get(nsp)
				del 1
			del 1

			!	Counter Weight Deck
			_deckThk = 0.370

			addz	0.013
				put	-7.470,	-3.720,	15,
					-7.714,	-3.160,	15,
					-7.714,	3.160,	15,
					-7.470,	3.720,	15,
					-5.530,	3.720,	15,
					-5.286,	3.160,	15,
					-5.286,	-3.160,	15,
					-5.530,	-3.720,	15

				prism_	nsp/3,	_deckThk,	get(nsp)
			del 1

			!	Counter Weight
			_nCW = 9
			_CWHgt = 0.391

			for	j = 1	to	2
				muly	-2*j + 3

					for	i = 1	to	_nCW
						addz	0.392 + 0.400*(i - 1)
							put	-7.470,	-3.720,	15,
								-7.714,	-3.160,	15,
								-7.714,	-1.550,	15,
								-5.286,	-1.550,	15,
								-5.286,	-3.160,	15,
								-5.530,	-3.720,	15

							prism_	nsp/3,	_CWHgt,	get(nsp)
						del 1
					next	i

				del 1
			next	j

			!	Main Deck - 1
			_zPosDeck_1 = 0.400
			_deckThk_1 = 0.060

			addz	_zPosDeck_1
				!	Bottom
				put	-1.501,	-1.9905,	15,
					-1.501,	-1.4905,	15,
					1.146,	-1.4905,	15,
					1.146,	-1.9905,	15

				prism_	nsp/3,	_deckThk_1,	get(nsp)

				put	1.189,	-2.907,	15,
					1.189,	-1.520,	15,
					2.247,	-1.520,	15,
					2.247,	-3.407,	15,
					2.027,	-3.407,	15

				prism_	nsp/3,	_deckThk_1,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = -1.471
					_coorGR[1][2] = -1.9905

					_coorGR[2][1] = 1.189
					_coorGR[2][2] = -1.9905

					_coorGR[3][1] = 1.189
					_coorGR[3][2] = -2.907

					_coorGR[4][1] = 2.027
					_coorGR[4][2] = -3.407

					_coorGR[5][1] = 2.217
					_coorGR[5][2] = -3.407

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 5,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.000,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2

				!	Top
				put	-1.501,	1.9905,	15,
					-1.501,	1.4905,	15,
					2.310,	1.4905,	15,
					2.310,	1.9905,	15

				prism_	nsp/3,	_deckThk_1,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = 2.280
					_coorGR[1][2] = 1.9905

					_coorGR[2][1] = -1.471
					_coorGR[2][2] = 1.9905

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.000,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2
			del 1

			!	Main Deck - 2
			_zPosDeck_2 = 0.575
			_deckThk_2 = 0.060

			addz	_zPosDeck_2
				!	Bottom
				put	-5.101,	-1.9905,	15,
					-5.101,	-1.4905,	15,
					-1.569,	-1.4905,	15,
					-1.569,	-1.9905,	15

				prism_	nsp/3,	_deckThk_2,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = -5.071
					_coorGR[1][2] = -1.9905

					_coorGR[2][1] = -1.599
					_coorGR[2][2] = -1.9905

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 3.472/3,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2

				!	Top
				put	-5.101,	1.9905,	15,
					-5.101,	1.4905,	15,
					-1.569,	1.4905,	15,
					-1.569,	1.9905,	15

				prism_	nsp/3,	_deckThk_2,	get(nsp)

					!	Guard Rail
					_coorGR[1][1] = -1.599
					_coorGR[1][2] = 1.9905

					_coorGR[2][1] = -5.071
					_coorGR[2][2] = 1.9905

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 3.472/3,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Operator's Cab
	!-----------------------------------------------------------------------------------------------
		_zPosOper = 2.154 - 0.030

		addz	_zPosOper
			!	Deck
			_deckThk_Oper = 0.260

			put	2.2705,	-3.410,	15,
				2.2705,	-1.675,	15,
				4.1355,	-1.675,	15,
				4.1355,	-3.410,	15

			prism_	nsp/3,	_deckThk_Oper,	get(nsp)

				!	Guard Rail
				addz	0.195
					_coorGR[1][1] = 2.3005
					_coorGR[1][2] = -3.410

					_coorGR[2][1] = 4.1055
					_coorGR[2][2] = -3.410

					call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 2,
																		HM_GRail_Pt = _coorGR,
																		HM_GRailType = 1,
																		HM_GRailRef = 3,
																		HM_GRailH = 1.000 + 0.025/2,
																		HM_GRailD = 0.025,
																		HM_GRailT = 0.025,
																		HM_GRailDist = 1.805/2,
																		HM_GRailDist_hori = 0.450,
																		HM_matGRail = HM_matBody2
				del 1

			!	Operator's Cab
			add	2.3265,	-1.637,	_deckThk_Oper + 0.040
				call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Type = 4,
																HM_OP_SL = 2
			del 1
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------\
		_zPosMast = 3.080 + 0.415

		addz	_zPosMast
			addx	1.000
				call	"HM_mc_MastStrut.gsm"	parameters	all	iType = _mastType,
																bAnchor = 1,
																xPosEnd = _xPosEnd,
																zPosMast = 0.310,
																distMast = 2.230,
																mastLth = _mastLth,
																mastThk = 0.032,
																mastRad = 0.1975,
																pulleyRad_1 = 0.325,
																pulleyRad_2 = 0.2875
				if	not(HM_CraneSpec = SPEC_STANDARD)	then
					addz	0.310
						call	"HM_mc_MastStrut.gsm"	parameters	all	iCraneName = _iCraneName,
																		iType = _mastType_2nd,
																		xPosEnd = _xPosEnd_2nd,
																		mastLth = _mastLth_2nd
					del 1
				endif
			del 1
		del 1

	!	Pallet Weight
	!-----------------------------------------------------------------------------------------------
		if	not(HM_CraneSpec = SPEC_STANDARD)	then
			if	HM_PWeight = PALLET_11M	then	_xPosPW = 11.000
			if	HM_PWeight = PALLET_13M	then	_xPosPW = 13.000
			if	HM_PWeight = PALLET_16M	then	_xPosPW = 16.000

			addx	-_xPosPW
				!	Base
				put	-2.450/2,	-7.560/2,	15,
					-2.450/2,	7.560/2,	15,
					2.450/2,	7.560/2,	15,
					2.450/2,	-7.560/2,	15

				prism_	nsp/3,	0.600,	get(nsp)

				!	Weight
				_nPW = 8
				_PWHgt = 0.391

				for	i = 1	to	_nPW
					addz	0.600 + 0.400*(i - 1)
						put	-2.450/2,	2.170/2,	15,
							-2.450/2,	-2.170/2,	15,
							2.450/2,	-2.170/2,	15,
							2.450/2,	2.170/2,	15

						for	j = 1	to	2
							muly	-2*j + 3
								addy	2.675
									prism_	nsp/3,	_PWHgt,	use(nsp)
								del 1
							del 1
						next	j

						prism_	nsp/3,	_PWHgt,	get(nsp)
					del 1
				next	i
			del 1
		endif

	!	Boom/Jib
	!-----------------------------------------------------------------------------------------------
		_zPosBoom = 3.080

		addz	_zPosBoom
			addx	_xPosBoom
				!	Boom
				call	"HM_mc_BoomJib.gsm"	parameters	all

				roty	-HM_BoomAng
					addx	HM_BoomLength
						!	Jib Pulley
						for	i = 1	to	2
							muly	-2*i + 3
								if	HM_CraneMode = MODE_HEAVY_BOOM	then
									_rPulley = 0.3015
									addz	-_zPosMainPulley
										addy	0.650
											call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 1.000,
																						HM_Pulley_R1 = 0.350,
																						HM_Pulley_R2 = _rPulley,
																						HM_Pulley_Thick = 0.012,
																						HM_matPulley = HM_matBody2,
																						HM_Pulley_Fixed = 0
										del 1
									del 1

									addx	-0.100
										addz	1.290
											addy	0.243
												call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.074,
																							HM_Pulley_R1 = 0.3765,
																							HM_Pulley_R2 = 0.353,
																							HM_Pulley_Thick = 0.012,
																							HM_matPulley = HM_matBody2,
																							HM_Pulley_Fixed = 0
											del 1
										del 1
									del 1
								endif
								if	HM_CraneMode = MODE_MAIN_BOOM | HM_CraneMode = MODE_LONG_BOOM	then
									_rPulley = 0.353
									addz	-_zPosMainPulley
										call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.608,
																					HM_Pulley_R1 = 0.4015,
																					HM_Pulley_R2 = _rPulley,
																					HM_Pulley_Thick = 0.012,
																					HM_matPulley = HM_matBody2,
																					HM_Pulley_Fixed = 0
									del 1
								endif
								if	HM_CraneMode = MODE_LUFFING_JIB	then
									_rPulley = 0.3015
									addx	_xPosJib
										addz	-_zPosJib
											roty	HM_BoomAng - HM_JibAng
												addx	HM_JibLength
													addz	-_zPosJibPulley
														call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.540,
																									HM_Pulley_R1 = 0.350,
																									HM_Pulley_R2 = 0.3015,
																									HM_Pulley_Thick = 0.012,
																									HM_matPulley = HM_matBody2,
																									HM_Pulley_Fixed = 0
													del 1
												del 1
											del 1
										del 1
									del 1
								endif
							del 1
						next	i

						!	Strut
						if	HM_CraneMode = MODE_LUFFING_JIB	then
							!	Strut 1st
							addx	1.135
								addz	-0.545
									roty	HM_BoomAng
										call	"HM_mc_MastStrut.gsm"	parameters	all	iCraneName = CRANE_SL6000J,
																						iType = TYPE_STRUT_HEAVY_LIFT,	
																						iStrutLct = STRUT_FRONT
									del 1
								del 1
							del 1

							!	Strut 2nd
							addx	0.600
								addz	0.850
									roty	HM_BoomAng
										call	"HM_mc_MastStrut.gsm"	parameters	all	iCraneName = CRANE_SL6000J,
																						iType = TYPE_STRUT_HEAVY_LIFT,	
																						iStrutLct = STRUT_REAR
									del 1
								del 1
							del 1
						endif
					del 1
				del 1
			del 1
		del 1

	!	Wire
	!-----------------------------------------------------------------------------------------------
		if	HM_CraneMode = MODE_HEAVY_BOOM | HM_CraneMode = MODE_MAIN_BOOM | HM_CraneMode = MODE_LONG_BOOM	then
			_zAddPos = HM_BoomLength*sin(HM_BoomAng) - _zPosMainPulley*cos(HM_BoomAng)
			_wireLen = HM_boomWireLen
			_yPosWire = 0
			_nWire = 1
			if	HM_CraneMode = MODE_HEAVY_BOOM	then
				_yPosWire = 0.650
				_nWire = 2
			endif
		endif
		if	HM_CraneMode = MODE_LUFFING_JIB	then
			_zAddPos = (HM_BoomLength + _xPosJib)*sin(HM_BoomAng) - _zPosJib*cos(HM_BoomAng) + HM_JibLength*sin(HM_JibAng) - _zPosJibPulley*cos(HM_JibAng)
			_wireLen = HM_JibWireLen
			_yPosWire = 0
			_nWire = 1
		endif

		!	Boom or Jib → Hook
		!-----------------------------------------------------------------------------------------------
		_zPosJibPulley = _zPosBoom + _zAddPos

		for	i = 1	to	_nWire
			muly	-2*i + 3
				call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR + _rPulley,
															x2 = HM_WorkingR + HM_HookRadius,
															y1 = _yPosWire,
															y2 = 0,
															z1 = _zPosJibPulley,
															z2 = _zPosJibPulley - _wireLen + HM_HookHeight

				call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR - _rPulley,
															x2 = HM_WorkingR - HM_HookRadius,
															y1 = _yPosWire,
															y2 = 0,
															z1 = _zPosJibPulley,
															z2 = _zPosJibPulley - _wireLen + HM_HookHeight
			del 1
		next	i

		!	Boom Rear → Front
		!-----------------------------------------------------------------------------------------------
		if	HM_CraneMode = MODE_HEAVY_BOOM	then
			for	i = 1	to	_nWire
				muly	-2*i + 3
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR - (_zPosMainPulley*sin(HM_BoomAng) + 0.100*cos(HM_BoomAng) + 1.290*sin(HM_BoomAng)) + 0.353*cos(HM_BoomAng),
																x2 = HM_WorkingR + _rPulley*cos(HM_BoomAng),
																y1 = 0.243,
																y2 = 0.243,
																z1 = _zPosJibPulley + (_zPosMainPulley*cos(HM_BoomAng) - 0.100*sin(HM_BoomAng) + 1.290*cos(HM_BoomAng)) + 0.353*sin(HM_BoomAng),
																z2 = _zPosJibPulley + _rPulley*sin(HM_BoomAng)
				del 1
			next	i
		endif


	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosBoom + _zAddPos

		add	HM_WorkingR,	0,	_zPosHook
			if	HM_CraneMode = MODE_HEAVY_BOOM | HM_CraneMode = MODE_MAIN_BOOM | HM_CraneMode = MODE_LONG_BOOM	then
				hotspot	0,	0,	0,					unID,	HM_boomWireLen,	1+128	:	unID = unID + 1
				hotspot	0,	0,	1,					unID,	HM_boomWireLen,	3		:	unID = unID + 1
				hotspot	0,	0,	-HM_boomWireLen,	unID,	HM_boomWireLen,	2		:	unID = unID + 1

				addz	-HM_boomWireLen
					rotz	_aRot_Hook
						call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = HM_HookThick
					del 1
				del 1
			endif
			if	HM_CraneMode = MODE_LUFFING_JIB	then
				hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
				hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

				addz	-HM_JibWireLen
					rotz	_aRot_Hook
						call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = HM_HookThick
					del 1
				del 1
			endif
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - _wireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!-----------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif


del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all



end