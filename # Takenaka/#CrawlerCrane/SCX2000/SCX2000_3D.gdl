!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

	!	Crawler Wheel
	!-----------------------------------------------------------------------------------------------
		rotz	-90
			call	"HM_mc_Wheel.gsm"	parameters	all	HM_wheelHeight_total = 1.3995,
														HM_wheelWidth = 1.118,
														HM_wheelLen = 7.860,
														HM_wheelDistance = 5.282,
														HM_wheelBeltThk = 0.110
		del 1

	!	Connector to Main Object - Part1
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.4415
		_ConnectThk = 0.7815

		addz	_zPosConnect
			put	-1.620,		-2.641,	15,
				-1.620,		-2.300,	15,
				-1.3815,	-2.300,	15,
				-1.219,		-1.600,	15,
				-1.219,		1.600,	15,
				-1.3815,	2.300,	15,
				-1.620,		2.300,	15,
				-1.620,		2.641,	15,
				-0.986,		2.641,	15,
				-0.986,		2.300,	15,
				-0.450,		1.600,	15,
				0.450,		1.600,	15,
				0.986,		2.300,	15,
				0.986,		2.641,	15,
				1.620,		2.641,	15,
				1.620,		2.300,	15,
				1.3815,		2.300,	15,
				1.219,		1.600,	15,
				1.219,		-1.600,	15,
				1.3815,		-2.300,	15,
				1.620,		-2.300,	15,
				1.620,		-2.641,	15,
				0.986,		-2.641,	15,
				0.986,		-2.300,	15,
				0.450,		-1.600,	15,
				-0.450,		-1.600,	15,
				-0.986,		-2.300,	15,
				-0.986,		-2.641,	15

			prism_	nsp/3,	_ConnectThk,	get(nsp)
		del 1

rotz	HM_WorkingAng
	!	Connector to Main Object - Part2
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.4415
		_ConnectThk = 0.7815

		addz	_zPosConnect
			addz	_ConnectThk
				cylind	0.487,	1.446
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		_zPosMain = 1.710

		addz	_zPosMain

			!	Main Deck
			_mainDeckThk = 0.140

			put	-2.680,	-1.530,	15,
				-2.680,	-2.220,	15,
				0.910,	-2.220,	15,
				0.910,	-3.024,	15,
				3.870,	-3.024,	15,
				3.870,	-1.530,	15

			prism_	nsp/3,	_mainDeckThk,	get(nsp)

			put	-2.680,	1.570,	15,
				-2.680,	2.220,	15,
				0.964,	2.220,	15,
				0.964,	1.570,	15

			prism_	nsp/3,	_mainDeckThk,	get(nsp)

			!	Main Object
			_mainDpt = 2.870

			addy	_mainDpt/2
				rotx	90
					put	1.500,	0,			15,
						-5.845,	0,			15,
						-5.845,	1.405,		15,
						0,		1.405,		15,
						1.178,	1.325,		15,
						1.530,	1.257,		13,
						1.500,	1.100,		900,
						0,		-99.0531,	4000+13,
						1.650,	1.048,		15,
						1.500,	0.6195,		15

					prism_	nsp/3,	_mainDpt,	get(nsp)
				del 1
			del 1

			!	Counter Weight
			resol	36

			_nCW = 6
			_CWHgt = 0.500

			for	i = 1	to	_nCW
				addz	0.3685 + (_CWHgt + 0.014)*(i - 1)
					for	j = 1	to	2
						muly	-2*j + 3
							put	-3.370,	-1.510,	15,
								-5.670,	-1.510,	15,
								-5.670,	-1.962,	15,
								-4.972,	-3.360,	15,
								-4.067,	-3.360,	15,
								-3.370,	-1.962,	15

							prism_	nsp/3,	_CWHgt,	get(nsp)
						del 1
					next	j
				del 1
			next	i

			resol	HM_3D_resol
		del 1

	!	Operator Cap
	!-----------------------------------------------------------------------------------------------
		_zPosOper = _zPosMain + _mainDeckThk

		addz	_zPosOper
			add	1.880,	-1.530,	0
				call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Type = 3
			del 1
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------\
		_zPosMast = 2.8105 + 0.215

		addz	_zPosMast
			addy	_yPosBoom
				addx	1.000
					call	"HM_mc_MastStrut.gsm"	parameters	all	iType = _mastType,
																	bAnchor = 1,
																	xPosEnd = _xPosEnd,
																	zPosMast = 0.465,
																	distMast = 1.870,
																	mastLth = _mastLth,
																	mastThk = 0.032,
																	mastRad = 0.130,
																	pulleyRad_1 = 0.325,
																	pulleyRad_2 = 0.265
				del 1
			del 1
		del 1

	!	Boom/Jib
	!-----------------------------------------------------------------------------------------------
		_zPosBoom = 2.8105

		addz	_zPosBoom
			addy	_yPosBoom
				addx	_xPosBoom
					!	Boom
					call	"HM_mc_BoomJib.gsm"	parameters	all

					roty	-HM_BoomAng
						addx	HM_BoomLength
							!	Jib Pulley
							for	i = 1	to	2
								muly	-2*i + 3
									if	HM_CraneMode = MODE_LONG_BOOM	then
										!	Common
										_rPulley = 0.322
										addz	-_zPosMainPulley
											call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.454,
																						HM_Pulley_R1 = 0.350,
																						HM_Pulley_R2 = 0.322,
																						HM_Pulley_Thick = 0.012,
																						HM_matPulley = HM_matBody2,
																						HM_Pulley_Fixed = 0
										del 1
										if	i = 1	then	addy	0.0475
										if	i = 2	then	addy	0.190
											addx	-0.600
												addz	0.850
														call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.074,
																									HM_Pulley_R1 = 0.350,
																									HM_Pulley_R2 = 0.322,
																									HM_Pulley_Thick = 0.012,
																									HM_matPulley = HM_matBody2,
																									HM_Pulley_Fixed = 0
												del 1
											del 1
										del 1

										!	Crane Jib
										if	HM_CraneSubSpec = SUB_SPEC_CRANE_JIB | HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM_CRANE_JIB	then
											_rPulley = 0.322
											addx	_xPosJib
												roty	HM_JibAng
													addx	HM_JibLength
														call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.074,
																									HM_Pulley_R1 = 0.350,
																									HM_Pulley_R2 = 0.322,
																									HM_Pulley_Thick = 0.012,
																									HM_matPulley = HM_matBody2,
																									HM_Pulley_Fixed = 0
													del 1
												del 1
											del 1
										endif
									endif
									if	HM_CraneMode = MODE_TOWER_BOOM	then
										_rPulley = 0.322
										addx	_xPosJib
											addz	-_zPosJib
												roty	HM_BoomAng - HM_JibAng
													addx	HM_JibLength
														addz	-_zPosJibPulley
															call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.074,
																										HM_Pulley_R1 = 0.350,
																										HM_Pulley_R2 = 0.322,
																										HM_Pulley_Thick = 0.012,
																										HM_matPulley = HM_matBody2,
																										HM_Pulley_Fixed = 0
														del 1
														addx	-0.300
															addz	0.360
																call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.074,
																											HM_Pulley_R1 = 0.350,
																											HM_Pulley_R2 = 0.322,
																											HM_Pulley_Thick = 0.012,
																											HM_matPulley = HM_matBody2,
																											HM_Pulley_Fixed = 0
															del 1
														del 1
													del 1
												del 1
											del 1
										del 1
									endif
								del 1
							next	i


							!	Aux Sheave
							if	HM_CraneMode = MODE_LONG_BOOM	then
								if	HM_CraneSubSpec = SUB_SPEC_AUX_SHEAVE | HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM_AUX_SHEAVE | HM_CraneSubSpec = SUB_SPEC_THIRD_MAIN_BOOM_AUX_SHEAVE	then
									addx	_xPosAuxSheave
										addz	-_zPosAuxSheave
											gosub	"draw_Aux_Sheave"
										del 1
									del 1
								endif
							endif

							!	Strut
							if	HM_CraneMode = MODE_LONG_BOOM	then
								if	HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM_CRANE_JIB | HM_CraneSubSpec = SUB_SPEC_CRANE_JIB	then
									! addx	0.442
									! 	addz	0.324
									! 		roty	HM_BoomAng
									! 			call	"HM_mc_MastStrut.gsm"	parameters	all	iType = TYPE_STRUT,
									! 															nStrut = 1,
									! 															xPosEnd_Strut_1 = 2.900,
									! 															distStrut_SP = 0.750,
									! 															distStrut_EP = 0.320,
									! 															strutLth_1 = 4.470,
									! 															strutThk = 0.016,
									! 															strutRad_1 = 0.075
									! 		del 1
									! 	del 1
									! del 1
								endif
							endif
							if	HM_CraneMode = MODE_TOWER_BOOM	then
								addx	0.610
									addz	0.150
										roty	HM_BoomAng
											call	"HM_mc_MastStrut.gsm"	parameters	all	iType = TYPE_STRUT,
																							nStrut = 2,
																							xPosEnd_Strut_1 = 6.630,
																							bMirror_Strut_1 = 1,
																							xPosEnd_Strut_2 = 4.415,
																							distStrut_SP = 2.100,
																							distStrut_EP = 2.100,
																							strutLth_1 = 6.700,
																							strutLth_2 = 6.700,
																							strutThk = 0.025,
																							strutRad_1 = 0.130,
																							strutRad_2 = 0.110
										del 1
									del 1
								del 1
							endif
						del 1
					del 1
				del 1
			del 1
		del 1

	!	Wire
	!-----------------------------------------------------------------------------------------------
		if	HM_CraneMode = MODE_LONG_BOOM	then
			if	HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM | HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM_AUX_SHEAVE \
			 | HM_CraneSubSpec = SUB_SPEC_MAIN_BOOM_CRANE_JIB | HM_CraneSubSpec = SUB_SPEC_THIRD_MAIN_BOOM | HM_CraneSubSpec = SUB_SPEC_THIRD_MAIN_BOOM_AUX_SHEAVE	then
				_zAddPos = HM_BoomLength*sin(HM_BoomAng) - _zPosMainPulley*cos(HM_BoomAng)
				_yPosWire = 0.145
				_wireLen = HM_boomWireLen
			endif
			if	HM_CraneSubSpec = SUB_SPEC_AUX_SHEAVE	then
				_zAddPos = (HM_BoomLength + _xPosAuxSheave)*sin(HM_BoomAng) - _zPosAuxSheave*cos(HM_BoomAng)
				_yPosWire = 0
				_wireLen = HM_boomWireLen
			endif
			if	HM_CraneSubSpec = SUB_SPEC_CRANE_JIB	then
				_zAddPos = (HM_BoomLength + _xPosJib)*sin(HM_BoomAng) - _zPosJib*cos(HM_BoomAng) + HM_JibLength*sin(HM_BoomAng - HM_JibAng) - _zPosJibPulley*cos(HM_BoomAng - HM_JibAng)
				_yPosWire = 0
				_wireLen = HM_JibWireLen
			endif
		endif
		if	HM_CraneMode = MODE_TOWER_BOOM	then
			_zAddPos = (HM_BoomLength + _xPosJib)*sin(HM_BoomAng) - _zPosJib*cos(HM_BoomAng) + HM_JibLength*sin(HM_JibAng) - _zPosJibPulley*cos(HM_JibAng)
			_yPosWire = 0
			_wireLen = HM_JibWireLen
		endif

		!	Boom or Jib â†’ Hook
		!-----------------------------------------------------------------------------------------------
		_zPosJibPulley = _zPosBoom + _zAddPos

		addy	_yPosBoom
			if	not(HM_HookType = HOOK_TYPE_S_SIZE)	then
				for	i = 1	to	2
					muly	-2*i + 3
						call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR + _rPulley,
																	x2 = HM_WorkingR + HM_HookRadius,
																	y1 = _yPosWire,
																	y2 = 0,
																	z1 = _zPosJibPulley,
																	z2 = _zPosJibPulley - _wireLen + HM_HookHeight

						call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR - _rPulley,
																	x2 = HM_WorkingR - HM_HookRadius,
																	y1 = _yPosWire,
																	y2 = 0,
																	z1 = _zPosJibPulley,
																	z2 = _zPosJibPulley - _wireLen + HM_HookHeight
					del 1
				next	i
			else
				call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR,
															x2 = HM_WorkingR,
															y1 = 0,
															y2 = 0,
															z1 = _zPosJibPulley,
															z2 = _zPosJibPulley - _wireLen + HM_HookHeight
			endif
		del 1

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosBoom + _zAddPos

		add	HM_WorkingR,	_yPosBoom,	_zPosHook
			if	HM_CraneMode = MODE_LONG_BOOM	then
				if	not(HM_CraneSubSpec = SUB_SPEC_CRANE_JIB)	then
					hotspot	0,	0,	0,					unID,	HM_boomWireLen,	1+128	:	unID = unID + 1
					hotspot	0,	0,	1,					unID,	HM_boomWireLen,	3		:	unID = unID + 1
					hotspot	0,	0,	-HM_boomWireLen,	unID,	HM_boomWireLen,	2		:	unID = unID + 1

					addz	-HM_boomWireLen
						rotz	_aRot_Hook
							call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.100
						del 1
					del 1
				else
					hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
					hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
					hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

					addz	-HM_JibWireLen
						rotz	_aRot_Hook
							call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.100
						del 1
					del 1
				endif
			endif
			if	HM_CraneMode = MODE_TOWER_BOOM	then
				hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
				hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

				addz	-HM_JibWireLen
					rotz	_aRot_Hook
						call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.100
					del 1
				del 1
			endif
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - _wireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	_yPosBoom,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!-----------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	_yPosBoom,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif


del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	addy	_yPosBoom
		call	"HM_mc_WorkingR.gsm"	parameters all
	del 1



end



!-----------------------------------------------------------------------------------------------
"draw_Aux_Sheave":
!-----------------------------------------------------------------------------------------------

	!	Pulley
	call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.076,
												HM_Pulley_R1 = 0.331,
												HM_Pulley_R2 = 0.302,
												HM_Pulley_Thick = 0.012,
												HM_matPulley = HM_matBody2,
												HM_Pulley_Fixed = 0

	!	Side Plate
	for	i = 1	to	2
		muly	-2*i + 3
			roty	_aAuxSheave
				addy	0.2035 + 0.066/2
					rotx	90
						put	0,			0.1406,		15,
							-0.4228,	0.1819,		15,
							-0.4493,	0.1408,		15,
							-1.070,		-0.170,		15,
							-1.070,		-0.452,		15,
							-0.570,		-0.452,		15,
							0.0642,		-0.1244,	15,
							0,			0,			900,
							0,			152.2430,	4000+15

						prism_	nsp/3,	0.066,	get(nsp)
					del 1
				del 1
			del 1
		del 1
	next	i

	!	Rod
	addy	0.2035 - 0.066/2
		rotx	90
			cylind	(0.2035 - 0.066/2)*2,	0.070
		del 1
	del 1

	roty	_aAuxSheave
		addx	-1.000

			addz	-0.382
				addy	0.2035 - 0.066/2
					rotx	90
						cylind	(0.2035 - 0.066/2)*2,	0.025
					del 1
				del 1
			del 1

			addz	-0.222
				addy	0.2035 - 0.066/2
					rotx	90
						cylind	(0.2035 - 0.066/2)*2,	0.025
					del 1
				del 1
			del 1

		del 1
	del 1

	!	Sub Side Plate
	for	i = 1	to	2
		muly	-2*i + 3
			roty	_aAuxSheave
				addy	0.1485 + 0.025/2
					rotx	90
						put	-1.1674,	-0.4350,	15,
							-1.0655,	-0.1993,	15,
							-0.9734,	-0.1572,	15,
							-0.5734,	-0.3172,	15,
							-0.5301,	-0.3923,	15,
							-0.5364,	-0.4350,	15

						prism_	nsp/3,	0.025,	get(nsp)
					del 1
				del 1
			del 1
		del 1
	next	i


return