!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

	!	Crawler Wheel
	!-----------------------------------------------------------------------------------------------
		rotz	-90
			call	"HM_mc_Wheel.gsm"	parameters	all	HM_wheelHeight_total = 1.753,
														HM_wheelWidth = 1.219,
														HM_wheelLen = 9.310,
														HM_wheelDistance = 6.281,
														HM_wheelBeltThk = 0.119
		del 1

	!	Connector to Main Object - Part1
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.622

		_ConnectThk = 0.921

		addz	_zPosConnect
			put	-1.050,	-3.140,	15,
				-1.100,	-2.965,	15,
				-1.500,	-2.965,	15,
				-1.500,	2.965,	15,
				-1.100,	2.965,	15,
				-1.050,	3.140,	15,
				1.050,	3.140,	15,
				1.100,	2.965,	15,
				1.500,	2.965,	15,
				1.500,	-2.965,	15,
				1.100,	-2.965,	15,
				1.050,	-3.140,	15

			prism_	nsp/3,	_ConnectThk,	get(nsp)
		del 1

rotz	HM_WorkingAng
	!	Connector to Main Object - Part2
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = 0.622

		_ConnectThk = 0.921

		addz	_zPosConnect
			addz	_ConnectThk
				cylind	0.552,	1.476
			del 1
		del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		_zPosMain = 2.095

		_mainDpt = 2.990

		addz	_zPosMain
			!	Main Object
			addy	_mainDpt/2
				rotx	90
					put	-1.480,	0,		15,
						-1.480,	0.039,	15,
						-5.466,	0.380,	15,
						-5.466,	1.457,	15,
						-3.215,	1.746,	15,
						0.275,	1.748,	15,
						0.700,	1.589,	15,
						1.800,	1.589,	15,
						1.800,	0,		15

					prism_	nsp/3,	_mainDpt,	get(nsp)
				del 1
			del 1

			!	Counter Weight Deck
			_deckThk = 0.370

			addz	-0.213
				put	-7.305,	-3.745,	15,
					-7.575,	-3.174,	15,
					-7.575,	3.174,	15,
					-7.305,	3.745,	15,
					-5.395,	3.745,	15,
					-5.125,	3.174,	15,
					-5.125,	-3.174,	15,
					-5.395,	-3.745,	15

				prism_	nsp/3,	_deckThk,	get(nsp)
			del 1

			!	Counter Weight
			_nCW = 5
			_CWHgt = 0.391

			for	j = 1	to	2
				muly	-2*j + 3

					for	i = 1	to	_nCW
						addz	0.166 + 0.400*(i - 1)
							put	-7.305,	-3.745,	15,
								-7.575,	-3.174,	15,
								-7.575,	-1.581,	15,
								-5.125,	-1.581,	15,
								-5.125,	-3.174,	15,
								-5.395,	-3.745,	15

							prism_	nsp/3,	_CWHgt,	get(nsp)
						del 1
					next	i

				del 1
			next	j
		del 1

	!	Boom/Jib
	!-----------------------------------------------------------------------------------------------
		_zPosBoom = 3.326

		addz	_zPosBoom
			addx	1.600
				!	Boom
				call	"HM_mc_BoomJib.gsm"	parameters	all

				roty	-HM_BoomAng
					addx	HM_BoomLength
						!	Jib Pulley
						for	i = 1	to	2
							muly	-2*i + 3
								if	(HM_CraneMode = MODE_MAIN_BOOM) | (HM_CraneMode = MODE_LONG_BOOM)	then
									addz	-_zPosMainPulley
										call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.155,
																					HM_Pulley_R1 = 0.350,
																					HM_Pulley_R2 = 0.325,
																					HM_Pulley_Thick = 0.012,
																					HM_matPulley = HM_matBody2,
																					HM_Pulley_Fixed = 0
									del 1
								endif
								if	HM_CraneMode = MODE_LUFFING_JIB	then
									addx	_xPosJib
										addz	-_zPosJib
											roty	HM_BoomAng - HM_JibAng
												addx	HM_JibLength
													addz	-_zPosJibPulley
														call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.180,
																									HM_Pulley_R1 = 0.350,
																									HM_Pulley_R2 = 0.325,
																									HM_Pulley_Thick = 0.012,
																									HM_matPulley = HM_matBody2,
																									HM_Pulley_Fixed = 0
													del 1
												del 1
											del 1
										del 1
									del 1
								endif
							del 1
						next	i
					del 1
				del 1
			del 1
		del 1

	!	Load Text
	!-----------------------------------------------------------------------------------------------
		if	(HM_CraneMode = MODE_MAIN_BOOM) | (HM_CraneMode = MODE_LONG_BOOM)	then
			_zAddPos = HM_BoomLength*cos(HM_BoomAng)
		endif
		if	HM_CraneMode = MODE_LUFFING_JIB	then
			_zAddPos = (HM_BoomLength + _zPosJib)*sin(HM_BoomAng) + _zPosJib*cos(HM_BoomAng) + _zPosJibPulley*cos(HM_JibAng)
		endif

		_zPosText = _zPosBoom + _zAddPos - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif


del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all



end