!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20

!----------------------------------------------------------------------------------------------------
!	General
!----------------------------------------------------------------------------------------------------
if	GLOB_FEEDBACK_MODE	then
	HM_3D_resol = 3
endif

resol	HM_3D_resol

!----------------------------------------------------------------------------------------------------
!	Representation
!----------------------------------------------------------------------------------------------------
if	GLOB_VIEW_TYPE = 2	then
	HM_3D_pen = HM_2D_cranePen
endif
pen	HM_3D_pen

call	"HM_Material_Red.gsm"
call	"HM_Material_Yellow.gsm"
call	"HM_Material_Blue.gsm"
call	"HM_Material_White.gsm"


!----------------------------------------------------------------------------------------------------
!	Shape
!----------------------------------------------------------------------------------------------------
if	HM_matWarning = ""	then
	_material = HM_matBody2
else
	_material = HM_matWarning
endif

material	_material

rotz	HM_WorkingAng
	!	Mast Basement
	!-----------------------------------------------------------------------------------------------
		rotz	45
			call	"HM_mc_OutriggerTower.gsm"	parameters	all	HM_OutrType = 1,	!	Cross
																HM_OutrWidth = 5.500,
																HM_OutrDepth = 5.500,
																HM_OutrHeight = 1.250,
																mBeamW = 0.300,
																mBeamFlangeT = 0.028,
																mBeamWebT = 0.018,
																bPlate = 0
		del 1

	!	Mast
	!-----------------------------------------------------------------------------------------------
		_zPosMast = 1.306

		addz	_zPosMast
			!	Base Plate(Bottom)
			for	i = 1	to	4
				put	0.700,	0.700,	15,
					0.700,	0.240,	15,
					0.510,	0.240,	15,
					0.240,	0.510,	15,
					0.240,	0.700,	15

				prism_	nsp/3,	-0.056,		get(nsp)

				rotz	90
			next	i

			del 4

			!	Mast Paramaters
			_mastPlateWdt = 0.475
			_mastPlateThk = 0.060
			_mastColWdt = 0.250
			_mastColThk = 0.035
			_bInnerCol = 1

			_nMast1 = 2
			_nMast2 = 6

			_mastHgt1 = 4.500
			_mastHgt2 = 6.000

			_mastEntireHgt = _nMast1*_mastHgt1 + _nMast2*_mastHgt2

			!	Mast Type 1
			call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
														HM_MastNumber = _nMast1,
														HM_MastHeight = _mastHgt1,
														HM_PlateWdt = _mastPlateWdt,
														HM_PlateThk = _mastPlateThk,
														HM_ColWdt = _mastColWdt,
														HM_ColThk = _mastColThk,
														bInnerColumn = _bInnerCol

			addz	_nMast1*_mastHgt1
				call	"HM_mc_Mast.gsm"	parameters	all	HM_MastType = 2,
															HM_MastNumber = _nMast2,
															HM_MastHeight = _mastHgt2,
															HM_PlateWdt = _mastPlateWdt,
															HM_PlateThk = _mastPlateThk,
															HM_ColWdt = _mastColWdt,
															HM_ColThk = _mastColThk,
															bInnerColumn = _bInnerCol
			del 1
		del 1

	!	Upper Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		dim	_coorGR[][]
		_deckThk = 0.100

		!	1st
		_zPosDeck_1st = _zPosMast + _mastEntireHgt - 3.681

		addz	_zPosDeck_1st
			put	-1.500,	-1.500,	15,
				-1.500,	-1.150,	15,
				-2.220,	-1.150,	15,
				-2.220,	1.150,	15,
				-1.500,	1.150,	15,
				-1.500,	1.500,	15,
				-0.875,	1.500,	15,
				-0.875,	1.700,	15,
				-0.175,	1.700,	15,
				-0.175,	1.500,	15,
				1.500,	1.500,	15,
				1.500,	-1.500,	15,
				-1.500,	-1.500,	-1,
				!	Hole
				-1.000,	-1.000,	15,
				-1.000,	1.000,	15,
				1.000,	1.000,	15,
				1.000,	-1.000,	15,
				-1.000,	-1.000,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -1.470
				_coorGR[1][2] = -1.470

				_coorGR[2][1] = -1.470
				_coorGR[2][2] = -1.120

				_coorGR[3][1] = -2.190
				_coorGR[3][2] = -1.120

				_coorGR[4][1] = -2.190
				_coorGR[4][2] = 1.120

				_coorGR[5][1] = -1.470
				_coorGR[5][2] = 1.120

				_coorGR[6][1] = -1.470
				_coorGR[6][2] = 1.470

				_coorGR[7][1] = -0.845
				_coorGR[7][2] = 1.470

				_coorGR[8][1] = -0.845
				_coorGR[8][2] = 1.670

				_coorGR[9][1] = -0.205
				_coorGR[9][2] = 1.670

				_coorGR[10][1] = -0.205
				_coorGR[10][2] = 1.470

				_coorGR[11][1] = 1.470
				_coorGR[11][2] = 1.470

				_coorGR[12][1] = 1.470
				_coorGR[12][2] = -1.470

				_coorGR[13][1] = -1.470
				_coorGR[13][2] = -1.470

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 13,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

		!	2nd
		_zPosDeck_2nd = _zPosDeck_1st + 3.000

		addz	_zPosDeck_2nd
			put	-1.700,	-1.750,	15,
				-1.700,	-1.250,	15,
				-2.500,	-1.250,	15,
				-2.500,	1.250,	15,
				-1.700,	1.250,	15,
				-1.700,	1.750,	15,
				1.700,	1.750,	15,
				1.700,	-1.750,	15,
				-1.700,	-1.750,	-1,
				!	Hole
				-1.000,	-1.000,	15,
				-1.000,	1.000,	15,
				1.000,	1.000,	15,
				1.000,	-1.000,	15,
				-1.000,	-1.000,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			!	Guard Rail
			addz	_deckThk
				!	Insert Coordinates
				_coorGR[1][1] = -1.670
				_coorGR[1][2] = -1.720

				_coorGR[2][1] = -1.670
				_coorGR[2][2] = -1.220

				_coorGR[3][1] = -2.470
				_coorGR[3][2] = -1.220

				_coorGR[4][1] = -2.470
				_coorGR[4][2] = 1.220

				_coorGR[5][1] = -1.670
				_coorGR[5][2] = 1.220

				_coorGR[6][1] = -1.670
				_coorGR[6][2] = 1.720

				_coorGR[7][1] = 1.670
				_coorGR[7][2] = 1.720

				_coorGR[8][1] = 1.670
				_coorGR[8][2] = -1.720

				_coorGR[9][1] = -1.670
				_coorGR[9][2] = -1.720

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 9,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 2,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 1.000,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Connect to Main Object
	!-----------------------------------------------------------------------------------------------
		_zPosConnect = _zPosMast + _mastEntireHgt

		!	Cylinder
		addz	_zPosConnect
			cylind	1.212,	1.315

			! addz	1.300
			! 	cylind	0.050,	1.085
			! del 1
		del 1

		!	Main Floor
		addz	_zPosConnect + 1.212
			put	-3.240,	-1.175,	15,
				-3.240,	1.175,	15,
				1.275,	1.175,	15,
				1.275,	-1.175,	15

			prism_	nsp/3,	0.588,	get(nsp)
		del 1

		!	H-Beam
		! addz	_zPosConnect + 1.462 + 0.212
		! 	add	-2.950,	1.151,	0
		! 		rotz	180
		! 			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
		! 															HM_Beam_Width_EP = 0.200,
		! 															HM_Beam_Height_SP = 0.588,
		! 															HM_Beam_Height_EP = 0.588,
		! 															HM_Beam_Length = 2.515,
		! 															HM_Beam_FlangeT = 0.016,
		! 															HM_Beam_WebT = 0.012,
		! 															HM_matBeam = HM_matBody2
		! 		del 1
		! 	del 1

		! 	add	-2.950,	-1.151,	0
		! 		rotz	180
		! 			call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.200,
		! 															HM_Beam_Width_EP = 0.200,
		! 															HM_Beam_Height_SP = 0.588,
		! 															HM_Beam_Height_EP = 0.588,
		! 															HM_Beam_Length = 2.515,
		! 															HM_Beam_FlangeT = 0.016,
		! 															HM_Beam_WebT = 0.012,
		! 															HM_matBeam = HM_matBody2
		! 		del 1
		! 	del 1

		! 	addy	-1.051
		! 		addx	-3.100
		! 			gosub	"draw_Connect_Beam_H"
		! 		del 1
		! 		addx	-4.405
		! 			gosub	"draw_Connect_Beam_H"
		! 		del 1
		! 		addx	-5.315
		! 			gosub	"draw_Connect_Beam_H"
		! 		del 1
		! 	del 1
		! del 1

	!	Main Deck
	!-----------------------------------------------------------------------------------------------
		!	Parameters
		_deckThk = 0.100

		_zPosMainDeck = _zPosConnect + 1.700

		addz	_zPosMainDeck
			resol	36

			put	-3.310,	-2.800,	15,
				-3.600,	-2.950,	15,
				-4.160,	-2.950,	15,
				0,		0,		900,
				0,		-56.60,	4001,
				-4.752,	1.850,	15,
				-4.745,	1.850,	15,
				-4.600,	2.200,	15,
				-3.005,	2.200,	15,
				-2.860,	1.850,	15,
				0.880,	1.850,	15,
				1.500,	1.390,	15,
				1.500,	-2.800,	15,
				-3.310,	-2.800,	-1,
				!	Hole
				-3.260,	-1.190,	15,
				-3.260,	1.190,	15,
				1.275,	1.190,	15,
				1.275,	-1.190,	15,
				-3.260,	-1.190,	-1

			prism_	nsp/3,	_deckThk,	get(nsp)

			resol	HM_3D_resol

			!	Guard Rail
			addz	_deckThk
				_coorGR[1][1] = -0.230
				_coorGR[1][2] = -2.770

				_coorGR[2][1] = -3.317
				_coorGR[2][2] = -2.770

				_coorGR[3][1] = -3.607
				_coorGR[3][2] = -2.920

				_coorGR[4][1] = -4.144
				_coorGR[4][2] = -2.920

				_coorGR[5][1] = -4.729
				_coorGR[5][2] = -1.826

				_coorGR[6][1] = -5.031
				_coorGR[6][2] = -0.623

				_coorGR[7][1] = -5.031
				_coorGR[7][2] = 0.616

				_coorGR[8][1] = -4.732
				_coorGR[8][2] = 1.820

				_coorGR[9][1] = -4.579
				_coorGR[9][2] = 2.170

				_coorGR[10][1] = -3.025
				_coorGR[10][2] = 2.170

				_coorGR[11][1] = -2.880
				_coorGR[11][2] = 1.820

				_coorGR[12][1] = 0.870
				_coorGR[12][2] = 1.820

				_coorGR[13][1] = 1.470
				_coorGR[13][2] = 1.375

				_coorGR[14][1] = 1.470
				_coorGR[14][2] = -1.570

				call	"HM_mc_GuardRail_Line.gsm"	parameters	all	HM_GRail_PtNum = 14,
																	HM_GRail_Pt = _coorGR,
																	HM_GRailType = 1,
																	HM_GRailRef = 1,
																	HM_GRailH = 1.000 + 0.025/2,
																	HM_GRailD = 0.025,
																	HM_GRailT = 0.025,
																	HM_GRailDist = 6.820/5,
																	HM_GRailDist_hori = 0.450,
																	HM_matGRail = HM_matBody2
			del 1
		del 1

	!	Machinery
	!-----------------------------------------------------------------------------------------------
		_zPosMachine = _zPosConnect + 1.800

		addz	_zPosMachine
			!	U
			! add	-3.220,	-0.520,	0
			! 	block	0.640,	1.389,	0.227
			! del 1

			add	-3.800,	-0.730,	0.597
				call	"HM_mc_Pulley.gsm"	parameters	all		HM_Pulley_Width = 0.439,
																HM_Pulley_R1 = 0.529,
																HM_Pulley_R2 = 0.375,
																HM_Pulley_Thick = 0.025,
																HM_Pulley_Fixed = 0,
																HM_matPulley = HM_matBody2
			del 1

			add	-3.700,	1.066,	0.470
				call	"HM_mc_Pulley.gsm"	parameters	all		HM_Pulley_Width = 0.850,
																HM_Pulley_R1 = 0.312,
																HM_Pulley_R2 = 0.290,
																HM_Pulley_Thick = 0.025,
																HM_Pulley_Fixed = 0,
																HM_matPulley = HM_matBody2
			del 1
		del 1

	!	Operator Cap
	!-----------------------------------------------------------------------------------------------
		_zPosOperCap = _zPosConnect + 1.800

		addz	_zPosOperCap
			add	-0.200,	-2.800,	0
				call	"HM_mc_OperatorCab.gsm"	parameters	all	HM_OP_Type = 2,
																HM_OP_Width = 1.200,
																HM_OP_Depth_bot = 1.700,
																HM_OP_Depth_roo = 1.550,
																HM_OP_Height = 2.100
			del 1
		del 1

	!	Cat Head
	!-----------------------------------------------------------------------------------------------
		_zPosCatHead = _zPosConnect + 2.000

		addz	_zPosCatHead
			addx	-3.000
				call	"HM_mc_CatHead.gsm"	parameters	all	HM_CH_Type = 1,
															HM_CH_Width = 4.050,
															HM_CH_Depth = 2.325,
															HM_CH_Height = 10.350,
															HM_CH_Radius1 = 0.108,
															HM_CH_Radius2 = 0.160,
															HM_CH_Topnt_posx = -0.350,
															!	Deck1
															HM_CH_Deck = 1,
															HM_CH_DeckW = 1.800,
															HM_CH_DeckD = 3.500,
															HM_CH_DeckT = 0.150,
															HM_CH_Deck_HoleW = 1.250,
															HM_CH_Deck_HoleD = 2.800,
															HM_CH_Deck_posx = 0.950,
															HM_CH_Deck_posz = 9.080,
															!	Pulley
															HM_CH_Pulley_R1 = 0.250,
															HM_CH_Pulley_R2 = 0.200
			del 1
		del 1

	!	Jib
	!-----------------------------------------------------------------------------------------------
		_zPosJib = _zPosConnect + 2.050

		if	HM_Jib_flag	then
			addz	_zPosJib
				addx	-2.600
					!	Jib
					call	"HM_mc_Jib.gsm"	parameters	all	HM_Jib_type = 1

					roty	-HM_JibAng
						addx	HM_JibLength - _mJib_pulleyR
							!	Jib Pulley
							rotx	90
								addz	-1.030
									cylind	1.030*2,	0.080
								del 1
							del 1

							for	i = 1	to	2
								muly	-2*i + 3
									addy	0.810
										call	"HM_mc_Pulley.gsm"	parameters	all	HM_Pulley_Width = 0.183,
																					HM_Pulley_R1 = 0.2685,
																					HM_Pulley_R2 = 0.2485,
																					HM_Pulley_Thick = 0.009,
																					HM_matPulley = HM_matBody2,
																					HM_Pulley_Fixed = 0
									del 1
								del 1
							next	i
						del 1
					del 1
				del 1
			del 1
		endif

	!	Wire
	!-----------------------------------------------------------------------------------------------
		_zPosWire = _zPosMachine

		addz	_zPosWire
			!	Mirror
			for	i = 1	to	2
				muly	(2*i - 3)
					!	Deck → Cat Head
					!-----------------------------------------------------------------------------------------------
					!	Machine
					if	i = 1	then	_y1 = 0.730
					if	i = 2	then	_y1 = -0.730
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(3.800 - 0.375),
																x2 = -(3.350 + 0.159),
																y1 = _y1,
																y2 = -2.325/2,
																z1 = 0.597,
																z2 = 10.550

					if	i = 1	then	_y1 = -1.066
					if	i = 2	then	_y1 = 1.066
					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -(3.700 - 0.290),
																x2 = -(3.350 + 0.159),
																y1 = _y1,
																y2 = -2.325/2,
																z1 = 0.470,
																z2 = 10.550

					!	Cat Head → Jib
					!-----------------------------------------------------------------------------------------------
					_xPosJibPulley = -2.600 + (HM_JibLength - _mJib_pulleyR)*cos(HM_JibAng)
					_zPosJibPulley = 0.250 + (HM_JibLength - _mJib_pulleyR)*sin(HM_JibAng) + 0.2485

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = -3.350,
																x2 = _xPosJibPulley,
																y1 = -2.325/2,
																y2 = -0.810,
																z1 = 10.550 + 0.200,
																z2 = _zPosJibPulley

					_xPosJibPulley = -2.600 + (HM_JibLength - _mJib_pulleyR)*cos(HM_JibAng)
					_zPosJibPulley = 0.250 + (HM_JibLength - _mJib_pulleyR)*sin(HM_JibAng)

					call	"HM_mc_WirePulley.gsm"	parameters	all	HM_WP_startptX1 = -3.350,
																	HM_WP_startptZ1 = (10.550 - 0.200),
																	HM_WP_startptX2 = -3.350,
																	HM_WP_startptZ2 = (10.550 + 0.200),
																	HM_WP_startptY = -2.325/2,
																	HM_WP_endptX = _xPosJibPulley,
																	HM_WP_endptZ = _zPosJibPulley,
																	HM_WP_endptY = -0.960,
																	HM_WP_posPulley_ratio = 0.35

					!	Jib → Hook
					!-----------------------------------------------------------------------------------------------
					_zPosJibPulley = 0.250 + HM_JibLength*sin(HM_JibAng)

					call	"HM_mc_Wire.gsm"	parameters	all	x1 = HM_WorkingR,
																x2 = HM_WorkingR,
																y1 = -0.810,
																y2 = -HM_HookRadius,
																z1 = _zPosJibPulley,
																z2 = _zPosJibPulley - HM_JibWireLen + HM_HookHeight
				del 1
			next	i
		del 1

	!	Hook
	!-----------------------------------------------------------------------------------------------
		_zPosHook = _zPosJib + HM_JibLength*sin(HM_JibAng)

		add	HM_WorkingR,	0,	_zPosHook
			hotspot	0,	0,	0,				unID,	HM_JibWireLen,	1+128	:	unID = unID + 1
			hotspot	0,	0,	1,				unID,	HM_JibWireLen,	3		:	unID = unID + 1
			hotspot	0,	0,	-HM_JibWireLen,	unID,	HM_JibWireLen,	2		:	unID = unID + 1

			addz	-HM_JibWireLen
				call	"HM_mc_Hook.gsm"	parameters	all	HM_HookThick = 0.160

			del 1
		del	1

	!	Suspended Load
	!----------------------------------------------------------------------------------------------------
		_zPosLoad = _zPosHook - HM_JibWireLen

		if	HM_SLshow	then
			add	HM_WorkingR,	0,	_zPosLoad
				hotspot	0,	0,	0,				unID,	HM_SLwireLen,	1	: unID = unID + 1
				hotspot	0,	0,	-HM_SLwireLen,	unID,	HM_SLwireLen,	2	: unID = unID + 1
				hotspot	0,	0,	1,				unID,	HM_SLwireLen,	3	: unID = unID + 1

				addz	-HM_SLwireLen
					rotz	90
						call	"HM_mc_Suspendedloads.gsm"	parameters	all
					del	1
				del	1
			del	1
		endif

	!	Load Text
	!----------------------------------------------------------------------------------------------------
		_zPosText = _zPosLoad - HM_SLwireLen

		if	HM_3D_text_show_load	then
			!	On the Load 
			if	not(GLOB_FEEDBACK_MODE) 	then
				add	HM_WorkingR + HM_SLdepth/2,	0,	_zPosText
					rotz	90
						call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 1,
																		HM_3D_text_show_plane = 0
					del	1
				del	1
			endif
		endif

		if	HM_3D_text_show_plane	then
			!	On the Bottom
			call	"HM_mc_LoadText.gsm"	parameters all	HM_3D_text_show_load = 0,
															HM_3D_text_show_plane = 1
		endif
del 1



!----------------------------------------------------------------------------------------------------
!	Dimension
!----------------------------------------------------------------------------------------------------

	!	Working Radius
	!----------------------------------------------------------------------------------------------------
	call	"HM_mc_WorkingR.gsm"	parameters all




end



!	Connect Beam Horizontal
!-----------------------------------------------------------------------------------------------
"draw_Connect_Beam_H":

	rotz	90
		call	"HM_mc_H-Beam.gsm"	parameters	all		HM_Beam_Width_SP = 0.300,
														HM_Beam_Width_EP = 0.300,
														HM_Beam_Height_SP = 0.588,
														HM_Beam_Height_EP = 0.588,
														HM_Beam_Length = 2.102,
														HM_Beam_FlangeT = 0.016,
														HM_Beam_WebT = 0.012,
														HM_matBeam = HM_matBody2
	del 1

return