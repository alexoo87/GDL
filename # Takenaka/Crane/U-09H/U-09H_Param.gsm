!======================================================================================================
!	TAKENAKA CRANE LIBRARY 
!======================================================================================================
!	VER : AC 20


!----------------------------------------------------------------------------------------------------
!	Macro
!----------------------------------------------------------------------------------------------------
!	Global
call	"HM_Global.gsm"	parameters	all

!	Rated Load
call	"HM_mc_RatedLoad.gsm"	parameters all	returned_parameters	_WorkingR_max,	_WorkingR_min

!	Suspended Load 
call	"HM_mc_Suspendedloads.gsm"	parameters	all


!----------------------------------------------------------------------------------------------------
!	Calculation
!----------------------------------------------------------------------------------------------------
_xPosJib = 0.800
_PulleyRad = 0.1025

!	Jib Angle, Jib Height, Working Radius
if	GLOB_MODPAR_NAME = "HM_JibHeight"	then
	HM_jibAng = asn(HM_JibHeight/HM_JibLength)
	HM_WorkingR = HM_JibLength*cos(HM_jibAng) + _xPosJib + _PulleyRad
else
	if	GLOB_MODPAR_NAME = "HM_JibAng"	then
		HM_JibHeight = HM_JibLength*sin(HM_jibAng)
		HM_WorkingR = HM_JibLength*cos(HM_jibAng) + _xPosJib + _PulleyRad
	else
		HM_jibAng = acs((HM_WorkingR - (_xPosJib + _PulleyRad))/HM_JibLength)
		HM_JibHeight = HM_JibLength*sin(HM_jibAng)
	endif
endif


!----------------------------------------------------------------------------------------------------
!	Define Values
!----------------------------------------------------------------------------------------------------



!----------------------------------------------------------------------------------------------------
!	Define Range
!----------------------------------------------------------------------------------------------------

	!	Jib Angle
	!----------------------------------------------------------------------------------------------------
	_JibAng_min = acs((_WorkingR_max - (_xPosJib + _PulleyRad))/HM_JibLength)
	_JibAng_max = acs((_WorkingR_min - (_xPosJib + _PulleyRad))/HM_JibLength)
	values	"HM_JibAng"	range [_JibAng_min, _JibAng_max]

	!	Jib Height
	!----------------------------------------------------------------------------------------------------
	_mJibHeight_min = HM_JibLength*sin(_JibAng_min)
	_mJibHeight_max = HM_JibLength*sin(_JibAng_max)
	values	"HM_JibHeight"	range [_mJibHeight_min, _mJibHeight_max]




!----------------------------------------------------------------------------------------------------
!	Insert Value
!----------------------------------------------------------------------------------------------------
!	Crane Information
parameters 	HM_CraneName = HM_CraneName,
			HM_CraneMaker = HM_CraneMaker 

!	Crane Size
parameters 	HM_CraneWidth = HM_CraneWidth,
			HM_CraneDepth = HM_CraneDepth,
			HM_CraneHeight = HM_CraneHeight

!	Jib Length
parameters	HM_JibLength = HM_JibLength


!	Jib Angle, Jib Height, Working Radius
if	GLOB_MODPAR_NAME = "HM_JibHeight" 	then
	parameters	HM_WorkingR = HM_WorkingR
	parameters	HM_jibAng = HM_jibAng
else
	if	GLOB_MODPAR_NAME = "HM_JibAng"	then
		parameters	HM_JibHeight = HM_JibHeight
		parameters	HM_WorkingR = HM_WorkingR
	else
		parameters	HM_JibHeight = HM_JibHeight
		parameters	HM_jibAng = HM_jibAng
	endif
endif

!	Outrigger Width
parameters	HM_OutrWidth = HM_OutrLen


!----------------------------------------------------------------------------------------------------
!	Parameter Hide/Lock
!----------------------------------------------------------------------------------------------------
!	Crane Size
lock	"HM_CraneWidth", "HM_CraneDepth", "HM_CraneHeight", "HM_CraneWeight"

!	Hide Title
hideparameter	"HM_Boom", "HM_Mast"

!	Judgement Color
lock	"HM_Judgement"

!	Hook Size
lock	"HM_HookRadius", "HM_HookHeight"